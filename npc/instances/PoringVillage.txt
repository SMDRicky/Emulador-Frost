firstcity,199,267,5	script	Veggie Enchanter#0	4_F_02,2,2,{
	mes "[Encantador Vegetariano]";
	mes "50 Jellopy e 20.000 zenys são tudo que você precisa para obter um efeito especial em sua cebola verde ou cenoura!";
	next;
	mes "[Encantador Vegetariano]";
	mes "Encantar e redefinir têm ^ff000030% de falha.^000000 Invista no seu veggie e na sorte!";
	next;
	switch( select( "Quanto preciso encantar?", "Encantar o Poring Village Veggie", "Redefinir Poring Village Vegetariano" ) ) {
	case 1:
		mes "[Encantador Vegetariano]";
		mes "Como ouvi, você pode conseguir Cebola Verde ou Cenoura de Poring Village se for um pouco mais para o oeste a partir daqui.";
		next;
		mes "[Encantador Vegetariano]";
		mes "Eu encanto com o vegetal fresco.";
		next;
		mes "[Encantador Vegetariano]";
		mes "Encantar ou Reiniciar requer 20.000z e 50 Jellopy! ";
		next;
		mes "[Encantador Vegetariano]";
		mes "Bem... Como você sabe, é um vegetal de qualquer maneira. ";
		close;
	case 2:
		.@equip_id = getequipid(EQI_HEAD_LOW);

		switch( .@equip_id ) {
		case -1:
			mes "[Encantador Vegetariano]";
			mes "Por favor, coloque o equipamento que você deseja encantar primeiro.";
			close;
		case 19238:	// PoringTownOnion
		case 19239:	// PoringTownCarrot
			break;
		default:
			mes "[Encantador Vegetariano]";
			mes "Isso não é um vegetal!?";
			close;
		}
		if (getequipcardid(EQI_HEAD_LOW,3) > 0) {
			mes "[Encantador Vegetariano]";
			mes "Este vegetal não pode mais ser encantado.";
			close;
		}
		if (countitem(909) < 50 || Zeny < 20000) {
			mes "[Encantador Vegetariano]";
			mes "Este encantamento requer 50 Jellopy e 20.000z..... Mas... você não os tem.";
			close;
		}
		delequip EQI_HEAD_LOW;
		delitem 909,50;	// Jellopy
		Zeny -= 20000;

		if (rand(100) < 70) {
			.@r = rand(5356);
			if (.@r < 160) .@card = 4795;	// HP100
			else if (.@r < 200) .@card = 4796;	// HP200
			else if (.@r < 1000) .@card = 4928;	// SP10
			else if (.@r < 1400) .@card = 4870;	// SP25
			else if (.@r < 1480) .@card = 4800;	// SP50
			else if (.@r < 1880) .@card = 4700;	// Strength1
			else if (.@r < 2280) .@card = 4730;	// Agility1
			else if (.@r < 3410) .@card = 4740;	// Vitality1
			else if (.@r < 3810) .@card = 4710;	// Inteligence1
			else if (.@r < 4210) .@card = 4720;	// Dexterity1
			else if (.@r < 5340) .@card = 4750;	// Luck1
			else if (.@r < 5348) .@card = 4992;	// HPdrain1
			else if (.@r < 5356) .@card = 4993;	// SPdrain1

			getitem2 .@equip_id,1,1,0,0,0,0,0,.@card;
			mes "[Encantador Vegetariano]";
			mes "Tada! ";
			specialeffect2 EF_REPAIRWEAPON;
			close;
		}
		mes "[Encantador Vegetariano]";
		mes "Deve ter sido um vegetal podre. ";
		specialeffect2 EF_LORD;
		close;
	case 3:
		.@equip_id = getequipid(EQI_HEAD_LOW);

		switch( .@equip_id ) {
		case -1:
			mes "[Encantador Vegetariano]";
			mes "Por favor, coloque o equipamento que você deseja encantar primeiro.";
			close;
		case 19238:	// PoringTownOnion
		case 19239:	// PoringTownCarrot
			break;
		default:
			mes "[Encantador Vegetariano]";
			mes "Isso não é um vegetal!?";
			close;
		}
		if (getequipcardid(EQI_HEAD_LOW,3) == 0) {
			mes "[Encantador Vegetariano]";
			mes "Este é um vegetal que nunca foi fortificado...";
			close;
		}
		if (countitem(909) < 50 || Zeny < 20000) {
			mes "[Encantador Vegetariano]";
			mes "Este encantamento requer 50 Jellopy e 20.000z..... Mas... você não os tem.";
			close;
		}
		delequip EQI_HEAD_LOW;
		delitem 909,50;	// Jellopy
		Zeny -= 20000;

		if (rand(100) < 70) {
			getitem .@equip_id,1;
			mes "[Encantador Vegetariano]";
			mes "A matéria está completa! ";
			specialeffect2 EF_REPAIRWEAPON;
			close;
		}
		mes "[Encantador Vegetariano]";
		mes "Deve ter sido um vegetal podre. ";
		specialeffect2 EF_LORD;
		close;
	}
	end;

OnTouch:
	npctalk "Veggie Enchanter: Deixe sua cebola verde ou cenoura mais forte!", "", bc_self;
	end;
}
// Instance
firstcity,201,264,5	script	Emily#0	4_GEFFEN_03,4,4,{
	if (BaseLevel < 5) {
		mes "[Emily]";
		mes "Uau~ Você parece uma novata. Quero aventurar-me contigo um dia, quando cresceres!";
		close;
	}
	if (BaseLevel > 99) {
		mes "[Emily]";
		mes "Ah, agora é mais forte do que eu. Como cresceu tão rápido?";
		next;
		mes "[Emily]";
		mes "Devo encontrar outro aventureiro que seja semelhante ao meu nível. Foi um prazer conhecê-la.";
		close;
	}
	if (isbegin_quest(12416) == 0) {
		mes "[Emily]";
		mes "O meu nome é Emily. Estava à espera de alguém que fosse em aventuras comigo!";
		next;
		select("Quem disse que vou me aventurar com o senhor?");
		mes "[Emily]";
		mes "Bem, vamos lá. Não seja severo. Ouça, você pode decidir depois de ouvir a minha história. Claro, posso te dar uma recompensa adequada.";
		next;
		if (select( "Recompensa Adequada?", "Não estou interessado." ) == 2) {
			mes "[Emily]";
			mes "Que quadrado! É muito melhor estarmos juntos!";
			close;
		}
		mes "[Emily]";
		mes "Sim! Recompensa Adequada.";
		next;
		mes "[Emily]";
		mes "Parece que esta Aldeia Porosa pode ser derrotada por iniciantes como nós. Mas é só um palpite...";
		next;
		mes "[Emily]";
		mes "Bem, na verdade, não posso te dar a recompensa oficialmente através da associação de aventureiros. Mas podemos separar a renda depois de caçar.";
		next;
		select("Mas 50-50 parece justo para mim.");
		mes "[Emily]";
		mes "De jeito nenhum. 70-30. E eu tenho 70, é claro.";
		next;
		select("Tem que ser 50-50 ou não é bom");
		mes "[Emily]";
		mes "Que tal 60-40!!";
		next;
		select("É meio a meio");
		mes "[Emily]";
		mes "Isso enlouquece-me. Ok, 50-50. Vamos com isso.";
		setquest 12416;	// Contract with Emily
		completequest 12416;
		close;
	}
	switch( checkquest(12417,PLAYTIME) ) {
	case -1:
		break;
	case 0:
	case 1:
		mes "^ff0000Qualquer processamento de monstros, como domar monstros dentro da masmorra, não será considerado um progresso normal. Observe que.^000000";
		next;
		mes "^ff0000No modo iniciante, o jogador se transforma em um monstro. Observe que o efeito de transformação existente desaparece ao entrar e prosseguir.^000000";
		next;
		mes "[Emily]";
		mes "Estão prontos?";
		next;
		mes "[Emily]";
		mes "Você parece bastante cansado.";
		next;
		mes "[Emily]";
		mes "Por que não volta depois de um tempo? Faça uma pausa por enquanto.";
		close;
	case 2:
		mes "^ff0000Qualquer processamento de monstros, como domar monstros dentro da masmorra, não será considerado um progresso normal. Observe que.^000000";
		next;
		mes "^ff0000No modo iniciante, o jogador se transforma em um monstro. Observe que o efeito de transformação existente desaparece ao entrar e prosseguir. ^000000";
		next;
		mes "[Emily]";
		mes "Estão prontos?";
		next;
		mes "^0000ffTodos os vestígios da missão Poring Village foram removidos. Agora, fale com Emily novamente para inserir^000000";
		erasequest 12417;
		close;
	}
	mes "^ff0000Qualquer processamento de monstros, como domar monstros dentro da masmorra, não será considerado um progresso normal. Observe que.^000000";
	next;
	mes "^ff0000No modo iniciante, o jogador se transforma em um monstro. Observe que o efeito de transformação existente desaparece ao entrar e prosseguir.^000000";
	next;
	mes "[Emily]";
	mes "Estão prontos?";
	if (isbegin_quest(12418) == 0)
		setquest 12418;	// First Visit to Poring Village
	next;
	if (getcharid(1) < 1) {
		mes "[Emily]";
		mes "Hmm, você deve criar um grupo primeiro para começar esta grande aventura.";
		close;
	}
	.@md_name$ = "Poring Village";

	if (is_party_leader())
		.@menu$[0] = "Criar a entrada";
	if (instance_live_info(ILI_NAME, instance_id(IM_PARTY)) == .@md_name$)
		.@menu$[1] = "Entre na masmorra";

	switch( select( .@menu$[0], .@menu$[1], "Cancelar" ) ) {
	case 1:
		mes "[Emily]";
		mes "Agora estou procurando a entrada, por favor, fale comigo novamente depois que estiver totalmente aberta.";
		instance_create(.@md_name$);
		close;
	case 2:
		switch( instance_enter(.@md_name$) ) {
		case IE_OTHER:
			mes "Ocorreu um erro desconhecido.";
			close;
		case IE_NOINSTANCE:
			mes "Arachadura de ^ ff0000Time ainda não foi criada.^000000";
			close;
		case IE_NOMEMBER:
			end;
		case IE_OK:
			mapannounce "prt_fild05", "Membro do grupo" + strcharinfo(0) + "da parte" + getpartyname(getcharid(0)) + "insere" + .@md_name$ + ".", bc_map,0xFF9900;
			setquest 12417;	// Overcoming fatigue
			// warp "1@begi",100,22;
			end;
		}
		end;
	case 3:
		end;
	}
	end;

OnTouch:
	if (isbegin_quest(12416) == 0 && BaseLevel >= 30 && BaseLevel <= 60)
		npctalk "Emily: Olá! Posso falar contigo um minuto?";
	end;

OnInit:
	questinfo( QTYPE_JUMPING_PORING, QMARK_YELLOW , "isbegin_quest(12416) == 0 && BaseLevel >= 30 && BaseLevel <= 60" );
	end;
}

1@begi,106,30,0	script	#start_wp1_0	HIDDEN_WARP_NPC,4,4,{
	end;
OnTouch:
	disablenpc();
	donpcevent instance_npcname("#start_wp1_1") + "::OnStart";
	end;

OnInstanceInit:
	'map_begi$ = instance_mapname("1@begi");

	setcell 'map_begi$,146,35,146,41,cell_walkable,0;
	setcell 'map_begi$,154,100,154,107,cell_walkable,0;
	setcell 'map_begi$,59,187,59,197,cell_walkable,0;

	disablenpc instance_npcname("#start_wp1_2");
	disablenpc instance_npcname("#gift");
	end;
}

1@begi,106,30,5	script	#start_wp1_1	4_GEFFEN_03,{
	mes "[Emily]";
	mes "Vamos em frente!";
	close;

OnStart:
	npctalk "Emily: Então... isso é Poring Village?";
	sleep 2000;
	npctalk "Emily: Ok. Vamos lá!";
	sleep 2000;
	disablenpc();
	donpcevent instance_npcname("#start_wp1_2") + "::OnStart";
	donpcevent instance_npcname("poring_village_1") + "::OnStart";
	end;
}

1@begi,140,37,5	script	#start_wp1_2	4_GEFFEN_03,{
	mes "[Emily]";
	mes "Vou começar devagar agora...";
	close;

OnStart:
	enablenpc();
	sleep 1000;
	npctalk "Emily: Hmmm. Parece que eles fizeram barreiras para não nos deixar ir facilmente.";
	sleep 3000;
	npctalk "Emily: Tenho certeza de que Poring reuniu muitos tesouros em algum lugar.";
	sleep 4000;
	mapannounce 'map_begi$, "???: Posso ouvir a voz de um humano. Façam com que sejam bem-vindos, pessoal.", bc_map, 0xFFFF00;
	sleep 4000;
	npctalk "Emily: O que está escrito?";
	sleep 3000;
	npctalk "Emily: Parece que alguém está com pressa aqui...";
	sleep 2500;
	mapannounce 'map_begi$, "???: Não os deixe correr! Pegue todos os itens valiosos deles!", bc_map, 0xFFFF00;
	sleep 2500;
	npctalk "Emily: Bem... OK. Eles não parecem muito amigáveis. Rápido, esconda-se!";
	sleep 4000;
	// disablenpc();
	hideonnpc();	// hideonnpc instead of disablenpc (official) to display text on rAthena
	sleep 1000;
	npctalk "Emily: Oooops. Você não consegue se esconder? Bem... Anime-se!";
	// disablenpc instance_npcname("#poring_wl18");
	hideonnpc instance_npcname("#poring_wl18");
	specialeffect EF_LORD,AREA, instance_npcname("#poring_wl18");
	sleep 500;
	// disablenpc instance_npcname("#poring_wl19");
	hideonnpc instance_npcname("#poring_wl19");
	specialeffect EF_LORD,AREA, instance_npcname("#poring_wl19");
	sleep 500;
	disablenpc instance_npcname("#poring_wl15");
	sleep 500;
	disablenpc instance_npcname("#poring_wl16");
	disablenpc instance_npcname("#poring_wl17");
	disablenpc instance_npcname("#poring_wl20");
	disablenpc instance_npcname("#poring_wl21");
	setcell 'map_begi$,146,35,146,41,cell_walkable,1;
	end;
}

-	script	dummy_npc	-1,{
	end;
}


// First wall
1@begi,146,35,6	duplicate(dummy_npc)	#poring_wl15	4_ROPEPILE,1,0
1@begi,146,36,6	duplicate(dummy_npc)	#poring_wl16	4_ROPEPILE,1,0
1@begi,146,37,6	duplicate(dummy_npc)	#poring_wl17	4_ROPEPILE,1,0
1@begi,146,38,6	duplicate(dummy_npc)	#poring_wl18	4_ROPEPILE,1,0
1@begi,146,39,6	duplicate(dummy_npc)	#poring_wl19	4_ROPEPILE,1,0
1@begi,146,40,6	duplicate(dummy_npc)	#poring_wl20	4_ROPEPILE,1,0
1@begi,146,41,6	duplicate(dummy_npc)	#poring_wl21	4_ROPEPILE,1,0

// Second wall
1@begi,59,187,6	duplicate(dummy_npc)	#poring_wl9	4_ROPEPILE,1,0
1@begi,59,188,6	duplicate(dummy_npc)	#poring_wl10	4_ROPEPILE,1,0
1@begi,59,189,6	duplicate(dummy_npc)	#poring_wl11	4_ROPEPILE,1,0
1@begi,59,190,6	duplicate(dummy_npc)	#poring_wl12	4_ROPEPILE,1,0
1@begi,59,191,6	duplicate(dummy_npc)	#poring_wl13	4_ROPEPILE,1,0
1@begi,59,192,6	duplicate(dummy_npc)	#poring_wl14	4_ROPEPILE,1,0

// Others
1@begi,154,100,6	duplicate(dummy_npc)	#poring_wl1	4_ROPEPILE,1,0
1@begi,154,101,6	duplicate(dummy_npc)	#poring_wl2	4_ROPEPILE,1,0
1@begi,154,102,6	duplicate(dummy_npc)	#poring_wl3	4_ROPEPILE,1,0
1@begi,154,103,6	duplicate(dummy_npc)	#poring_wl4	4_ROPEPILE,1,0
1@begi,154,104,6	duplicate(dummy_npc)	#poring_wl5	4_ROPEPILE,1,0
1@begi,154,105,6	duplicate(dummy_npc)	#poring_wl6	4_ROPEPILE,1,0
1@begi,154,106,6	duplicate(dummy_npc)	#poring_wl7	4_ROPEPILE,1,0
1@begi,154,107,6	duplicate(dummy_npc)	#poring_wl8	4_ROPEPILE,1,0


1@begi,1,1,5	script	poring_village_1	-1,{
	end;
OnStart:
	.@event$ = instance_npcname("poring_village_1") + "::OnMobDead";

	areamonster 'map_begi$, 153,32, 171,32, "--en--", "MD_DROPS",7, .@event$;
	areamonster 'map_begi$, 48,54, 71,57, "--en--", "MD_DROPS",8, .@event$;
	areamonster 'map_begi$, 170,38, 189,38, "--en--", "MD_POPORING",8, .@event$;
	areamonster 'map_begi$, 24,72, 28,92, "--en--", "MD_POPORING",8, .@event$;
	areamonster 'map_begi$, 188,42, 191,58, "--en--", "MD_MARIN",8, .@event$;
	areamonster 'map_begi$, 158,67, 174,68, "--en--", "MD_MARIN",3, .@event$;
	areamonster 'map_begi$, 117,68, 131,68, "--en--", "MD_MARIN",4, .@event$;
	areamonster 'map_begi$, 79,113, 92,113, "--en--", "MD_MARIN",6, .@event$;

	monster 'map_begi$,101,66, "--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,57,113, "--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,68,113, "--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,176,66, "--en--","MD_PORING",1, .@event$;
	monster 'map_begi$,158,68, "--en--","MD_PORING",1, .@event$;
	monster 'map_begi$,157,67, "--en--","MD_PORING",1, .@event$;
	monster 'map_begi$,149,66, "--en--","MD_PORING",1, .@event$;
	monster 'map_begi$,125,67, "--en--","MD_PORING",1, .@event$;
	monster 'map_begi$,121,67, "--en--","MD_PORING",1, .@event$;
	monster 'map_begi$,102,67, "--en--","MD_PORING",1, .@event$;
	monster 'map_begi$,101,68, "--en--","MD_PORING",1, .@event$;
	end;
OnMobDead:
	if (mobcount( 'map_begi$, instance_npcname("poring_village_1") + "::OnMobDead" ) > 3)
		end;
	killmonster 'map_begi$, instance_npcname("poring_village_1") + "::OnMobDead";
	mapannounce 'map_begi$, "Emily: Uau ~ Você é muito forte! Você derrotou todos esses Porings!", bc_map, 0xFFFF00;
	sleep 2000;
	mapannounce 'map_begi$, "Emily: Mas o Poring parece único de alguma forma. Tome cuidado!", bc_map, 0xFFFF00;
	sleep 2000;
	mapannounce 'map_begi$, "Emily: Ah, existe uma força única se você chegar perto da coluna de luz azul.", bc_map, 0xFFFF00;
	sleep 2000;
	monster 'map_begi$,132,103,"--en--","MD_GOLDRING",1, instance_npcname("poring_village_1") + "::OnBossDead";
	end;
OnBossDead:
	if (mobcount( 'map_begi$, instance_npcname("poring_village_1") + "::OnBossDead" ) > 0)
		end;
	killmonster 'map_begi$, instance_npcname("poring_village_1") + "::OnBossDead";
	donpcevent instance_npcname("poring_village_2") + "::OnStart";
	sleep 6000;
	// disablenpc instance_npcname("#poring_wl4");
	hideonnpc instance_npcname("#poring_wl4");
	specialeffect EF_LORD, AREA, instance_npcname("#poring_wl4");
	specialeffect EF_LORD, AREA, instance_npcname("#poring_wl4");
	specialeffect EF_LORD, AREA, instance_npcname("#poring_wl4");
	specialeffect EF_LORD, AREA, instance_npcname("#poring_wl4");
	specialeffect EF_LORD, AREA, instance_npcname("#poring_wl4");
	specialeffect EF_LORD, AREA, instance_npcname("#poring_wl4");
	specialeffect EF_LORD, AREA, instance_npcname("#poring_wl4");
	specialeffect EF_LORD, AREA, instance_npcname("#poring_wl4");
	// disablenpc instance_npcname("#poring_wl5");
	hideonnpc instance_npcname("#poring_wl5");
	specialeffect EF_LORD, AREA, instance_npcname("#poring_wl5");
	specialeffect EF_LORD, AREA, instance_npcname("#poring_wl5");
	specialeffect EF_LORD, AREA, instance_npcname("#poring_wl5");
	specialeffect EF_LORD, AREA, instance_npcname("#poring_wl5");
	specialeffect EF_LORD, AREA, instance_npcname("#poring_wl5");
	specialeffect EF_LORD, AREA, instance_npcname("#poring_wl5");
	specialeffect EF_LORD, AREA, instance_npcname("#poring_wl5");
	specialeffect EF_LORD, AREA, instance_npcname("#poring_wl5");
	disablenpc instance_npcname("#poring_wl1");
	disablenpc instance_npcname("#poring_wl2");
	disablenpc instance_npcname("#poring_wl3");
	disablenpc instance_npcname("#poring_wl6");
	disablenpc instance_npcname("#poring_wl7");
	disablenpc instance_npcname("#poring_wl8");
	setcell 'map_begi$,154,100,154,107,cell_walkable,1;
	end;
}

1@begi,1,1,5	script	poring_village_2	-1,{
	end;
OnStart:
	.@event$ = instance_npcname("poring_village_2") + "::OnMobDead";

	monster 'map_begi$,191,97,"--en--","MD_DROPS",1, .@event$;
	monster 'map_begi$,191,97,"--en--","MD_DROPS",1, .@event$;
	monster 'map_begi$,191,97,"--en--","MD_DROPS",1, .@event$;
	monster 'map_begi$,192,97,"--en--","MD_DROPS",1, .@event$;
	monster 'map_begi$,194,97,"--en--","MD_DROPS",1, .@event$;
	monster 'map_begi$,194,97,"--en--","MD_DROPS",1, .@event$;
	monster 'map_begi$,195,97,"--en--","MD_DROPS",1, .@event$;
	monster 'map_begi$,195,97,"--en--","MD_DROPS",1, .@event$;
	monster 'map_begi$,199,108,"--en--","MD_POPORING",1, .@event$;
	monster 'map_begi$,201,107,"--en--","MD_POPORING",1, .@event$;
	monster 'map_begi$,203,108,"--en--","MD_POPORING",1, .@event$;
	monster 'map_begi$,206,107,"--en--","MD_POPORING",1, .@event$;
	monster 'map_begi$,213,108,"--en--","MD_POPORING",1, .@event$;
	monster 'map_begi$,214,109,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,214,109,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,214,109,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,214,115,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,214,112,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,214,113,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,214,117,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,214,111,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,215,108,"--en--","MD_POPORING",1, .@event$;
	monster 'map_begi$,215,107,"--en--","MD_POPORING",1, .@event$;
	monster 'map_begi$,216,107,"--en--","MD_POPORING",1, .@event$;
	monster 'map_begi$,197,143,"--en--","MD_PORING",1, .@event$;
	monster 'map_begi$,197,143,"--en--","MD_PORING",1, .@event$;
	monster 'map_begi$,192,143,"--en--","MD_PORING",1, .@event$;
	monster 'map_begi$,190,143,"--en--","MD_PORING",1, .@event$;
	monster 'map_begi$,188,143,"--en--","MD_PORING",1, .@event$;
	monster 'map_begi$,188,143,"--en--","MD_PORING",1, .@event$;
	monster 'map_begi$,184,143,"--en--","MD_PORING",1, .@event$;
	monster 'map_begi$,182,143,"--en--","MD_PORING",1, .@event$;
	monster 'map_begi$,157,144,"--en--","MD_PORING",1, .@event$;
	monster 'map_begi$,154,143,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,154,144,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,152,143,"--en--","MD_DROPS",1, .@event$;
	monster 'map_begi$,142,144,"--en--","MD_POPORING",1, .@event$;
	monster 'map_begi$,130,144,"--en--","MD_PORING",1, .@event$;
	monster 'map_begi$,129,144,"--en--","MD_PORING",1, .@event$;
	monster 'map_begi$,127,144,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,120,144,"--en--","MD_PORING",1, .@event$;
	monster 'map_begi$,117,144,"--en--","MD_PORING",1, .@event$;
	monster 'map_begi$,116,144,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,114,143,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,103,144,"--en--","MD_POPORING",1, .@event$;
	monster 'map_begi$,102,144,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,102,144,"--en--","MD_DROPS",1, .@event$;
	monster 'map_begi$,93,143,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,88,144,"--en--","MD_DROPS",1, .@event$;
	monster 'map_begi$,88,144,"--en--","MD_POPORING",1, .@event$;
	monster 'map_begi$,84,144,"--en--","MD_POPORING",1, .@event$;
	monster 'map_begi$,84,144,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,83,143,"--en--","MD_PORING",1, .@event$;
	monster 'map_begi$,76,143,"--en--","MD_POPORING",1, .@event$;
	monster 'map_begi$,75,144,"--en--","MD_DROPS",1, .@event$;
	monster 'map_begi$,73,144,"--en--","MD_POPORING",1, .@event$;
	monster 'map_begi$,73,143,"--en--","MD_DROPS",1, .@event$;
	monster 'map_begi$,72,144,"--en--","MD_POPORING",1, .@event$;
	monster 'map_begi$,69,143,"--en--","MD_POPORING",1, .@event$;
	monster 'map_begi$,64,144,"--en--","MD_DROPS",1, .@event$;
	monster 'map_begi$,59,143,"--en--","MD_PORING",1, .@event$;
	monster 'map_begi$,59,144,"--en--","MD_DROPS",1, .@event$;
	monster 'map_begi$,52,144,"--en--","MD_DROPS",1, .@event$;
	monster 'map_begi$,51,143,"--en--","MD_PORING",1, .@event$;
	end;
OnMobDead:
	if (mobcount( 'map_begi$, instance_npcname("poring_village_2") + "::OnMobDead" ) > 4)
		end;
	killmonster 'map_begi$, instance_npcname("poring_village_2") + "::OnMobDead";
	mapannounce 'map_begi$, "Emily: Hmm... aqui está outro chefe... Parece forte...", bc_map, 0xFFFF00;
	sleep 2000;
	mapannounce 'map_begi$, "Emily: Eu não sou boa em batalha.. Deixe-me procurar o tesouro primeiro. Até breve~", bc_map, 0xFFFF00;
	sleep 2000;
	monster 'map_begi$,42,173,"--en--","MD_AMERING",1, instance_npcname("poring_village_2") + "::OnBossDead";
	end;
OnBossDead:
	if (mobcount( 'map_begi$, instance_npcname("poring_village_2") + "::OnBossDead" ) > 0)
		end;
	killmonster 'map_begi$, instance_npcname("poring_village_2") + "::OnBossDead";
	donpcevent instance_npcname("poring_village_3") + "::OnStart";
	sleep 6000;
	// disablenpc instance_npcname("#poring_wl10");
	hideonnpc instance_npcname("#poring_wl10");
	specialeffect EF_LORD, AREA, instance_npcname("#poring_wl10");
	specialeffect EF_LORD, AREA, instance_npcname("#poring_wl10");
	specialeffect EF_LORD, AREA, instance_npcname("#poring_wl10");
	specialeffect EF_LORD, AREA, instance_npcname("#poring_wl10");
	specialeffect EF_LORD, AREA, instance_npcname("#poring_wl10");
	specialeffect EF_LORD, AREA, instance_npcname("#poring_wl10");
	// disablenpc instance_npcname("#poring_wl13");
	hideonnpc instance_npcname("#poring_wl13");
	specialeffect EF_LORD, AREA, instance_npcname("#poring_wl13");
	specialeffect EF_LORD, AREA, instance_npcname("#poring_wl13");
	specialeffect EF_LORD, AREA, instance_npcname("#poring_wl13");
	specialeffect EF_LORD, AREA, instance_npcname("#poring_wl13");
	specialeffect EF_LORD, AREA, instance_npcname("#poring_wl13");
	specialeffect EF_LORD, AREA, instance_npcname("#poring_wl13");
	disablenpc instance_npcname("#poring_wl9");
	disablenpc instance_npcname("#poring_wl11");
	disablenpc instance_npcname("#poring_wl12");
	disablenpc instance_npcname("#poring_wl14");
	setcell 'map_begi$,59,187,59,197,cell_walkable,1;
	end;
}

1@begi,1,1,5	script	poring_village_3	-1,{
	end;
OnStart:
	.@event$ = instance_npcname("poring_village_3") + "::OnMobDead";

	monster 'map_begi$,81,197,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,84,197,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,85,197,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,86,197,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,87,197,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,88,197,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,90,197,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,93,197,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,94,197,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,94,197,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,95,197,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,98,197,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,99,197,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,103,197,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,103,197,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,104,197,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,108,197,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,111,197,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,128,185,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,128,185,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,130,185,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,132,185,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,132,185,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,132,185,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,133,185,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,134,185,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,135,185,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,136,185,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,137,185,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,141,185,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,143,185,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,144,185,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,144,185,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,146,185,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,150,185,"--en--","MD_MARIN",1, .@event$;
	monster 'map_begi$,150,185,"--en--","MD_MARIN",1, .@event$;
	end;
OnMobDead:
	if (mobcount( 'map_begi$, instance_npcname("poring_village_3") + "::OnMobDead" ) > 4)
		end;
	killmonster 'map_begi$, instance_npcname("poring_village_3") + "::OnMobDead";
	mapannounce 'map_begi$, "King Poring: Com licença... Mas... Você poderia voltar agora?...", bc_map, 0xFFFF;
sleep 2000;
mapannounce 'map_begi$, "Emily: O quê? Quem é você?", bc_map, 0xFFFF00;
sleep 2000;
mapannounce 'map_begi$, "King Poring: Eu sou o líder desta Vila Poring. Desculpe pela introdução tardia.", bc_map, 0xFFFF;
sleep 2000;
mapannounce 'map_begi$, "King Poring: Talvez haja algum mal-entendido em nossos folhetos. Esta vila é muito pobre e não tem nada de valioso.", bc_map, 0xFFFF;
sleep 2000;
mapannounce 'map_begi$, "Emily: Cabe a mim julgar depois de dar uma olhada melhor....", bc_map, 0xFFFF00;
sleep 2000;
mapannounce 'map_begi$, "King Poring: Ah.... Eu não gosto de lutar, mas não há escolha.", bc_map, 0xFFFF;
	sleep 2000;
	monster 'map_begi$,182,194,"--en--","PORING",10;
	for ( .@i = 0; .@i < 10; ++.@i ) {
		setunitdata $@mobid[.@i], UMOB_MODE, (MD_CANMOVE | MD_LOOTER | MD_AGGRESSIVE | MD_CANATTACK);
		setunitdata $@mobid[.@i], UMOB_SPEED, 200;
	}
	monster 'map_begi$,182,194,"--en--","MD_KING_PORING",1, instance_npcname("poring_village_3") + "::OnBossDead";
	end;
OnBossDead:
	if (mobcount( 'map_begi$, instance_npcname("poring_village_3") + "::OnBossDead" ) > 0)
		end;
	killmonster 'map_begi$, instance_npcname("poring_village_3") + "::OnBossDead";
	enablenpc instance_npcname("#gift");
	end;
}

1@begi,117,108,5	script	Blue Light Column#1	4_ENERGY_BLUE,4,4,{
	end;
OnTouch:
	switch( rand(3) ) {
	case 0:
		unittalk getcharid(3), "" + strcharinfo(0) + ": Ohhhhh!!! Solte o papagaio lá dentro!!!!";
		break;
	case 1:
		unittalk getcharid(3), "" + strcharinfo(0) + ": Acho que não há problema em bicar alguém agora!!";
		break;
	case 2:
		unittalk getcharid(3), "" + strcharinfo(0) + ": Você foi mordido por um papagaio louco?";
		break;
	}
	transform 1629,60000;
	specialeffect2 EF_ENHANCE;
	sc_start SC_GLASTHEIM_STATE,30000,20;
	end;
}
1@begi,37,165,5	duplicate(Blue Light Column#1)	Blue Light Column#2	4_ENERGY_BLUE,4,4
1@begi,175,199,5	duplicate(Blue Light Column#1)	Blue Light Column#3	4_ENERGY_BLUE,4,4


1@begi,199,186,5	script	#gift	4_GEFFEN_03,{
	mes "[Emily]";
	mes "Você pode ter todos os itens que encontrar nesta masmorra. Eu já tenho alguns.";
	next;
	if (isbegin_quest(12418) == 1) {
		mes "[Emily]";
		mes "Ah, encontrei alguns itens. Deixa-me dar-te um deles. Só para comemorar a sua primeira visita!";
		next;
		if (rand(100) < 50)
			getitem 19238,1;	// PoringTownOnion
		else
			getitem 19239,1;	// PoringTownCarrot
		completequest 12418;
	}
	mes "[Emily]";
	mes "Eu não preciso desta caixa de novatos. Pode pegar. Agora, vamos para o outro lado!";
	close2;
	getitem 35016,1;	// Moeda De Instancia
	getitem 12106,1;
	warp "prontera",156,189;
	end;
}