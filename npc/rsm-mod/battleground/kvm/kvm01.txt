//===== rAthena Script =======================================
//= BattleGround Expansion - KvM 80~99
//===== Por: =================================================
//= RomuloSM (sbk_)
//===== Versão Atual: ========================================
//= 1.0
//===== Compátivel com: ======================================
//= rAthena RSM Mod
//===== Descrição: ===========================================
//= Arena no qual as equipes devem se enfretar.
//===== Comentários Adicional: ===============================
//= 1.0 Primeira versão.[RomuloSM]
//===== Contato: =============================================
//= Email: romulodevel@gmail.com
//= Facebook: https://www.facebook.com/RomuloDevel
//============================================================

// Waiting Room NPCs
//============================================================
bat_room,169,226,5	script	Sala de Espera do KVM#a::KvM01R_Guillaume	418,{
	end;
OnStart:
	.@MaxPlayer = $@KvMBG1_MaxPlayer + 1;
	.@MinBaseLevel = $@KvMBG1_MinBaseLevel;
	.@MaxBaseLevel = $@KvMBG1_MaxBaseLevel;
	waitingroom "Estação de Batalha de " + $@KvMBG1_MaxPlayer + " Jogadores",.@MaxPlayer,"KvM01_BG_Out::OnGuillaumeJoin",1,0,.@MinBaseLevel,.@MaxBaseLevel;
	end;
OnEnterBG:
	set $@KvM01BG_id1, waitingroom2bg(BG_TEAM_A,"bat_c01",52,129,"KvM01_BG::OnGuillaumeQuit","KvM01_BG::OnGuillaumeDie");
	end;
}

bat_room,169,205,3	script	Sala de Espera do KVM#b::KvM01R_Croix	414,{
	end;
OnStart:
	.@MaxPlayer = $@KvMBG1_MaxPlayer + 1;
	.@MinBaseLevel = $@KvMBG1_MinBaseLevel;
	.@MaxBaseLevel = $@KvMBG1_MaxBaseLevel;
	waitingroom "Estação de Batalha de " + $@KvMBG1_MaxPlayer + " Jogadores",.@MaxPlayer,"KvM01_BG_Out::OnCroixJoin",1,0,.@MinBaseLevel,.@MaxBaseLevel;
	end;
OnEnterBG:
	set $@KvM01BG_id2, waitingroom2bg(BG_TEAM_B,"bat_c01",147,55,"KvM01_BG::OnCroixQuit","KvM01_BG::OnCroixDie");
	end;
}

bat_room,169,220,0	warp2	#kvm801	2,2,bat_room,154,150
bat_room,169,211,0	warp2	#kvm802	2,2,bat_room,154,150

// Starting Line
//============================================================
-	script	KVM01::CellEffect	-1,{
	end;
OnKvM01One:
	specialeffect EF_CHANGECOLD;
	end;
OnKvM01Two:
	specialeffect EF_CHANGEPOISON;
	end;
}

bat_c01,54,124,3	duplicate(CellEffect)	#RedcellA1-1	139
bat_c01,55,124,3	duplicate(CellEffect)	#RedcellA2-1	139
bat_c01,56,124,3	duplicate(CellEffect)	#RedcellA3-1	139
bat_c01,57,124,3	duplicate(CellEffect)	#RedcellA4-1	139
bat_c01,57,125,3	duplicate(CellEffect)	#RedcellA5-1	139
bat_c01,57,126,3	duplicate(CellEffect)	#RedcellA6-1	139
bat_c01,57,127,3	duplicate(CellEffect)	#RedcellA7-1	139

bat_c01,145,59,3	duplicate(CellEffect)	#RedcellB1-1	139
bat_c01,144,59,3	duplicate(CellEffect)	#RedcellB2-1	139
bat_c01,143,59,3	duplicate(CellEffect)	#RedcellB3-1	139
bat_c01,142,59,3	duplicate(CellEffect)	#RedcellB4-1	139
bat_c01,142,56,3	duplicate(CellEffect)	#RedcellB5-1	139
bat_c01,142,57,3	duplicate(CellEffect)	#RedcellB6-1	139
bat_c01,142,58,3	duplicate(CellEffect)	#RedcellB7-1	139

bat_c01,54,128,3	script	#A_camp_start01	139,4,4,{
	end;
OnInit:
	disablenpc "#A_camp_start01";
	end;
OnEnable:
	enablenpc "#A_camp_start01";
	end;
OnDisable:
	disablenpc "#A_camp_start01";
	end;
OnTouch:
	set Bat_Team,1;
	if (!getbattleflag("feature.bgqueue"))
		setquest 6025;
	end;
}

bat_c01,146,56,3	script	#B_camp_start01	139,4,4,{
	end;
OnInit:
	disablenpc "#B_camp_start01";
	end;
OnEnable:
	enablenpc "#B_camp_start01";
	end;
OnDisable:
	disablenpc "#B_camp_start01";
	end;
OnTouch:
	set Bat_Team,2;
	if (!getbattleflag("feature.bgqueue"))
		setquest 6025;
	end;
}

// Battleground Engine
//============================================================
-	script	KvM01_BG	-1,{
	end;

OnInit:
	if (getbattleflag("feature.bgqueue"))
		bg_unbook "bat_c01";
	
	mapwarp "bat_c01","bat_room",154,150;
	setwall "bat_c01",54,122,6,7,0,"batc01wall_a";
	setwall "bat_c01",55,122,5,7,0,"batc01wall_b";
	setwall "bat_c01",140,56,6,7,0,"batc01wall_c";
	setwall "bat_c01",140,57,5,7,0,"batc01wall_d";
	disablenpc "Oficial KVM#KVM01A";
	disablenpc "Oficial KVM#KVM01B";
	end;

OnGuillaumeQuit:
	if (getbattleflag("feature.bgqueue"))
		bg_desert;
		
	.@quit = true;
//	else
//		set BG_Delay_Tick, gettimetick(2) + 1200;
OnGuillaumeDie:
	// Ranking
	callfunc "F_BG_RANKING", 5, getcharid(0), 0, 1, bg_ranking_get2(BGR_SCORE_WINS), 1;
	bg_ranking_save 5, true;

	if( .@quit != true ) {
		if( killerrid ) {
			.@name$ = rid2name(killerrid);
			.@cid = getcharid(0,.@name$);
			if( .@cid ) {
				bg_ranking_set BGR_SCORE_WINS, bg_ranking_get(BGR_SCORE_WINS,.@cid)+1, .@cid;
				bg_ranking_set2 BGR_SCORE_WINS, bg_ranking_get2(BGR_SCORE_WINS,.@cid)+1, .@cid;
			}
		}
	}
	
	if( $@KvM01BG == 2 )
	{
		set .Guillaume_Count, .Guillaume_Count - 1;
		bg_updatescore "bat_c01",.Guillaume_Count,.Croix_Count;
		if( .Guillaume_Count < 1 ) donpcevent "KvM01_BG::OnCroixWin";
		else {
			mapannounce "bat_c01", "O número de membros de Guillaume é "+.Guillaume_Count+".",bc_map,"0x00ff00";
			mapannounce "bat_c01", "O número de membros de Croix é "+.Croix_Count+".",bc_map,"0x00ff00";
		}
	}
	end;

OnCroixQuit:
	if (getbattleflag("feature.bgqueue"))
		bg_desert;
		
	.@quit = true;
//	else
//		set BG_Delay_Tick, gettimetick(2) + 1200;
OnCroixDie:
	// Ranking
	callfunc "F_BG_RANKING", 5, getcharid(0), 1, 1, 1, bg_ranking_get2(BGR_SCORE_WINS);
	bg_ranking_save 5, true;

	if( .@quit != true ) {
		if( killerrid ) {
			.@name$ = rid2name(killerrid);
			.@cid = getcharid(0,.@name$);
			if( .@cid ) {
				bg_ranking_set BGR_SCORE_WINS, bg_ranking_get(BGR_SCORE_WINS,.@cid)+1, .@cid;
				bg_ranking_set2 BGR_SCORE_WINS, bg_ranking_get2(BGR_SCORE_WINS,.@cid)+1, .@cid;
			}
		}
	}
	
	if( $@KvM01BG == 2 )
	{
		set .Croix_Count, .Croix_Count - 1;
		bg_updatescore "bat_c01",.Guillaume_Count,.Croix_Count;
		if( .Croix_Count < 1 ) donpcevent "KvM01_BG::OnGuillaumeWin";
		else {
			mapannounce "bat_c01", "O número de membros de Guillaume é "+.Guillaume_Count+".",bc_map,"0x00ff00";
			mapannounce "bat_c01", "O número de membros de Croix é "+.Croix_Count+".",bc_map,"0x00ff00";
		}
	}
	end;

OnGuillaumeActive:
	if( $@KvM01BG == 2 ) {
		warp "bat_c01",61,120;
		set .Guillaume_Count, .Guillaume_Count + 1;
		bg_updatescore "bat_c01",.Guillaume_Count,.Croix_Count;
	}
	else
		warp "bat_c01",53,128;	
	end;

OnCroixActive:
	if( $@KvM01BG == 2 ) {
		warp "bat_c01",138,63;
		set .Croix_Count, .Croix_Count + 1;
		bg_updatescore "bat_c01",.Guillaume_Count,.Croix_Count;
	}
	else
		warp "bat_c01",146,55;
	end;

OnStart:
	disablenpc "Oficial KVM#KVM01A";
	disablenpc "Oficial KVM#KVM01B";
	set $@KvM01BG_Victory, 0;
	// Warp Teams
	bg_warp $@KvM01BG_id1,"bat_c01",53,128;
	bg_warp $@KvM01BG_id2,"bat_c01",146,55;
	initnpctimer;
	end;

OnTimer1000:
	mapannounce "bat_c01", "Em 1 minuto, a KVM irá começar.",bc_map,"0x00ff00";
	end;

OnTimer3000:
	mapannounce "bat_c01", "O tempo máximo de uma batalha KVM são de " + $@KvMBG1_BattleTimer + " minutos.",bc_map,"0x00ff00";
	end;

OnTimer6000:
	mapannounce "bat_c01", "Se preparem para a batalha KVM.",bc_map,"0x00ff00";
	mapannounce "bat_c01", "Vocês podem usar buffs em seu time.",bc_map,"0x00ff00";
	donpcevent "#A_camp_start01::OnEnable";
	donpcevent "#B_camp_start01::OnEnable";
	end;

OnTimer13000:
	donpcevent "#A_camp_start01::OnDisable";
	donpcevent "#B_camp_start01::OnDisable";
	end;

OnTimer30000:
	mapannounce "bat_c01", "30 segundos restantes para iniciar a batalha KVM.",bc_map,"0x00ff00";
	end;

OnTimer45000:
	mapannounce "bat_c01", "15 segundos restantes para iniciar a batalha KVM.",bc_map,"0x00ff00";
	donpcevent "::OnKvM01One";
	end;

OnTimer50000:
	mapannounce "bat_c01", "10 segundos restantes para iniciar a batalha KVM.",bc_map,"0x00ff00";
	donpcevent "::OnKvM01Two";
	end;

OnTimer55000:
	mapannounce "bat_c01", "5 segundos restantes para iniciar a batalha KVM.",bc_map,"0x00ff00";
	donpcevent "::OnKvM01One";
	end;

OnTimer59000:
	mapannounce "bat_c01", "KVM está começando agora.",bc_map,"0x00ff00";
	donpcevent "::OnKvM01Two";
	end;

OnTimer61000:
	// Team Members
	set .Guillaume_Count, bg_get_data($@KvM01BG_id1, 0);
	set .Croix_Count, bg_get_data($@KvM01BG_id2, 0);

	if (!getbattleflag("feature.bgqueue")) {
		if (.Guillaume_Count < $@KvMBG1_MaxPlayer || .Croix_Count < $@KvMBG1_MaxPlayer) {
			set $@KvM01BG_Victory, 3;
			set $@KvM01BG, 3;
			mapannounce "bat_c01","Não há jogadores suficientes para começar a batalha.",1,0x696969;
			stopnpctimer;
			donpcevent "KvM01_BG::OnStop";
			donpcevent "KvM01_BG_countdown::OnStop";
			end;
		}
	}

	donpcevent "KvM01_BG_countdown::OnStart";
	bg_setcountdown $@KvM01BG_id1, $@KvMBG1_BattleTimer*60;
	bg_setcountdown $@KvM01BG_id2, $@KvMBG1_BattleTimer*60;
	bg_updatescore "bat_c01",.Guillaume_Count,.Croix_Count;
	set $@KvM01BG, 2; // Playing
bg_warp $@KvM01BG_id1,"bat_c01",61,120;
bg_warp $@KvM01BG_id2,"bat_c01",138,63;
	end;

OnGuillaumeWin:
	set $@KvM01BG, 3;
	set $@KvM01BG_Victory, 1;
	// Ranking
	callfunc "F_BG_RANKING_TEAM", 5, $@KvM01BG_id1, $@KvM01BG_id2, 0, -1, -1, true;
	mapannounce "bat_c01", "Guillaume venceu!",bc_map,"0x00ff00";
	mapannounce "bat_c01", "Parabéns aos membros de Guillaume.",bc_map,"0x00ff00";
	mapannounce "bat_c01", "Todos serão enviados ao ponto inicial.",bc_map,"0x00ff00";
	donpcevent "KvM01_BG::OnStop";
	donpcevent "KvM01_BG_countdown::OnStop";
	end;

OnCroixWin:
	set $@KvM01BG, 3;
	set $@KvM01BG_Victory, 2;
	// Ranking
	callfunc "F_BG_RANKING_TEAM", 5, $@KvM01BG_id1, $@KvM01BG_id2, 1, -1, -1, true;
	mapannounce "bat_c01", "Croix venceu!",bc_map,"0x00ff00";
	mapannounce "bat_c01", "Parabéns aos membros de Croix.",bc_map,"0x00ff00";
	mapannounce "bat_c01", "Todos serão enviados ao ponto inicial.",bc_map,"0x00ff00";
	donpcevent "KvM01_BG::OnStop";
	donpcevent "KvM01_BG_countdown::OnStop";
	end;

OnStop:
	stopnpctimer;
	enablenpc "Oficial KVM#KVM01A";
	enablenpc "Oficial KVM#KVM01B";
	// Warp Teams
	bg_warp $@KvM01BG_id1,"bat_c01",53,128;
	bg_warp $@KvM01BG_id2,"bat_c01",146,55;
	donpcevent "KvM01_BG_Out::OnBegin";
	donpcevent "KvM01_BG_countdown::OnStop";
	if (getbattleflag("feature.bgqueue"))
		bg_reserve "bat_c01", true;
	end;
}

-	script	KvM01_BG_countdown	-1,{
OnStart:
	.TimerCount = 0;
	initnpctimer;
	if( $@KvMBG1_BattleTimer <= 1 ) {
		setnpctimer 60000;
		mapannounce "bat_c01", "1 minuto restante para o final da batalha KVM.",bc_map,"0x00ff00";
	}
	end;
	
OnStop:
	stopnpctimer;
	end;
	
OnTimer60000:
	.TimerCount++;
	if( (.TimerCount+1) < $@KvMBG2_BattleTimer )
		setnpctimer 0;
	else
		mapannounce "bat_c01", "1 minuto restante para o final da batalha KVM.",bc_map,"0x00ff00";
	end;
	
OnTimer90000:
	mapannounce "bat_c01", "30 segundos restantes para o final da batalha KVM.",bc_map,"0x00ff00";
	end;

OnTimer105000:
	mapannounce "bat_c01", "15 segundos restantes para o final da batalha KVM.",bc_map,"0x00ff00";
	end;

OnTimer110000:
	mapannounce "bat_c01", "10 segundos restantes para o final da batalha KVM.",bc_map,"0x00ff00";
	end;

OnTimer115000:
	mapannounce "bat_c01", "5 segundos restantes para o final da batalha KVM.",bc_map,"0x00ff00";
	end;

OnTimer120000:
	mapannounce "bat_c01", "A batalha KVM acabou.",bc_map,"0x00ff00";
	if( .Croix_Count > .Guillaume_Count )
		donpcevent "KvM01_BG::OnCroixWin";
	else if( .Croix_Count < .Guillaume_Count )
		donpcevent "KvM01_BG::OnGuillaumeWin";
	else
	{ // Draw Game
		set $@KvM01BG, 3;
		set $@KvM01BG_Victory, 3;
		// Ranking
		callfunc "F_BG_RANKING_TEAM", 5, $@KvM01BG_id1, $@KvM01BG_id2, 2, -1, -1, true;
		mapannounce "bat_c01", "O número de membros de Guillaume é "+.Guillaume_Count+".",bc_map,"0x00ff00";
		mapannounce "bat_c01", "O número de membros de Croix é "+.Croix_Count+".",bc_map,"0x00ff00";
		mapannounce "bat_c01", "A batalha terminou em empate.",bc_map,"0x00ff00";
		donpcevent "KvM01_BG::OnStop";
	}
	end;
}

-	script	KvM01_BG_Out	-1,{
	end;

OnBegin:
	bg_setcountdown $@KvM01BG_id1, 30;
	bg_setcountdown $@KvM01BG_id2, 30;
	initnpctimer;
	end;

OnTimer1000:
	mapannounce "bat_c01", "Por favor fale com o Oficial para receber sua recompensa.",bc_map,"0x00ff00";
	end;

OnTimer3000:
	mapannounce "bat_c01", "O Oficial dará a você sua recompensa por 30 segundos.",bc_map,"0x00ff00";
	mapannounce "bat_c01", "Em 30 segundos, o Oficial deixará o local.",bc_map,"0x00ff00";
	end;

OnTimer5000:
	mapannounce "bat_c01", "Se você não falar com o Oficial, você não irá receber sua recompensa.",bc_map,"0x00ff00";
	mapannounce "bat_c01", "Por favor tenha cuidado.",bc_map,"0x00ff00";
	end;

OnTimer25000:
	mapannounce "bat_c01", "Você será enviado de volta.",bc_map,"0x00ff00";
	end;

OnTimer30000:
	stopnpctimer;
	if (getbattleflag("feature.bgqueue"))
		bg_reserve "bat_c01", true;
	mapwarp "bat_c01","bat_room",154,150;
	set getvariableofnpc(.Croix_Count,"KvM01_BG"), 0;
	set getvariableofnpc(.Guillaume_Count,"KvM01_BG"), 0;
	set $@KvM01BG_Victory, 0;
	if( $@KvM01BG_id1 ) { bg_destroy $@KvM01BG_id1; set $@KvM01BG_id1, 0; }
	if( $@KvM01BG_id2 ) { bg_destroy $@KvM01BG_id2; set $@KvM01BG_id2, 0; }
	if (getbattleflag("feature.bgqueue"))
		bg_unbook "bat_c01";
	disablenpc "Oficial KVM#KVM01A";
	disablenpc "Oficial KVM#KVM01B";
	set $@KvM01BG, 0;
	end;

OnGuillaumeJoin:
OnCroixJoin:
	if( $@KvM01BG )
		end;
	set .@Guillaume, getwaitingroomstate(0,"KvM01R_Guillaume");
	set .@Croix, getwaitingroomstate(0,"KvM01R_Croix");

	if( .@Guillaume < $@KvMBG1_MaxPlayer || .@Croix < $@KvMBG1_MaxPlayer )
		end;

	set $@KvM01BG, 1; // Starting
	donpcevent "KvM01R_Croix::OnEnterBG";
	donpcevent "KvM01R_Guillaume::OnEnterBG";
	donpcevent "KvM01_BG::OnStart";
	end;
}

// Battleground rewards
//============================================================
bat_c01,51,130,5	script	Oficial KVM#KVM01A	419,{
	if( $@KvM01BG_Victory ) {
		mes "[Oficial KVM]";
		if( $@KvM01BG_Victory == Bat_Team ) {
			mes "Bom jogo.";
			mes "Que a glória do KVM esteja com você.";
			if( !getbattleflag("bg_reward_enable") )
				mes "Você recebe os pontos de vencedor: 5";
			else
				mes "Você receberá sua recompensa.";
			close2;
			if( !getbattleflag("bg_reward_enable") )
				set kvm_point,kvm_point + 5;
			else
				bg_reward 3, 0, (VIP_SCRIPT && vip_status(VIP_STATUS_ACTIVE));
		}
		else {
			mes "Sinto muito.";
			mes "Espero que tenha boa sorte na próxima vez.";
			if( !getbattleflag("bg_reward_enable") )
				mes "Você recebe os pontos de derrota: 1";
			else
				mes "Você receberá sua recompensa.";
			close2;
			if( !getbattleflag("bg_reward_enable") )
				set kvm_point,kvm_point + 1;
			else
				bg_reward 3, ($@KvM01BG_Victory == 3 ? 2 : 1), (VIP_SCRIPT && vip_status(VIP_STATUS_ACTIVE));
		}
		bg_leave;
		set Bat_Team,0;
		if (!getbattleflag("feature.bgqueue"))
			warp "bat_room",154,150;
	}
	end;
}
bat_c01,148,53,1	duplicate(Oficial KVM#KVM01A)	Oficial KVM#KVM01B	415

// BG Queue makes these scripts useless
-	script	BGQueueInit#kvm01	-1,{
	end;

OnInit:
	if (getbattleflag("feature.bgqueue")) {
		unloadnpc "KvM01R_Guillaume";
		unloadnpc "KvM01R_Croix";
		unloadnpc "#kvm801";
		unloadnpc "#kvm802";
	}
	end;
}

//===== rAthena Script =======================================
//= BattleGround System - KvM 80-99
//===== Description: =========================================
//= [Official Conversion]
//= Kreiger Von Midgard Battleground for levels 80 to 99
//= - Winning Team: 5 points
//= - Losing Team: 1 point
//===== Changelogs: ==========================================
//= 1.0 First Version. [L0ne_W0lf]
//= 1.1 Updated using official Aegis files. [L0ne_W0lf]
//= 1.2 Upated some announces and dialogs from iRO.
//=     Changed how the scoreboard works slightly.
//=     Removed the areapercentheals, and minor things.
//= 1.3 Added battle_config checks to allow this script to use the new queue interface or the previous method. [Aleos]
//============================================================