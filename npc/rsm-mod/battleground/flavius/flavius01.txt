//===== rAthena Script =======================================
//= BattleGround Expansion - Arena de Flavius
//===== Por: =================================================
//= RomuloSM (sbk_)
//===== Versão Atual: ========================================
//= 1.0
//===== Compátivel com: ======================================
//= rAthena RSM Mod
//===== Descrição: ===========================================
//= Arena em que a equipe deve eliminar o Cristal da equipe
//= inimiga, porém deve ser destruído dois Guardiões que a
//= protegem e a mantém imune a ataques.
//===== Comentários Adicional: ===============================
//= 1.0 Primeira versão.[RomuloSM]
//===== Contato: =============================================
//= Email: romulodevel@gmail.com
//= Facebook: https://www.facebook.com/RomuloDevel
//============================================================

// Waiting Room NPCs
//============================================================
bat_room,86,227,4	script	Tenente Ator	418,{
	end;
OnStart:
	.@MaxPlayer = $@FlaviusBG1_MaxPlayer + 1;
	.@MinBaseLevel = $@FlaviusBG1_MinBaseLevel;
	.@MaxBaseLevel = $@FlaviusBG1_MaxBaseLevel;
	waitingroom "Estação de Batalha",.@MaxPlayer,"start#bat_b01::OnReadyCheck",1,0,.@MinBaseLevel,.@MaxBaseLevel;
	end;
OnEnterBG:
	set $@FlaviusBG1_id1, waitingroom2bg(BG_TEAM_A,"bat_b01",10,290,"start#bat_b01::OnGuillaumeQuit","");
	end;
}

bat_room,85,204,0	script	Tenente Thelokus	414,{
	end;
OnStart:
	.@MaxPlayer = $@FlaviusBG1_MaxPlayer + 1;
	.@MinBaseLevel = $@FlaviusBG1_MinBaseLevel;
	.@MaxBaseLevel = $@FlaviusBG1_MaxBaseLevel;
	waitingroom "Estação de Batalha",.@MaxPlayer,"start#bat_b01::OnReadyCheck",1,0,.@MinBaseLevel,.@MaxBaseLevel;
	end;
OnEnterBG:
	set $@FlaviusBG1_id2, waitingroom2bg(BG_TEAM_B,"bat_b01",390,10,"start#bat_b01::OnCroixQuit","");
	end;
}

bat_room,85,220,0	warp	back_bgroomb01a	1,1,bat_room,154,150
bat_room,85,211,0	warp	back_bgroomb01b	1,1,bat_room,154,150

bat_room,2,151,3	script	#bat_b01_timer	844,{
	end;

OnInit:
	if (getbattleflag("feature.bgqueue"))
		end;
OnEnable:
	initnpctimer;
	end;

OnStop:
	stopnpctimer;
	end;

OnTimer1000:
	stopnpctimer;
	if (bg_get_data($@FlaviusBG1_id1, 0) == 0 && bg_get_data($@FlaviusBG1_id2, 0) == 0) {
		donpcevent "countdown#bat_b01::OnStop";
		donpcevent "time_out#bat_b01::OnStop";
		if (getbattleflag("feature.bgqueue"))
			bg_reserve "bat_b01", true;
		$@FlaviusBG1 = 0; $@FlaviusBG1_Victory = 0; $@Guill_ScoreBG1 = 0; $@Croix_ScoreBG1 = 0;
		if( $@FlaviusBG1_id1 ) { bg_destroy $@FlaviusBG1_id1; $@FlaviusBG1_id1 = 0; }
		if( $@FlaviusBG1_id2 ) { bg_destroy $@FlaviusBG1_id2; $@FlaviusBG1_id2 = 0; }
		if (getbattleflag("feature.bgqueue"))
			bg_unbook "bat_b01";
		end;
	}
	initnpctimer;
	end;
}

// Flavius Battleground Engine
//============================================================
bat_b01,15,15,3	script	start#bat_b01	844,{
OnInit:
	mapwarp "bat_b01","bat_room",154,150;
	end;

OnReadyCheck:
	if( $@FlaviusBG1 )
		end;
	if (!getbattleflag("feature.bgqueue")) {
		set .@Guillaume, getwaitingroomstate(0,"Tenente Ator");
		set .@Croix, getwaitingroomstate(0,"Tenente Thelokus");
		if( !.@Guillaume && !.@Croix ) {
			donpcevent "#bat_b01_timer::OnStop";
			end;
		}
		if( .@Guillaume < $@FlaviusBG1_MaxPlayer || .@Croix < $@FlaviusBG1_MaxPlayer )
			end;
	}
	set $@FlaviusBG1, 1;
	set $@FlaviusBG1_Victory, 0;
	set $@Croix_ScoreBG1, 0;
	set $@Guill_ScoreBG1, 0;
	bg_updatescore "bat_b01",$@Guill_ScoreBG1,$@Croix_ScoreBG1;

	if (!getbattleflag("feature.bgqueue")) {
		donpcevent "Tenente Ator::OnEnterBG";
		donpcevent "Tenente Thelokus::OnEnterBG";
	}
	donpcevent "OBJ#bat_b01_a::OnKill";
	donpcevent "OBJ#bat_b01_a::OnEnable";
	donpcevent "OBJ#bat_b01_b::OnKill";
	donpcevent "OBJ#bat_b01_b::OnEnable";
	donpcevent "guardian#bat_b01_a::OnKill";
	donpcevent "guardian#bat_b01_b::OnKill";
	donpcevent "guardian#bat_b01_a::OnEnable";
	donpcevent "guardian#bat_b01_b::OnEnable";
	donpcevent "cell#bat_b01_a::OnRed";
	donpcevent "cell#bat_b01_b::OnRed";
	donpcevent "time#bat_b01::OnEnable";
	disablenpc "Tenente de Guillaume#b01_a";
	disablenpc "Tenente de Croix#b01_b";
	//disablenpc "Vintenar#bat_b01_aover";
	//disablenpc "Vintenar#bat_b01_bover";
	bg_warp $@FlaviusBG1_id1,"bat_b01",87,75;
	bg_warp $@FlaviusBG1_id2,"bat_b01",311,224;
	donpcevent "countdown#bat_b01::OnEnable";
	donpcevent "::OnBatB01Emblem";
	bg_setcountdown $@FlaviusBG1_id1, $@FlaviusBG1_BattleTimer*60;
	bg_setcountdown $@FlaviusBG1_id2, $@FlaviusBG1_BattleTimer*60;
	initnpctimer;
	end;

OnReset:
	donpcevent "OBJ#bat_b01_a::OnKill";
	donpcevent "OBJ#bat_b01_a::OnEnable";
	donpcevent "OBJ#bat_b01_b::OnKill";
	donpcevent "OBJ#bat_b01_b::OnEnable";
	donpcevent "guardian#bat_b01_a::OnKill";
	donpcevent "guardian#bat_b01_b::OnKill";
	donpcevent "guardian#bat_b01_a::OnEnable";
	donpcevent "guardian#bat_b01_b::OnEnable";
	donpcevent "cell#bat_b01_a::OnRed";
	donpcevent "cell#bat_b01_b::OnRed";
	end;

OnGuillaumeActive:
	warp "bat_b01",87,75;
	end;

OnCroixActive:
	warp "bat_b01",311,224;
	end;

OnGuillaumeQuit:
	// Ranking
	callfunc "F_BG_RANKING", 3, getcharid(0), 0, 1, $@Guill_ScoreBG1, $@Croix_ScoreBG1;

	if (getbattleflag("feature.bgqueue"))
		bg_desert;
	else
		bg_leave;
	end;

OnCroixQuit:
	// Ranking
	callfunc "F_BG_RANKING", 3, getcharid(0), 1, 1, $@Guill_ScoreBG1, $@Croix_ScoreBG1;

	if (getbattleflag("feature.bgqueue"))
		bg_desert;
	else
		bg_leave;
	end;

OnTimer10000:
	stopnpctimer;
	donpcevent "#bat_b01_timer::OnEnable";
	end;
}

bat_b01,1,1,3	script	OBJ#bat_b01_a	844,{
OnEnable:
	$@FlavBG1_C1_ID = bg_monster($@FlaviusBG1_id1,"bat_b01",61,150,"Cristal Vermelho",1915,"OBJ#bat_b01_a::OnMyMobDead");
	setunitdata $@FlavBG1_C1_ID,UMOB_DMGIMMUNE,1; // Make Crystal immune to damage until Guardians are defeated
	bg_setmobdesc $@FlavBG1_C1_ID, "Objetivo";
	end;

OnKill:
	killmonster "bat_b01","OBJ#bat_b01_a::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_b01","OBJ#bat_b01_a::OnMyMobDead") < 1) {
		mapannounce "bat_b01", "O Cristal de Guillaume foi destruído.",bc_map,"0xFFCE00";
		set $@Croix_ScoreBG1,$@Croix_ScoreBG1+1;
		if ($@Croix_ScoreBG1 >= $@FlaviusBG1_MaxRound) {
			// Team Ranking
			callfunc "F_BG_RANKING_TEAM", 3, $@FlaviusBG1_id1, $@FlaviusBG1_id2, 2, $@Guill_ScoreBG1, $@Guill_ScoreBG1, true;
			
			set $@FlaviusBG1_Victory,2;
			enablenpc "Tenente de Guillaume#b01_a";
			enablenpc "Tenente de Croix#b01_b";
			donpcevent "time#bat_b01::OnStop";
			donpcevent "countdown#bat_b01::OnStop";
			donpcevent "time_out#bat_b01::OnStart";
			if (getbattleflag("feature.bgqueue"))
				bg_reserve "bat_b01", true;
		}
		else {
			donpcevent "time#bat_b01::OnEnable";
			donpcevent "start#bat_b01::OnReset";
		}
		donpcevent "#bat_b01_timer::OnStop";
		bg_updatescore "bat_b01",$@Guill_ScoreBG1,$@Croix_ScoreBG1;
		bg_warp $@FlaviusBG1_id1,"bat_b01",10,290;
		bg_warp $@FlaviusBG1_id2,"bat_b01",390,10;
		donpcevent "#bat_b01_timer::OnEnable";
	}
	end;
}

bat_b01,1,2,3	script	OBJ#bat_b01_b	844,{
OnEnable:
	$@FlavBG1_C2_ID = bg_monster($@FlaviusBG1_id2,"bat_b01",328,150,"Cristal Azul",1914,"OBJ#bat_b01_b::OnMyMobDead");
	setunitdata $@FlavBG1_C2_ID,UMOB_DMGIMMUNE,1; // Make Crystal immune to damage until Guardians are defeated
	bg_setmobdesc $@FlavBG1_C2_ID, "Objetivo";
	end;

OnKill:
	killmonster "bat_b01","OBJ#bat_b01_b::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_b01","OBJ#bat_b01_b::OnMyMobDead") < 1) {
		mapannounce "bat_b01", "O Cristal de Croix foi destruído.",bc_map,"0xFFCE00";
		set $@Guill_ScoreBG1,$@Guill_ScoreBG1+1;
		if ($@Guill_ScoreBG1 >= $@FlaviusBG1_MaxRound) {
			// Team Ranking
			callfunc "F_BG_RANKING_TEAM", 3, $@FlaviusBG1_id1, $@FlaviusBG1_id2, 1, $@Guill_ScoreBG1, $@Guill_ScoreBG1, true;

			set $@FlaviusBG1_Victory,1;
			enablenpc "Tenente de Guillaume#b01_a";
			enablenpc "Tenente de Croix#b01_b";
			donpcevent "time#bat_b01::OnStop";
			donpcevent "countdown#bat_b01::OnStop";
			donpcevent "time_out#bat_b01::OnStart";
			if (getbattleflag("feature.bgqueue"))
				bg_reserve "bat_b01", true;
		}
		else {
			donpcevent "time#bat_b01::OnEnable";
			donpcevent "start#bat_b01::OnReset";
		}
		donpcevent "#bat_b01_timer::OnStop";
		bg_updatescore "bat_b01",$@Guill_ScoreBG1,$@Croix_ScoreBG1;
		bg_warp $@FlaviusBG1_id1,"bat_b01",10,290;
		bg_warp $@FlaviusBG1_id2,"bat_b01",390,10;
		donpcevent "#bat_b01_timer::OnEnable";
	}
	end;
}

bat_b01,1,3,3	script	guardian#bat_b01_a	844,{
OnEnable:
	.@UID = bg_monster($@FlaviusBG1_id1,"bat_b01",108,159,"Guardião",1949,"guardian#bat_b01_a::OnMyMobDead");
	bg_setmobdesc .@UID, "Acampamento";
	.@UID = bg_monster($@FlaviusBG1_id1,"bat_b01",108,141,"Guardião",1949,"guardian#bat_b01_a::OnMyMobDead");
	bg_setmobdesc .@UID, "Acampamento";
	end;

OnKill:
	killmonster "bat_b01","guardian#bat_b01_a::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_b01","guardian#bat_b01_a::OnMyMobDead") < 1) {
		donpcevent "cell#bat_b01_a::OnGreen";
		mapannounce "bat_b01", "O Guardião que protegia o Cristal de Guillaume foi destruído.",bc_map,"0xFFCE00";
		setunitdata $@FlavBG1_C1_ID,UMOB_DMGIMMUNE,0; // Make Crystal damageable again
	}
	end;
}

bat_b01,1,3,3	script	guardian#bat_b01_b	844,{
OnEnable:
	.@UID = bg_monster($@FlaviusBG1_id2,"bat_b01",307,160,"Guardião",1949,"guardian#bat_b01_b::OnMyMobDead");
	bg_setmobdesc .@UID, "Acampamento";
	.@UID = bg_monster($@FlaviusBG1_id2,"bat_b01",307,138,"Guardião",1949,"guardian#bat_b01_b::OnMyMobDead");
	bg_setmobdesc .@UID, "Acampamento";
	end;

OnKill:
	killmonster "bat_b01","guardian#bat_b01_b::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount("bat_b01","guardian#bat_b01_b::OnMyMobDead") < 1) {
		donpcevent "cell#bat_b01_b::OnGreen";
		mapannounce "bat_b01", "O Guardião que protegia o Cristal de Croix foi destruído.",bc_map,"0xFFCE00";
		setunitdata $@FlavBG1_C2_ID,UMOB_DMGIMMUNE,0; // Make Crystal damageable again
	}
	end;
}

bat_b01,1,4,3	script	cell#bat_b01_a	844,{
OnRed:
	setcell "bat_b01",62,149,60,151,cell_basilica,1;
	setcell "bat_b01",62,149,60,151,cell_walkable,0;
	end;

OnGreen:
	setcell "bat_b01",62,149,60,151,cell_basilica,0;
	setcell "bat_b01",62,149,60,151,cell_walkable,1;
	end;
}

bat_b01,1,5,3	script	cell#bat_b01_b	844,{
OnRed:
	setcell "bat_b01",327,151,329,149,cell_basilica,1;
	setcell "bat_b01",327,151,329,149,cell_walkable,0;
	end;

OnGreen:
	setcell "bat_b01",327,151,329,149,cell_basilica,0;
	setcell "bat_b01",327,151,329,149,cell_walkable,1;
	end;
}

bat_b01,1,6,1	script	time#bat_b01	844,{
OnEnable:
	donpcevent "Terapeuta de Batalha#b01_a::OnEnable";
	donpcevent "Terapeuta de Batalha#b01_b::OnEnable";
	end;

OnStop:
	donpcevent "Terapeuta de Batalha#b01_a::OnStop";
	donpcevent "Terapeuta de Batalha#b01_b::OnStop";
	end;
}

bat_b01,10,294,3	script	Terapeuta de Batalha#b01_a	95,{
	specialeffect2 EF_HEAL;
	mes "[Terapeuta de Batalha]";
	mes "Agora feche o seus olhos";
	mes "e respire bem fundo.";
	mes "Você pode se libertar da dor agora.";
	close;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	// enablenpc "bat_b01_rp1_a_warp";
	areapercentheal "bat_b01",0,280,20,300,100,100;
	areawarp "bat_b01",0,280,20,300,"bat_b01",87,73;
	end;

// OnTimer26000:
	// disablenpc "bat_b01_rp1_a_warp";
	// end;

OnTimer26500:
	stopnpctimer;
	donpcevent "Terapeuta de Batalha#b01_a::OnEnable";
	end;

OnEnable:
	initnpctimer;
	enablenpc "Terapeuta de Batalha#b01_a";
	end;

OnStop:
	// disablenpc "bat_b01_rp1_a_warp";
	disablenpc "Terapeuta de Batalha#b01_a";
	stopnpctimer;
	end;
}

/*
// replaced by areapercentheal and areawarp to prevent enqueue issue
bat_b01,10,290,0	script	bat_b01_rp1_a_warp	45,10,10,{
OnInit:
	disablenpc "bat_b01_rp1_a_warp";
	end;

OnTouch:
	percentheal 100,100;
	warp "bat_b01",87,73;
	end;
}
*/

bat_b01,389,14,3	script	Terapeuta de Batalha#b01_b	95,{
	specialeffect2 EF_HEAL;
	mes "[Terapeuta de Batalha]";
	mes "Agora feche o seus olhos";
	mes "e respire bem fundo.";
	mes "Você pode se libertar da dor agora.";
	close;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	// enablenpc "bat_b01_rp1_b_warp";
	areapercentheal "bat_b01",379,0,399,20,100,100;
	areawarp "bat_b01",379,0,399,20,"bat_b01",312,225;
	end;

// OnTimer26000:
	// disablenpc "bat_b01_rp1_b_warp";
	// end;

OnTimer26500:
	stopnpctimer;
	donpcevent "Terapeuta de Batalha#b01_b::OnEnable";
	end;

OnEnable:
	initnpctimer;
	enablenpc "Terapeuta de Batalha#b01_b";
	end;

OnStop:
	// disablenpc "bat_b01_rp1_b_warp";
	disablenpc "Terapeuta de Batalha#b01_b";
	stopnpctimer;
	end;
}

/*
bat_b01,389,10,0	script	bat_b01_rp1_b_warp	45,10,10,{
OnInit:
	disablenpc "bat_b01_rp1_b_warp";
	end;

OnTouch:
	percentheal 100,100;
	warp "bat_b01",312,225;
	end;
}
*/

bat_b01,87,76,0	script	A_CODE#bat_b01	-1,5,5,{
OnTouch:
	if (checkquest(2070) < 0)
		setquest 2070;
	end;
}

bat_b01,312,224,0	script	B_CODE#bat_b01	-1,5,5,{
OnTouch:
	if (checkquest(2070) < 0)
		setquest 2070;
	end;
}

bat_b01,10,294,3	script	Tenente de Guillaume#b01_a	934,{
	if ($@FlaviusBG1_id1 == getcharid(4)) {
		if ($@FlaviusBG1_Victory == 1)
			callfunc "F_BG_Badge",1,"Guillaume","Flavius",3;
		else
			callfunc "F_BG_Badge",0,"Guillaume","Flavius",3;
	}
	bg_leave;
	if (!getbattleflag("feature.bgqueue"))
		warp "bat_room",154,150;
	end;

OnInit:
	disablenpc "Tenente de Guillaume#b01_a";
	end;
}

bat_b01,389,14,3	script	Tenente de Croix#b01_b	934,{
	if ($@FlaviusBG1_id2 == getcharid(4)) {
		if ($@FlaviusBG1_Victory == 2)
			callfunc "F_BG_Badge",1,"Croix","Flavius",3;
		else
			callfunc "F_BG_Badge",0,"Croix","Flavius",3;
	}
	bg_leave;
	if (!getbattleflag("feature.bgqueue"))
		warp "bat_room",154,150;
	end;

OnInit:
	disablenpc "Tenente de Croix#b01_b";
	end;
}

bat_b01,1,5,3	script	countdown#bat_b01	844,{
OnInit:
	stopnpctimer;
	end;

OnEnable:
	.TimerCount = 0;
	stopnpctimer;
	initnpctimer;
	sleep 7000;
	mapannounce "bat_b01", "Tenente de Guillaume Axl Rose : Vamos atacar para destruir aquele Cristal!",bc_map,"0xFF9900";
	sleep 1000;
	mapannounce "bat_b01", "Tenente de Croix Swandery : Mesmo que Guillaume se esforce para nos derrotar, a vitória será nossa. Vamos mostrar a eles nosso poder.",bc_map,"0xFF99CC";
	end;

OnStop:
	stopnpctimer;
	end;

OnTimer60000:
	.TimerCount++;
	.@BattleTimer = $@FlaviusBG1_BattleTimer;
	if( (.TimerCount+1) >= .@BattleTimer )
		mapannounce "bat_b01", "Marollo VII : Guillaume Marollo, Croix Marollo! E seus seguidores!",bc_map,"0x99CC00";
	else {
		stopnpctimer;
		initnpctimer;
	}
	end;

OnTimer93000:
	mapannounce "bat_b01", "Marollo VII : Ambos exércitos são muito competitivos, por isso é difícil julgar qual equipe é superior.",bc_map,"0x99CC00";
	end;

OnTimer98000:
	mapannounce "bat_b01", "Marollo VII : A batalha de Flavius é uma perda de tempo. Decidirei vitória e derrota por seu progresso.",bc_map,"0x99CC00";
	end;

OnTimer112000:
	mapannounce "bat_b01", "Marollo VII : Se você não pode aceitar os resultados, tente novamente em outra batalha do vale!",bc_map,"0x99CC00";
	end;

OnTimer117000:
	mapannounce "bat_b01", "Axl Rose, Swandery : Sim, senhor.",bc_map,"0x99CC00";
	end;

OnTimer120000:
	if( $@Croix_ScoreBG1 < $@Guill_ScoreBG1 ) {
		set $@FlaviusBG1_Victory, 1;
		mapannounce "bat_b01", "Marollo VII : O Time de Guillaume obteve um melhor desempenho na batalha da Arena de Flavius.",bc_map,"0x99CC00";
	}
	else if( $@Guill_ScoreBG1 < $@Croix_ScoreBG1 ) {
		set $@FlaviusBG1_Victory, 2;
		mapannounce "bat_b01", "Marollo VII : O Time de Croix obteve um melhor desempenho na batalha da Arena de Flavius.",bc_map,"0x99CC00";
	}
	else {
		set $@FlaviusBG1_Victory, 3;
		mapannounce "bat_b01", "Marollo VII : Ambos os times teve o mesmo desempenho na batalha da Arena de Flavius.",bc_map,"0x99CC00";
	}
	
	// Team Ranking
	callfunc "F_BG_RANKING_TEAM", 3, $@FlaviusBG1_id1, $@FlaviusBG1_id2, ($@FlaviusBG1_Victory-1), $@Guill_ScoreBG1, $@Croix_ScoreBG1, true;
	
	donpcevent "time#bat_b01::OnStop";
	bg_warp $@FlaviusBG1_id1,"bat_b01",10,290;
	bg_warp $@FlaviusBG1_id2,"bat_b01",390,10;
	//enablenpc "Vintenar#bat_b01_aover";
	//enablenpc "Vintenar#bat_b01_bover";
	enablenpc "Tenente de Guillaume#b01_a";
	enablenpc "Tenente de Croix#b01_b";
	donpcevent "countdown#bat_b01::OnStop";
	donpcevent "time_out#bat_b01::OnStart";
	if (getbattleflag("feature.bgqueue"))
		bg_reserve "bat_b01", true;
	end;

OnTimer1900000:
	mapwarp "bat_b01","bat_room",154,150;
	donpcevent "countdown#bat_b01::OnStop";
	end;
}

bat_b01,1,6,3	script	time_out#bat_b01	CLEAR_NPC,{
OnStart:
	bg_setcountdown $@FlaviusBG1_id1, 20;
	bg_setcountdown $@FlaviusBG1_id2, 20;
	initnpctimer;
	end;
	
OnStop:
	stopnpctimer;
	end;

OnTimer5000:
	mapannounce "bat_b01", "Marollo VII: Em 15 segundos o Campo de Batalha será esvaziado, fale com o Tenente para pegar sua recompensa!",bc_map,"0x99CC00";
	end;
	
OnTimer10000:
	mapannounce "bat_b01", "Marollo VII: Em 10 segundos o Campo de Batalha será esvaziado, fale com o Tenente para pegar sua recompensa!",bc_map,"0x99CC00";
	end;
	
OnTimer15000:
	mapannounce "bat_b01", "Marollo VII: Em 5 segundos o Campo de Batalha será esvaziado, fale com o Tenente para pegar sua recompensa!",bc_map,"0x99CC00";
	end;
	
OnTimer20000:
	if( getbattleflag("feature.bgqueue") )
		bg_reserve "bat_b01", true;
	donpcevent "countdown#bat_b01::OnStop";
	mapwarp "bat_b01","bat_room",154,150;
	end;
}

bat_b01,81,83,4	script	Acampamento Guillaume#flag21	722,{
	end;

OnBatB01Emblem:
	bg_flagemblem $@FlaviusBG1_id1;
	end;
}

bat_b01,94,83,4	duplicate(Acampamento Guillaume#flag21)	Acampamento Guillaume#flag22	722
bat_b01,81,66,4	duplicate(Acampamento Guillaume#flag21)	Acampamento Guillaume#flag23	722
bat_b01,94,66,4	duplicate(Acampamento Guillaume#flag21)	Acampamento Guillaume#flag24	722
bat_b01,139,142,4	duplicate(Acampamento Guillaume#flag21)	Acampamento Guillaume#flag25	722
bat_b01,139,158,4	duplicate(Acampamento Guillaume#flag21)	Acampamento Guillaume#flag26	722
bat_b01,110,161,4	duplicate(Acampamento Guillaume#flag21)	Acampamento Guillaume#flag27	722
bat_b01,110,137,4	duplicate(Acampamento Guillaume#flag21)	Acampamento Guillaume#flag28	722
bat_b01,63,135,4	duplicate(Acampamento Guillaume#flag21)	Acampamento Guillaume#flag29	722
bat_b01,63,165,4	duplicate(Acampamento Guillaume#flag21)	Acampamento Guillaume#flag30	722
bat_b01,10,296,4	duplicate(Acampamento Guillaume#flag21)	Acampamento Guillaume#flag31	722

bat_b01,306,233,4	script	Acampamento Croix#flag21	722,{
	end;
	
OnBatB01Emblem:
	bg_flagemblem $@FlaviusBG1_id2;
	end;
}

bat_b01,317,233,4	duplicate(Acampamento Croix#flag21)	Acampamento Croix#flag22	722
bat_b01,306,216,4	duplicate(Acampamento Croix#flag21)	Acampamento Croix#flag23	722
bat_b01,317,216,4	duplicate(Acampamento Croix#flag21)	Acampamento Croix#flag24	722
bat_b01,257,158,4	duplicate(Acampamento Croix#flag21)	Acampamento Croix#flag25	722
bat_b01,257,141,4	duplicate(Acampamento Croix#flag21)	Acampamento Croix#flag26	722
bat_b01,297,164,4	duplicate(Acampamento Croix#flag21)	Acampamento Croix#flag27	722
bat_b01,297,136,4	duplicate(Acampamento Croix#flag21)	Acampamento Croix#flag28	722
bat_b01,336,161,4	duplicate(Acampamento Croix#flag21)	Acampamento Croix#flag29	722
bat_b01,336,139,4	duplicate(Acampamento Croix#flag21)	Acampamento Croix#flag30	722
bat_b01,389,16,4	duplicate(Acampamento Croix#flag21)	Acampamento Croix#flag31	722

/*
bat_b01,10,294,3	script	Vintenar#bat_b01_aover	419,{
	set .@A_B_gap,$@Guill_ScoreBG1 - $@Croix_ScoreBG1;
	if ($@FlaviusBG1_id1 == getcharid(4)) {
		if (.@A_B_gap > 0)
			callfunc "F_BG_Badge",1,"Guillaume","Flavius",3; //Guillaume wins
		else if (.@A_B_gap == 0)
			callfunc "F_BG_Badge",2,"Guillaume","Flavius",3; //Tie
		else
			callfunc "F_BG_Badge",0,"Guillaume","Flavius",3; //Croix wins
	}
	else {
		mes "[Axl Rose]";
		mes "Por que você está aqui, mercenário de Croix? Tenho certeza da vitória contra os tolos de Croix como você. Ha!";
		close;
	}
	bg_leave;
	if (!getbattleflag("feature.bgqueue"))
		warp "bat_room",154,150;
	end;

OnInit:
	disablenpc "Vintenar#bat_b01_aover";
	end;
}

bat_b01,389,14,3	script	Vintenar#bat_b01_bover	415,{
	set .@A_B_gap,$@Guill_ScoreBG1 - $@Croix_ScoreBG1;
	if ($@FlaviusBG1_id2 == getcharid(4)) {
		if (.@A_B_gap > 0)
			callfunc "F_BG_Badge",0,"Croix","Flavius",3; //Guillaume wins
		else if (.@A_B_gap == 0)
			callfunc "F_BG_Badge",2,"Croix","Flavius",3; //Tie
		else
			callfunc "F_BG_Badge",1,"Croix","Flavius",3; //Croix wins
	}
	else {
		mes "[Swandery]";
		mes "Por que você veio aqui como um Guillaume? Você será enviado para onde deveria estar!";
		close;
	}
	bg_leave;
	if (!getbattleflag("feature.bgqueue"))
		warp "bat_room",154,150;
	end;

OnInit:
	disablenpc "Vintenar#bat_b01_bover";
	end;
}
*/

bat_b01,1,10,3	script	Release all#b01	81,{
	set .@i, callfunc("F_GM_NPC",1854,0);
	if (.@i == -1) {
		mes "Cancelado.";
		close;
	} else if (.@i == 0) {
		end;
	} else {
		mes "Posso ajudá-lo?";
		next;
		switch(select("Liberar Campo de Batalha.:Cancelar.")) {
		case 1:
			mes "Tchau.";
			close2;
			mapwarp "bat_b01","bat_room",154,150;
			end;
		case 2:
			mes "Cancelado.";
			close;
		}
	}
}

// BG Queue makes these scripts useless
-	script	BGQueueInit#flavius01	-1,{
	end;

OnInit:
	if (getbattleflag("feature.bgqueue")) {
		unloadnpc "Tenente Ator";
		unloadnpc "Tenente Thelokus";
		unloadnpc "back_bgroomb01a";
		unloadnpc "back_bgroomb01b";
		unloadnpc "A_CODE#bat_b01";
		unloadnpc "B_CODE#bat_b01";
	}
	end;
}

//===== rAthena Script =======================================
//= BattleGround System - Flavius
//===== Description: =========================================
//= [Official Conversion]
//= Flavius Battleground.
//= - Winning Team: 9 badges (11 if VIP)
//= - Losing Team: 3 badges (5 if VIP)
//===== Changelogs: ==========================================
//= 1.0 First Version. [L0ne_W0lf]
//= 1.1 Fixed pink crystal spawning as blue. [L0ne_W0lf]
//= 1.2 Updated 'waitingroom' to support required zeny/lvl. [Kisuka]
//= 1.3 Removed MaxLvl check in waitingrooms. Replaced setwall with setcell.
//= 1.4 Label standardization. [Euphy]
//= 1.5 Added GM management function. [Euphy]
//= 1.6 Added VIP features and created a reward function. [Euphy]
//= 1.7 Made Crystals immune to attacks until Guardians are defeated. [Cydh/Aleos]
//= 1.8 Added battle_config checks to allow this script to use the new queue interface or the previous method. [Aleos]
//============================================================