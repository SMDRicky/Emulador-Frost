//===== RSM Mod ==============================================
//= BattleGround Expansion - Kingdom
//===== Por: =================================================
//= RomuloSM (sbk_)
//===== Versão Atual: ========================================
//= 1.0
//===== Compátivel com: ======================================
//= rAthena RSM Mod
//===== Descrição: ===========================================
//= Simula a Guerra do Emperium Primeira Edição do Castelo de
//= Kriemhild.
//===== Comentários Adicional: ===============================
//= 1.0 Primeira versão.[RomuloSM]
//===== Contato: =============================================
//= Email: romulodevel@gmail.com
//= Facebook: https://www.facebook.com/RomuloDevel
//============================================================

bat_room02,57,227,5	script	Tenente Rhuan	418,{
	end;
OnStart:
	.@MaxPlayer = $@KingdomBG1_MaxPlayer + 1;
	.@MinBaseLevel = $@KingdomBG1_MinBaseLevel;
	.@MaxBaseLevel = $@KingdomBG1_MaxBaseLevel;
	waitingroom "Estação de Batalha",.@MaxPlayer,"start#bat_g01::OnReadyCheck",1,0,.@MinBaseLevel,.@MaxBaseLevel;
	end;
	
OnEnterBG:
	set $@KingdomBG1_id1, waitingroom2bg(BG_TEAM_A,"bat_g01",26,130,"start#bat_g01::OnGuillaumeQuit","");
	end;
}

bat_room02,58,204,1	script	Tenente Urd	414,{
	end;
OnStart:
	.@MaxPlayer = $@KingdomBG1_MaxPlayer + 1;
	.@MinBaseLevel = $@KingdomBG1_MinBaseLevel;
	.@MaxBaseLevel = $@KingdomBG1_MaxBaseLevel;
	waitingroom "Estação de Batalha",.@MaxPlayer,"start#bat_g01::OnReadyCheck",1,0,.@MinBaseLevel,.@MaxBaseLevel;
	end;
	
OnEnterBG:
	set $@KingdomBG1_id2, waitingroom2bg(BG_TEAM_B,"bat_g01",187,14,"start#bat_g01::OnCroixQuit","");
	end;
}

bat_room02,57,220,0	warp	back_bgroomg01a	1,1,bat_room02,154,150
bat_room02,57,211,0	warp	back_bgroomg01b	1,1,bat_room02,154,150

bat_room02,1,151,3	script	#bat_g01_timer	844,{
	end;

OnInit:
	if (getbattleflag("feature.bgqueue"))
		end;
OnEnable:
	initnpctimer;
	end;

OnStop:
	stopnpctimer;
	end;

OnTimer1000:
	stopnpctimer;
	if (bg_get_data($@KingdomBG1_id1, 0) == 0 && bg_get_data($@KingdomBG1_id2, 0) == 0) {
		donpcevent "countdown#bat_g01::OnStop";
		donpcevent "time_out#bat_g01::OnStop";
		if (getbattleflag("feature.bgqueue"))
			bg_reserve "bat_g01", true;
		$@KingdomBG1 = 0;
		$@KingdomBG1_Team = 0;
		$@KingdomBG1_Victory = 0;
		$@KingdomBG1_Guill = 0;
		$@KingdomBG1_Croix = 0;
		bg_updatescore "bat_g01", 0, 0;
		if( $@KingdomBG1_id1 ) { bg_destroy $@KingdomBG1_id1; $@KingdomBG1_id1 = 0; }
		if( $@KingdomBG1_id2 ) { bg_destroy $@KingdomBG1_id2; $@KingdomBG1_id2 = 0; }
		if (getbattleflag("feature.bgqueue"))
			bg_unbook "bat_g01";
		end;
	}
	initnpctimer;
	end;
}

// Kingdom Kriemhild Battleground Engine
//============================================================
bat_g01,1,1,3	script	start#bat_g01	844,{
OnInit:
	mapwarp "bat_g01","bat_room",154,150;
	setcell "bat_g01", 77, 137, 95, 119, cell_nobgbattle, 1;
	setcell "bat_g01", 18, 135, 33, 124, cell_nobgskillcall, ($@KingdomBG1_NoSkillCall ? 1 : 0);
	setcell "bat_g01", 180, 23, 195, 8, cell_nobgskillcall, ($@KingdomBG1_NoSkillCall ? 1 : 0);
	end;

OnEnable:
	disablenpc "Tenente#bat_g01_a";
	disablenpc "Tenente#bat_g01_b";
	donpcevent "::OnBatG01Emblem";
	donpcevent "Emperium#bat_g01::OnKill";
	donpcevent "Emperium#bat_g01::OnEnable";
	donpcevent "Guardian#bat_g01::OnKill";
	donpcevent "time#bat_g01::OnEnable";
	bg_setcountdown $@KingdomBG1_id1, $@KingdomBG1_BattleTimer*60;
	bg_setcountdown $@KingdomBG1_id2, $@KingdomBG1_BattleTimer*60;
	donpcevent "countdown#bat_g01::OnEnable";
	end;

OnGuillaumeActive:
	warp "bat_g01",86,126;
	end;

OnCroixActive:
	warp "bat_g01",86,126;
	end;

OnGuillaumeQuit:
	// Ranking
	callfunc "F_BG_RANKING", 11, getcharid(0), 0, 1, $@KingdomBG1_Guill, $@KingdomBG1_Croix;

	if (getbattleflag("feature.bgqueue"))
		bg_desert;
	else
		bg_leave;
	end;

OnCroixQuit:
	// Ranking
	callfunc "F_BG_RANKING", 11, getcharid(0), 1, 1, $@KingdomBG1_Guill, $@KingdomBG1_Croix;

	if (getbattleflag("feature.bgqueue"))
		bg_desert;
	else
		bg_leave;
	end;

OnReadyCheck:
	if( $@KingdomBG1 )
		end;
	
	if (!getbattleflag("feature.bgqueue")) {
		set .@Guillaume, getwaitingroomstate(0,"Tenente Rhuan");
		set .@Croix, getwaitingroomstate(0,"Tenente Urd");
		if( !.@Guillaume && !.@Croix ) {
			donpcevent "#bat_g01_timer::OnStop";
			end;
		}
		else if( .@Guillaume < $@KingdomBG1_MaxPlayer || .@Croix < $@KingdomBG1_MaxPlayer )
			end;
	}
	set $@KingdomBG1,1;
	if (!getbattleflag("feature.bgqueue")) {
		donpcevent "Tenente Rhuan::OnEnterBG";
		donpcevent "Tenente Urd::OnEnterBG";
	}
	donpcevent "start#bat_g01::OnEnable";
	bg_warp $@KingdomBG1_id1,"bat_g01",86,126;
	bg_warp $@KingdomBG1_id2,"bat_g01",86,126;
	initnpctimer;
	end;

OnTimer10000:
	stopnpctimer;																																																																
	donpcevent "#bat_g01_timer::OnEnable";				
	end;																		
}

// Emperium
//============================================================
bat_g01,1,2,3	script	Emperium#bat_g01	844,{
OnEnable:
	if( $@KingdomBG1_Team ) {
		.@UID = bg_monster($@KingdomBG1_Team, "bat_g01", 197, 197, "Emperium", 1288, "Emperium#bat_g01::OnMyMobDead");
		bg_setmobdesc .@UID, "Kingdom Kriemhild";
	}
	else
		monster "bat_g01", 197, 197, "Emperium", 1288, 1, "Emperium#bat_g01::OnMyMobDead";
	end;
	
OnKill:
	killmonster "bat_g01", "Emperium#bat_g01::OnMyMobDead";
	end;
	
OnMyMobDead:
	if( $@KingdomBG1_Team != getcharid(4) ) {
		$@KingdomBG1_Team = getcharid(4);
		if( getcharid(4) == $@KingdomBG1_id1 ) {
			.@TeamName$ = "Guillaume";
			$@KingdomBG1_Guill++;
			bg_warp $@KingdomBG1_id1,"bat_g01",197,197;
			bg_warp $@KingdomBG1_id2,"bat_g01",187,14;
		} else {
			.@TeamName$ = "Croix";
			$@KingdomBG1_Croix++;
			bg_warp $@KingdomBG1_id1,"bat_g01",26,130;
			bg_warp $@KingdomBG1_id2,"bat_g01",197,197;
		}
		donpcevent "time#bat_g01::OnStop";
		donpcevent "time#bat_g01::OnEnable";
		donpcevent "::OnBatG01Emblem";
		donpcevent "Guardian#bat_g01::OnKill";
		donpcevent "Emperium#bat_g01::OnEnable";
		bg_updatescore "bat_g01", $@KingdomBG1_Guill, $@KingdomBG1_Croix;
		mapannounce "bat_g01", "O Emperium foi destruído pelo Exército de " + .@TeamName$ + ".",bc_map,"0xFFCE00";
		sleep 2000;
		mapannounce "bat_g01", "Capitão de " + .@TeamName$ + ", convocar Guardiões aumenta na defesa do Castelo.",bc_map,"0xFFCE00";
	}
	end;
}

// Guardian
//============================================================
bat_g01,1,3,3	script	Guardian#bat_g01	844,{
	end;

OnKill:
	for( set .@i,0; .@i <= 8; set .@i,.@i+1 ) {
		killmonster "bat_g01","Guardian#bat_g01::OnMobDead0"+.@i;
	}
	end;
	
OnMobDead01: OnMobDead02: OnMobDead03: OnMobDead04:
OnMobDead05: OnMobDead06: OnMobDead07: OnMobDead08:
	end;
}

bat_g01,112,181,0	script	Ahvray#bat_g01	55,{
	setarray .@guardiantype[0],1,1,1,2,2,3,3,3;
	setarray .@guardianposx[0],182,182,153,59,50,184,196,107;
	setarray .@guardianposy[0],62,116,86,28,36,183,189,179;
	
	if( $@KingdomBG1_Team == 0 ) {
		mes "[Ahvray]";
		mes "Eu estava esperando um mestre para cumprir o meu destino.";
		mes "Alma corajosa... o destino o guiará em direção ao seu futuro...";
		close;
	}
	
	.@CID = bg_get_data($@KingdomBG1_Team, 3);
	if( .@CID != getcharid(0) ) {
		mes "[Ahvray]";
		mes "Não importa o quanto você me incomode, sou leal ao Capitão ^ff0000"+strcharinfo(0,.@CID)+" do Exército de " + ($@KingdomBG1_Team == $@KingdomBG1_id1 ? "Guillaume" : "Croix") + "^000000. Aonde estão os Guardiões?! Leve estes intrusos para fora agora mesmo!";
		close;
	}
	mes "[Ahvray]";
	mes "Bem-vindo. Capitão, ^ff0000"+strcharinfo(0)+"^000000...";
	mes "Seu humilde servo, Ahvray, está aqui para servi-lo.";
	next;
	select("Convocar Guardiões");
	mes "[Ahvray]";
	mes "Você irá convocar um Guardião? Vai ser um protetor para nos defender lealmente.";
	mes "Por favor selecione um guardião para nos defender.";
	next;
	for ( set .@i,0; .@i <= 7; set .@i,.@i+1 ) {
		if (.@guardiantype[.@i] == 1) { set .@type$, "Guardião Soldado"; }
		else if (.@guardiantype[.@i] == 2) { set .@type$, "Guardião Arqueiro"; }
		else { set .@type$, "Guardião Espadachim"; }
		if( mobcount("bat_g01","Guardian#bat_g01::OnMobDead0"+(.@i+1)) ) {
			setarray .@gname$[.@i], .@type$ + " - Implementado";
		}
		else {
			setarray .@gname$[.@i], .@type$ + " - Não Implementado";
		}
	}
	set .@menu$, .@gname$[0]+":"+.@gname$[1]+":"+.@gname$[2]+":"+.@gname$[3]+":"+.@gname$[4]+":"+.@gname$[5]+":"+.@gname$[6]+":"+.@gname$[7];
	set .@gnum, select(.@menu$)-1;
	mes "[Ahvray]";
	mes "Você vai convocar o guardião escolhido? 10,000 zeny é requirido para convocar um Guardião.";
	next;
	switch (select("Convocar:Cancelar")) {
	case 1:
		if (mobcount("bat_g01","Guardian#bat_g01::OnMobDead0"+(.@gnum+1)) == 1) {
			mes "[Ahvray]";
			mes "Capitão, você já convocou este Guardião. Não podemos convocar outro.";
			close;
		}
		if (Zeny < 10000) {
			mes "[Ahvray]";
			mes "Bem... Sinto muito, mas não temos fundos para convocar o Guardião. Não conseguimos convocar o Guardião.";
			close;
		}
		set Zeny, Zeny-10000;
		if (.@guardiantype[.@gnum] == 1) { set .@type, 1287; }
		else if (.@guardiantype[.@gnum] == 2) { set .@type, 1285; }
		else { set .@type, 1286; }
		.@UID = bg_monster($@KingdomBG1_Team,"bat_g01",.@guardianposx[.@gnum],.@guardianposy[.@gnum],strmobinfo(2,.@type),.@type,"Guardian#bat_g01::OnMobDead0"+(.@gnum+1));
		bg_setmobdesc .@UID, "Kingdom Kriemhild";
		mes "[Ahvray]";
		mes "Completamos a convocação do Guardião. Agora nossas defesas aumentaram com ele no lugar.";
		close;
	case 2:
		mes "[Ahvray]";
		mes "Eu fiz o que você pediu. Mas lembre-se se você tem dinheiro de sobra, vai ser melhor convocá-lo.";
		close;
	}
}

// Warps
//============================================================
bat_g01,57,19,0	warp	bat_g01#w_01	1,1,bat_g01,80,49
bat_g01,41,34,0	warp	bat_g01#w_02	1,1,bat_g01,40,47
bat_g01,37,54,0	warp	bat_g01#w_03	1,1,bat_g01,192,119
bat_g01,71,54,0	warp	bat_g01#w_04	1,1,bat_g01,192,65
bat_g01,51,70,0	warp	bat_g01#w_05	1,1,bat_g01,202,92
bat_g01,84,49,0	warp	bat_g01#w_06	1,1,bat_g01,61,19
bat_g01,37,47,0	warp	bat_g01#w_07	1,1,bat_g01,45,34
bat_g01,62,34,0	warp	bat_g01#w_08	1,1,bat_g01,192,119
bat_g01,84,19,0	warp	bat_g01#w_09	1,1,bat_g01,192,65
bat_g01,206,92,0	warp	bat_g01#w_10	1,1,bat_g01,55,70
bat_g01,196,65,0	warp	bat_g01#w_11	1,1,bat_g01,75,54
bat_g01,196,119,0	warp	bat_g01#w_12	1,1,bat_g01,40,54
bat_g01,147,120,0	warp	bat_g01#w_13	1,1,bat_g01,75,187
bat_g01,75,183,0	warp	bat_g01#w_14	1,1,bat_g01,147,116
bat_g01,109,163,0	warp	bat_g01#w_15	1,1,bat_g01,202,183
bat_g01,206,183,0	warp	bat_g01#w_16	1,1,bat_g01,113,163
bat_g01,85,134,0	warp	bat_g01#w_17	1,1,bat_g01,99,32
bat_g01,103,32,0	warp	bat_g01#w_18	1,1,bat_g01,86,126

// Flags
//============================================================
bat_g01,58,56,4	script	Kingdom Kriemhild#bat_g01_01	722,{
	if( !$@KingdomBG1_Team ) {
		mes "[Edital do Reino Divino de Rune-Midgard]";
		mes " ";
		mes "1. Ordenança do divino Reino de Rune-Midgard, ";
		mes "Declaramos que";
		mes "não está formalizado um mestre neste castelo.";
		mes " ";
		mes "2. Para o único que pode ";
		mes "superar todas as provações";
		mes "e destruir o Emperium,";
		mes "o rei irá dotar aquele";
		mes "com a posse deste castelo.";
		close;
	}
	.@CID = bg_get_data($@KingdomBG1_Team,3);
	mes "[Edital do Reino Divino de Rune-Midgard]";
	mes " ";
	mes "1. Ordenança do divino Reino de Rune-Midgard,";
	mes "nós aprovamos a defesa deste lugar sobre Comando do ^FF0000Exército de " + ($@KingdomBG1_Team == $@KingdomBG1_id1 ? "Guillaume" : "Croix") + "^000000.";
	mes " ";
	mes "2. O Mestre desse castelo é o ^FF0000Capitão " + strcharinfo(0,.@CID) + "^000000.";
	mes "Se há alguém que se opõe a isso, que prove sua força e honra com uma lâmina em sua mão.";
	close;
	
OnBatG01Emblem:
	bg_flagemblem $@KingdomBG1_Team;
	end;
}

bat_g01,64,56,4	duplicate(Kingdom Kriemhild#bat_g01_01)	Kingdom Kriemhild#bat_g01_02	722
bat_g01,76,32,4	duplicate(Kingdom Kriemhild#bat_g01_01)	Kingdom Kriemhild#bat_g01_03	722
bat_g01,84,32,4	duplicate(Kingdom Kriemhild#bat_g01_01)	Kingdom Kriemhild#bat_g01_04	722
bat_g01,94,39,4	duplicate(Kingdom Kriemhild#bat_g01_01)	Kingdom Kriemhild#bat_g01_05	722
bat_g01,94,24,4	duplicate(Kingdom Kriemhild#bat_g01_01)	Kingdom Kriemhild#bat_g01_06	722
bat_g01,73,14,4	duplicate(Kingdom Kriemhild#bat_g01_01)	Kingdom Kriemhild#bat_g01_07	722
bat_g01,73,6,4	duplicate(Kingdom Kriemhild#bat_g01_01)	Kingdom Kriemhild#bat_g01_08	722
bat_g01,55,46,4	duplicate(Kingdom Kriemhild#bat_g01_01)	Kingdom Kriemhild#bat_g01_09	722
bat_g01,45,46,4	duplicate(Kingdom Kriemhild#bat_g01_01)	Kingdom Kriemhild#bat_g01_10	722

// Guillaume Flags
//============================================================
bat_g01,23,138,4	script	Base de Guillaume#bat_g01_a01	722,{
	end;
	
OnBatG01Emblem:
	bg_flagemblem $@KingdomBG1_id1;
	end;
}

bat_g01,28,138,4	duplicate(Base de Guillaume#bat_g01_a01)	Base de Guillaume#bat_g01_a02	722

// Croix Flags
//============================================================
bat_g01,185,22,4	script	Base de Croix#bat_g01_b01	722,{
	end;
	
OnBatG01Emblem:
	bg_flagemblem $@KingdomBG1_id2;
	end;
}

bat_g01,190,22,4	duplicate(Base de Croix#bat_g01_b01)	Base de Croix#bat_g01_b02	722

// Neutral Flags
//============================================================
bat_g01,81,130,4	script	Kingdom Kriemhild#bat_g01_n01	722,{
	mes "^FF0000[Kingdom Kriemhild]^000000";
	mes "Esta Batalha Campal é uma simulação da Guerra do Emperium Primeira Edição no Castelo de Kriemhild.";
	mes " ";
	mes "Este ponto é neutro e também o ponto de partida, nenhum jogador pode se enfrentar enquanto estiver nesta área.";
	close;
	
OnBatG01Emblem:
	bg_flagemblem $@KingdomBG1_id1;
	end;
}

bat_g01,90,130,4	script	Kingdom Kriemhild#bat_g01_n02	722,{
	mes "^FF0000[Kingdom Kriemhild]^000000";
	mes "Esta Batalha Campal é uma simulação da Guerra do Emperium Primeira Edição no Castelo de Kriemhild.";
	mes " ";
	mes "Este ponto é neutro e também o ponto de partida, nenhum jogador pode se enfrentar enquanto estiver nesta área.";
	close;
	
OnBatG01Emblem:
	bg_flagemblem $@KingdomBG1_id2;
	end;
}

// Terapeura
//============================================================
bat_g01,1,5,1	script	time#bat_g01	844,{
OnEnable:
	donpcevent "Terapeuta da Batalha#bat_g01_a::OnEnable";
	donpcevent "Terapeuta da Batalha#bat_g01_b::OnEnable";
	end;

OnStop:
	donpcevent "Terapeuta da Batalha#bat_g01_a::OnStop";
	donpcevent "Terapeuta da Batalha#bat_g01_b::OnStop";
	end;
}

bat_g01,26,135,3	script	Terapeuta da Batalha#bat_g01_a	95,{
	specialeffect2 EF_HEAL;
	mes "[Terapeuta da Batalha]";
	mes "Apenas feche seus olhos";
	mes "e respire fundo.";
	mes "Você pode se libertar da dor.";
	close;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	areapercentheal "bat_g01",18,135,33,124,100,100;
	areawarp "bat_g01",18,135,33,124,"bat_g01",86,126;
	end;

OnTimer26500:
	stopnpctimer;
	donpcevent "Terapeuta da Batalha#bat_g01_a::OnEnable";
	end;

OnEnable:
	initnpctimer;
	enablenpc "Terapeuta da Batalha#bat_g01_a";
	end;

OnStop:
	disablenpc "Terapeuta da Batalha#bat_g01_a";
	stopnpctimer;
	end;
}

bat_g01,187,19,3	script	Terapeuta da Batalha#bat_g01_b	95,{
	specialeffect2 EF_HEAL;
	mes "[Terapeuta da Batalha]";
	mes "Apenas feche seus olhos";
	mes "e respire fundo.";
	mes "Você pode se libertar da dor.";
	close;

OnTimer25000:
	specialeffect EF_SANCTUARY;
	areapercentheal "bat_g01",180,23,195,8,100,100;
	areawarp "bat_g01",180,23,195,8,"bat_g01",86,126;
	end;

OnTimer26500:
	stopnpctimer;
	donpcevent "Terapeuta da Batalha#bat_g01_b::OnEnable";
	end;

OnEnable:
	initnpctimer;
	enablenpc "Terapeuta da Batalha#bat_g01_b";
	end;

OnStop:
	disablenpc "Terapeuta da Batalha#bat_g01_b";
	stopnpctimer;
	end;
}

// Countdown
//============================================================
bat_g01,1,6,3	script	countdown#bat_g01	844,{
OnInit:
	stopnpctimer;
	end;

OnEnable:
	.TimerCount = 0;
	stopnpctimer;
	initnpctimer;
	sleep 7000;
	mapannounce "bat_g01", "Tenente de Guillaume Axl Rose : Vamos conquistar este castelo!",bc_map,"0xFF9900";
	sleep 1000;
	mapannounce "bat_g01", "Tenente de Croix Swandery : Mesmo que Guillaume se esforce para nos derrotar, a vitória será nossa. Vamos mostrar a eles nosso poder.",bc_map,"0xFF99CC";
	end;

OnStop:
	stopnpctimer;
	end;
	
OnTimer60000:
	.TimerCount++;
	.@BattleTimer = $@KingdomBG1_BattleTimer;
	if( (.TimerCount+1) >= .@BattleTimer )
		mapannounce "bat_g01", "Marollo VII : Guillaume Marollo, Croix Marollo! E seus seguidores!",bc_map,"0x99CC00";
	else {
		stopnpctimer;
		initnpctimer;
	}
	end;

OnTimer93000:
	mapannounce "bat_g01", "Marollo VII : Ambos Exércitos são muito competitivos, por isso é difícil julgar qual grupamento é superior.",bc_map,"0x99CC00";
	end;

OnTimer98000:
	mapannounce "bat_g01", "Marollo VII : Grandiosa batalha em Kingdom Kriemhild. Difícil decidir a quem pertence a vitória.",bc_map,"0x99CC00";
	end;

OnTimer112000:
	mapannounce "bat_g01", "Marollo VII : Se vocês não aceitarem esse resultado, tentem a sorte em outra batalha!",bc_map,"0x99CC00";
	end;

OnTimer117000:
	mapannounce "bat_g01", "Axl Rose, Swandery : Sim, senhor.",bc_map,"0x99CC00";
	end;

OnTimer120000:
	if( $@KingdomBG1_Guill > $@KingdomBG1_Croix ) {
		$@KingdomBG1_Victory = 1;
		mapannounce "bat_g01", "Marollo VII : O Exército de Guillaume foi o vencedor!",bc_map,"0x99CC00";
	}
	else if( $@KingdomBG1_Croix > $@KingdomBG1_Guill ) {
		$@KingdomBG1_Victory = 2;
		mapannounce "bat_g01", "Marollo VII : O Exército de Croix foi o vencedor!",bc_map,"0x99CC00";
	}
	else {
		$@KingdomBG1_Victory = 3;
		mapannounce "bat_g01", "Marollo VII: Esta batalha terminou em empate.",bc_map,"0x99CC00";
	}
	$@KingdomBG1_Team = 0;
	callfunc "F_BG_RANKING_TEAM", 11, $@KingdomBG1_id1, $@KingdomBG1_id2, ($@KingdomBG1_Victory-1), $@KingdomBG1_Guill, $@KingdomBG1_Croix, true;
	donpcevent "::OnBatG01Emblem";
	donpcevent "Guardian#bat_g01::OnKill";
	donpcevent "Emperium#bat_g01::OnKill";
	donpcevent "time#bat_g01::OnStop";
	donpcevent "time_out#bat_g01::OnStart";
	enablenpc "Tenente#bat_g01_a";
	enablenpc "Tenente#bat_g01_b";
	bg_warp $@KingdomBG1_id1, "bat_g01", 26, 130;
	bg_warp $@KingdomBG1_id2, "bat_g01", 187, 14;
	end;
}

bat_g01,1,7,3	script	time_out#bat_g01	844,{
OnStart:
	bg_setcountdown $@KingdomBG1_id1, 20;
	bg_setcountdown $@KingdomBG1_id2, 20;
	initnpctimer;
	end;
	
OnStop:
	stopnpctimer;
	end;

OnTimer5000:
	mapannounce "bat_g01", "Marollo VII: Em 15 segundos o Campo de Batalha será esvaziado, fale com o Tenente para pegar sua recompensa!",bc_map,"0x99CC00";
	end;
	
OnTimer10000:
	mapannounce "bat_g01", "Marollo VII: Em 10 segundos o Campo de Batalha será esvaziado, fale com o Tenente para pegar sua recompensa!",bc_map,"0x99CC00";
	end;
	
OnTimer15000:
	mapannounce "bat_g01", "Marollo VII: Em 5 segundos o Campo de Batalha será esvaziado, fale com o Tenente para pegar sua recompensa!",bc_map,"0x99CC00";
	end;
	
OnTimer20000:
	if( getbattleflag("feature.bgqueue") )
		bg_reserve "bat_g01", true;
	donpcevent "countdown#bat_g01::OnStop";
	mapwarp "bat_g01","bat_room",154,150;
	end;
}

// Rewards
//============================================================
bat_g01,26,135,3	script	Tenente#bat_g01_a	419,{
	if ($@KingdomBG1_id1 == getcharid(4)) {
		if ($@KingdomBG1_Victory == 1)
			callfunc "F_BG_Badge",1,"Guillaume","Kingdom",11; //Guillaume wins
		else if ($@KingdomBG1_Victory == 2)
			callfunc "F_BG_Badge",0,"Guillaume","Kingdom",11; //Croix wins
		else
			callfunc "F_BG_Badge",2,"Guillaume","Kingdom",11; //Tie
	}
	else {
		mes "[Axl Rose]";
		mes "Por que você está aqui, mercenário de Croix? Tenho certeza de uma vitória contra os tolos de Croix. Ha!";
		close;
	}
	bg_leave;
	if (!getbattleflag("feature.bgqueue"))
		warp "bat_room",154,150;
	end;

OnInit:
	disablenpc "Tenente#bat_g01_a";
	end;
}

bat_g01,187,19,3	script	Tenente#bat_g01_b	415,{
	if ($@KingdomBG1_id2 == getcharid(4)) {
		if ($@KingdomBG1_Victory == 1)
			callfunc "F_BG_Badge",0,"Croix","Kingdom",11; //Guillaume wins
		else if ($@KingdomBG1_Victory == 2)
			callfunc "F_BG_Badge",1,"Croix","Kingdom",11; //Croix wins
		else
			callfunc "F_BG_Badge",2,"Croix","Kingdom",11; //Tie
	}
	else {
		mes "[Swandery]";
		mes "Por que você veio aqui como um Guillaume? Você será enviado para onde deveria estar!";
		close;
	}
	bg_leave;
	if (!getbattleflag("feature.bgqueue"))
		warp "bat_room",154,150;
	end;

OnInit:
	disablenpc "Tenente#bat_g01_b";
	end;
}

// Release
//============================================================
bat_g01,1,8,3	script	Release all#ba_g01	81,{
	set .@i, callfunc("F_GM_NPC",1854,0);
	if (.@i == -1) {
		mes "Cancelado.";
		close;
	} else if (.@i == 0) {
		end;
	} else {
		mes "Posso ajudá-lo?";
		next;
		switch(select("Liberar Campo de Batalha.:Cancelar.")) {
		case 1:
			mes "Tchau.";
			close2;
			mapwarp "bat_g01","bat_room",154,150;
			end;
		case 2:
			mes "Cancelado.";
			close;
		}
	}
}

// BG Queue makes these scripts useless
//============================================================
-	script	BGQueueInit#kingdom01	-1,{
	end;

OnInit:
	if (getbattleflag("feature.bgqueue")) {
		unloadnpc "Tenente Rhuan";
		unloadnpc "Tenente Urd";
		unloadnpc "back_bgroomg01a";
		unloadnpc "back_bgroomg01b";
	}
	end;
}