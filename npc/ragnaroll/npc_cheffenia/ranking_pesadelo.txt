//==========================================================|
//    _    _                     _     _                    |
//   | |  | |                   | |   (_)                   |
//   | |__| |_   _ _ __ ___  ___| |__  _ _ __ ___   __ _    |
//   |  __  | | | | '__/ _ \/ __| '_ \| | '_ ` _ \ / _` |   |
//   | |  | | |_| | | | (_) \__ \ | | | | | | | | | (_| |   |
//   |_|  |_|\__, |_|  \___/|___/_| |_|_|_| |_| |_|\__,_|   |
//            __/ |Scripter                                 |
//           |___/                                          |
//==========================================================|
/* ------------------ SCRIPT INFO ------------------------- =
Name: Ranking Dificil                                              =
Ver : 1.1                                                   =
---------------------------------------------------------- */
//==========================================================|
que_temsky,99,149,5	script	Cheffenia Dificil	10441,{

	mes "^00BFFF[ ::: TOP CHEFFENIA Dificil ::: ]^000000";
	
	query_sql "SELECT `name`,SUM(kills) as total FROM `rank_mvp` GROUP BY `name` ORDER BY total DESC LIMIT "+.TopShow+"",.@name$,.@total;
	query_sql "SELECT SUM(kills) as total FROM `rank_mvp`",.@rTotal;
	
	if(!.@rTotal){ mes "^FF0000* Sem registros..."; close; }
	
	for(set .@i,0; .@i<getarraysize(.@name$); set .@i,.@i+1)
	{ mes ""+(.@i+1)+". "+.@name$[.@i]+" ^B22222["+.@total[.@i]+"]^000000"; }
	
	next;
	set .@mn,select("Estatísticas pessoais:Filtrar por MvP ^B22222["+.@rTotal+"]^000000"+(getgroupid() >= .GMA ? ":^B22222[ GM ]^000000 Top Player Filtro":"")+"");
	
	switch(.@mn)
	{
		case 1:
				next;
				mes "   ^00BFFF[ ::: Estatísticas Pessoais ::: ]^000000";
				query_sql "SELECT `mob_id`,`kills` FROM `rank_mvp` WHERE `char_id`="+getcharid(0)+" ORDER BY `kills` DESC",.@mob,.@kill;
				
				if(!getarraysize(.@mob)){ mes "^FF0000* Você ainda não matou mvps."; close; }
				
				for(set .@i,0; .@i<getarraysize(.@mob); set .@i,.@i+1)
				{ mes ""+(.@i+1)+". "+getmonsterinfo(.@mob[.@i],MOB_NAME)+" ^B22222["+.@kill[.@i]+"]^000000"; }
				message strcharinfo(0),"Total "+getarraysize(.@mob)+" MVP(s)";
				close;
		
		case 2:
				query_sql "SELECT `mob_id`,SUM(kills) as total FROM `rank_mvp` GROUP BY `mob_id` ORDER BY total DESC",.@mob,.@kill;
				
				for(set .@i,0; .@i<getarraysize(.@mob); set .@i,.@i+1)
				{
					set .@mvp_slt$,.@mvp_slt$+(.@i+1)+". "+getmonsterinfo(.@mob[.@i],MOB_NAME)+" ^B22222["+.@kill[.@i]+"]^000000:";
					setarray .@mvp_list[.@i],.@mob[.@i];
				}
				
				freeloop(1);
				while(true)
				{
					set .@slt,select(.@mvp_slt$)-1;
					next;
					
					mes "^00BFFF[ MVP - "+getmonsterinfo(.@mvp_list[.@slt],MOB_NAME)+" ]^000000";
					
					query_sql "SELECT `name`,`kills` FROM `rank_mvp` WHERE `mob_id`="+.@mvp_list[.@slt]+" ORDER BY `kills` DESC",.@nick$,.@kills;
					
					for(set .@i,0; .@i<getarraysize(.@nick$); set .@i,.@i+1)
					{ mes ""+(.@i+1)+". "+.@nick$[.@i]+" ^B22222["+.@kills[.@i]+"]^000000"; }
					
					deletearray .@nick$[0],getarraysize(.@nick$);
					deletearray .@kills[0],getarraysize(.@kills);
				}
				freeloop(0);
				close;
		
		case 3:
				query_sql "SELECT `name`,SUM(kills) as total FROM `rank_mvp` GROUP BY `name` ORDER BY total DESC LIMIT "+.TopShow+"",.@name$,.@total;
				
				for(set .@i,0; .@i<getarraysize(.@name$); set .@i,.@i+1)
				{
					set .@nick_slt$,.@nick_slt$+(.@i+1)+". "+.@name$[.@i]+":";
					setarray .@cid_list$[.@i],.@name$[.@i];
				}
				
				set .@slt,select(.@nick_slt$)-1;
				
				mes "^00BFFF[ "+.@cid_list$[.@slt]+" - Estatística ]^000000";
				
				query_sql "SELECT `mob_id`,`kills` FROM `rank_mvp` WHERE `name`='"+.@cid_list$[.@slt]+"' ORDER BY `kills` DESC",.@mob,.@kill;
				
				for(set .@i,0; .@i<getarraysize(.@mob); set .@i,.@i+1)
				{ mes ""+(.@i+1)+". "+getmonsterinfo(.@mob[.@i],MOB_NAME)+" ^B22222["+.@kill[.@i]+"]^000000"; }
				close;
	}


OnInit:

	//Defina o número de jogadores que será exibido na classificação do npc.
	set .TopShow,30;
	
	//Min GM Level para acessar a opção de filtro top jogadores
	set .GMA,90;
	setunitdata getnpcid(0), UNPC_GROUP_ID, 56;
	setunittitle getnpcid(0), "[Ranking Cheffenia]";
end;
}


// Kill MvP Control
//===
-	script	MvPCountKill	FAKE_NPC,{

OnClock0000:

	if(!.reset || gettime(DT_DAYOFMONTH) != 1) end;
	
	query_sql "SELECT `name`,`char_id`,SUM(kills) as total FROM `rank_mvp` GROUP BY `name` ORDER BY total DESC LIMIT "+.reward_rank+"",.@name$,.@cid,.@total;
	
	if(!getarraysize(.@cid)) end;
	
	announce "[ Ranking Cheffenia Dificil ]: O rank foi resetado, premiação enviada para o rodex!",bc_all;
	sleep 1500;
	announce "[ Ranking Cheffenia Dificil ]: Aqui está o top "+.reward_rank+" MvP Kill!",bc_all;
	
	for(set .@i,0; .@i<getarraysize(.@cid); set .@i,.@i+1)
	{
		announce "[ Ranking Cheffenia Dificil ]: Rank "+(.@i+1)+"º "+.@name$[.@i]+" ~ "+callfunc("F_InsertComma",.@total[.@i])+" kills",bc_all;
		setarray getd(".@rwdid"+.@i+""),.rwdID[.@i];
		setarray getd(".@rwdqt"+.@i+""),.rwdQT[.@i];
		mail .@cid[.@i],"[ Ranking Cheffenia Dificil ]","Premiação Mensal","["+gettimestr("%d/%m/%Y %H:%M:%S",21)+"]:\r Parabéns pela sua classificação: "+(.@i+1)+"º",0,getd(".@rwdid"+.@i+""),getd(".@rwdqt"+.@i+"");
		sleep 350;
	}
	query_sql "TRUNCATE TABLE `rank_mvp`";
	end;
	

OnNPCKillEvent:

	set .@idx,inarray(.MvPs[0],killedrid);
	if(.@idx == -1) end;
	
	query_sql "SELECT `kills` FROM `rank_mvp` WHERE `mob_id`="+.MvPs[.@idx]+" AND `char_id`="+getcharid(0)+"",.@kills;
	
	if(.@kills)
	{
		dispbottom "[ Ranking Cheffenia Dificil ]: Você matou "+(.@kills+1)+"x "+getmonsterinfo(killedrid,MOB_NAME)+".";
		query_sql "UPDATE `rank_mvp` SET `kills`=`kills`+1 WHERE `mob_id`="+killedrid+" AND `char_id`="+getcharid(0)+"";
	}
	else
	{
		dispbottom "[ Ranking Cheffenia Dificil ]: Você matou seu primeiro "+getmonsterinfo(killedrid,MOB_NAME)+"!";
		query_sql "INSERT INTO `rank_mvp` VALUES (NULL,"+getcharid(3)+","+getcharid(0)+",'"+strcharinfo(0)+"',"+killedrid+",1);";
	}
	
	if(.show_mvp_kill)
		announce "[ Ranking Cheffenia Dificil ]: O jogador ("+strcharinfo(0)+")"+(getcharid(1)?" do grupo ("+strcharinfo(1)+")":"")+" derrotou o MvP ("+getmonsterinfo(killedrid,MOB_NAME)+") no mapa "+strcharinfo(3)+"",bc_all;

	if(.extra_drop[0])
	{
		for(set .@i,0; .@i<getarraysize(.extra_drop); set .@i,.@i+3)
		{
			set .@sort,rand(1,10000);
			if(.@sort <= .extra_drop[(.@i+2)])
			{
				getitem .extra_drop[.@i],.extra_drop[(.@i+1)];
				if(.@sort <= .extra_drop_show && .extra_drop[(.@i+2)] < 10000)
				{
					set .@DropBCalc,(.extra_drop[(.@i+2)]*100)/10000;
					set .@DropRCalc$,""+(.@DropBCalc >= 100 ? 100:""+.@DropBCalc+""+(((.extra_drop[(.@i+2)]*100)%10000) ? (","+substr(""+((.extra_drop[(.@i+2)]*100)%10000),0,1)):"")+"")+"";
					announce "[ Ranking Cheffenia Dificil ]: "+strcharinfo(0)+" recebeu "+.extra_drop[(.@i+1)]+"x "+getitemname(.extra_drop[.@i])+" ("+.@DropRCalc$+"%) ao derrotar ("+getmonsterinfo(killedrid,MOB_NAME)+")!",bc_all;
				}
			}
		}
	}
	end;

OnInit:

	//Lista mvp_ids
	setarray .MvPs[0],20840,20841,20842,20843,20844,20845,20846,20847,20848,20849,20850,20851,20852,20853,20854,20855,20856,20857,20858,20859,20860,
					20861,20862,20863,20864,20865;
	
	//Mostrar anúncio quando quando um player matar o mvp (0= no, 1= yes)
	//==
	set .show_mvp_kill,0;
	
	//Drop extra para quem matar o mvp. (item_id, quantidade, chance)
	//== (1= 0,01%, 100= 1%, 5000= 50%, 10000= 100%)
	//==
	setarray .extra_drop[0],
		18141,10,1500,	//1500 = 15%
		17139,5,200,		//2200 = 22%
		18140,20,1000,	//10000= 100%
		35004,50,1;		//75   = 0,75%
	
	//Anunciar quando o player conseguir um item da lista de drop extra X% (1= 0.01%, 5000= 50%)
	//==
	set .extra_drop_show,2200; //0,50%
	
	//Reset ranking todo inicio do mês? (0= não, 1= sim)
	//== Reset feito todo dia 1 do mês
	//==
	set .reset,1;
	
	//Premiação Top Rank (especifique até que posição irá premiar, padrão: top 10)
	//==
	set .reward_rank,10;
	
	//Premiação para o Reset Mensal
	//== (recomendo adicionar uma box pois será enviado para o rodex do player)
	//== a premiação deve ser preenchida com base na quantidade de top rank que irá premiar definido em (.reward_rank)
	//==
	//setarray .rwdID[0],35004,35004,35004,35004,35004,35004,35004,35004,35004,35004;	//item_id Rank 1~10
	//setarray .rwdQT[0],200,150,120,100,80,70,60,50,40,30;							//qtd  Rank 1~10
	setarray .rwdID[0],16394,16394,16394,16394,16394,16394,16394,16394,16394,16394;	//item_id Rank 1~10
	setarray .rwdQT[0],10,9,8,7,6,5,4,3,2,1;							//qtd  Rank 1~10
	
	//Check table
	query_sql "CREATE TABLE IF NOT EXISTS `rank_mvp` (`id` int(15) NOT NULL AUTO_INCREMENT,`account_id` INT(12) NOT NULL,`char_id` INT(12) NOT NULL,`name` VARCHAR(28) NOT NULL DEFAULT '',`mob_id` MEDIUMINT(12) NOT NULL,`kills` INT(9) NULL DEFAULT 0, PRIMARY KEY (`id`))";
}