//===== rAthena Script =======================================
//= Extended Vending & Auto Vend
//===== Por: =================================================
//= RomuloSM (sbk_)
//===== Versão Atual: ========================================
//= 1.0
//===== Compátivel com: ======================================
//= rAthena RSM Mod
//===== Descrição: ===========================================
//= Função relacionada ao sistema de Extended Vending & Auto Vend.
//===== Comentários Adicional: ===============================
//= 1.0 Primeira versão.[RomuloSM]
//===== Contato: =============================================
//= Email: romulodevel@gmail.com
//= Site: https://rsmdeveloper.com.br
//= Facebook: https://www.facebook.com/RomuloDevel
//============================================================
-	script	Vending System#bank	-1,{
OnInit:
	setarray $@VendTypes[0], VENDING_BANK_SELL, VENDING_BANK_BUYINGSOTORE, VENDING_BANK_STALL_SELL, VENDING_BANK_STALL_BUYINGSTORE_OK, VENDING_BANK_STALL_BUYINGSTORE_FAIL;
	setarray $@VendNames$[0], "Venda Presencial", "Busca de Vendedores", "Loja de Rua", "Busca de Rua", "Reembolso em Busca de Rua";
	
	bindatcmd "@cashinfo", "Vending System#bank::OnCmd", 99, 99;
	end;
	
OnCmd:
	dispbottom "Você possuí " + #CASHPOINTS + " pontos de Cash.";
	end;

OnPayment:
OnPCLoginEvent:
	.@CID = getcharid(0);
	setarray $@VendTypes[0], VENDING_BANK_SELL, VENDING_BANK_BUYINGSOTORE, VENDING_BANK_STALL_SELL, VENDING_BANK_STALL_BUYINGSTORE_OK, VENDING_BANK_STALL_BUYINGSTORE_FAIL;
	setarray $@VendNames$[0], "Venda Presencial", "Busca de Vendedores", "Loja de Rua", "Busca de Rua", "Reembolso em Busca de Rua";

	for( .@t=0; .@t < getarraysize($@VendTypes); .@t++ ) {
		deletearray .@nameid;
		deletearray .@value;
		query_sql "SELECT `nameid`, `value` FROM `vending_bank` WHERE `char_id`='" + .@CID + "' AND `type`='" + $@VendTypes[.@t] + "'", .@nameid, .@value;
		
		.@size = getarraysize(.@nameid);
		if( !.@size )
			continue;
			
		for( .@i=0; .@i < .@size; .@i++ ) {
			if( .@value[.@i] <= 0 )
				continue;
					
			if( getbattleflag("item_zeny") && getbattleflag("item_zeny") == .@nameid[.@i] ) {
				if( (Zeny + .@value[.@i]) > MAX_ZENY )
					.@rescue = MAX_ZENY - Zeny;
				else
					.@rescue = .@value[.@i];
					
				if( .@rescue <= 0 ) {
					dispbottom "Você tem " + callfunc("F_InsertComma", .@value[.@i]) + " zenys de " + $@VendNames$[.@t] + " para resgatar, porém você atingiu o limite máximo.", 0xFF0000;
					continue;
				}
				
				Zeny += .@rescue;
				.@rest = .@value[.@i] - .@rescue;
				if( .@rescue )
					dispbottom "Você recebeu " + callfunc("F_InsertComma", .@rescue) + " zenys de " + $@VendNames$[.@t] + ".", 0x03D8E0;
				if( .@rest <= 0 )
					query_sql "DELETE FROM `vending_bank` WHERE `nameid`='" + .@nameid[.@i] + "' AND `char_id`='" + .@CID + "'";
				else {
					query_sql "UPDATE `vending_bank` SET `value`='" + .@rest + "' WHERE `nameid`='" + .@nameid[.@i] + "' AND `char_id`='" + .@CID + "'";
					dispbottom "Você tem " + callfunc("F_InsertComma", .@rest) + " zenys de " + $@VendNames$[.@t] + " para sacar, porém você atingiu o limite máximo.", 0xFF0000;
				}
			}
			else if( getbattleflag("item_cash") && getbattleflag("item_cash") == .@nameid[.@i] ) {
				if( (#CASHPOINTS + .@value[.@i]) > MAX_CASHPOINT )
					.@rescue = MAX_CASHPOINT - #CASHPOINTS;
				else
					.@rescue = .@value[.@i];
				
				if( .@rescue <= 0 ) {
					dispbottom "Você tem " + callfunc("F_InsertComma", .@value[.@i]) + " Pontos de Cash de " + $@VendNames$[.@t] + " para sacar, porém você atingiu o limite máximo.", 0xFF0000;
					continue;
				}
				
				#CASHPOINTS += .@rescue;
				.@rest = .@value[.@i] - .@rescue;
				if( .@rescue )
					dispbottom "Você recebeu " + callfunc("F_InsertComma", .@rescue) + " Pontos de Cash de " + $@VendNames$[.@t] + ".", 0x03D8E0;
				if( .@rest <= 0 ) {
					query_sql "DELETE FROM `vending_bank` WHERE `nameid`='" + .@nameid[.@i] + "' AND `char_id`='" + .@CID + "' AND `type`='" + $@VendTypes[.@t] + "'";
				}
				else {
					query_sql "UPDATE `vending_bank` SET `value`='" + .@rest + "' WHERE `nameid`='" + .@nameid[.@i] + "' AND `char_id`='" + .@CID + "' AND `type`='" + $@VendTypes[.@t] + "'";
					dispbottom "Você tem " + callfunc("F_InsertComma", .@rest) + " Pontos de Cash de " + $@VendNames$[.@t] + " para sacar, porém você atingiu o limite máximo.", 0xFF0000;
				}
			}
			else {
				if( !checkweight(.@nameid[.@i],.@value[.@i]) ) {
					dispbottom "Você tem " + callfunc("F_InsertComma", .@value[.@i]) + " " + getitemname(.@nameid[.@i]) + " de " + $@VendNames$[.@t] + " para sacar, porém você atingiu o limite máximo.", 0xFF0000;
					continue;
				}
				
				getitem .@nameid[.@i], .@value[.@i];
				dispbottom "Você recebeu " + callfunc("F_InsertComma", .@value[.@i]) + " " + getitemname(.@nameid[.@i]) + " de " + $@VendNames$[.@t] + ".", 0x03D8E0;
				query_sql "DELETE FROM `vending_bank` WHERE `nameid`='" + .@nameid[.@i] + "' AND `char_id`='" + .@CID + "' AND `type`='" + $@VendTypes[.@t] + "'";
			}
		}
	}
	end;
}

prontera,178,216,4	script	Sistema de Venda#main	-1,{
	.@loop_1 = 1;
	.@CID = getcharid(0);
	.@zenyItem = getbattleflag("item_zeny");
	.@cashItem = getbattleflag("item_cash");
	while(.@loop_1) {
		mes "[Sistema de Venda]";
		mes "Bem-vindo a ^0000FFSistema de Venda^000000.";
		
		deletearray .@nameid;
		query_sql "SELECT `nameid`, `value`, `type` FROM `vending_bank` WHERE `char_id`='" + .@CID + "'", .@nameid, .@value, .@type;
		
		.@size = getarraysize(.@nameid);
		if( !.@size ) {
			mes "No momento não há saldo para sacar de suas vendas.";
			close;
		}
		
		mes "Há saldos para sacar de sua venda.";
		mes "Em que posso te ajudar?";
		next;
		.@buildmenu$ = "";
		for( .@i=0; .@i < .@size; .@i++ )
			.@buildmenu$ += "- Sacar " + getitemname(.@nameid[.@i]) + ".:";
		.@buildmenu$ += "^FF0000- Cancelar.^000000";
		.@i = select(.@buildmenu$)-1;
		
		if( .@i >= .@size )
			break;
			
		.@loop_2 = 1;
		while(.@loop_2) {
			mes "[Sistema de Venda]";
			mes "Você possuí ^0000FF" + callfunc("F_InsertComma", .@value[.@i]) + "x " + getitemname(.@nameid[.@i]) + "^000000.";
			if( .@zenyItem && .@zenyItem == .@nameid[.@i] ) {
				if( (Zeny + 1) > MAX_ZENY ) {
					mes "No momento você não pode mais sacar nenhuma quantia, pois você possuí o máximo permitido.";
					next;
					break;
				}
			}
			else if( .@cashItem && .@cashItem == .@nameid[.@i] ) {
				if( (#CASHPOINTS + 1) > MAX_CASHPOINT ) {
					mes "No momento você não pode mais sacar nenhuma quantia, pois você possuí o máximo permitido.";
					next;
					break;
				}
			}
			else {
				if( !checkweight(.@nameid[.@i],1) ) {
					mes "No momento você não pode mais sacar nenhuma quantia, pois você está no seu limite de peso.";
					next;
					continue;
				}
			}
			mes "Digite o valor que deseja sacar ou ^FF00000 para cancelar^000000.";
			next;
			input .@amount;
			
			if( .@amount <= 0 ) {
				.@loop_2 = 0;
				break;
			}
			
			mes "[Sistema de Venda]";
			if( .@amount > .@value[.@i] ) {
				mes "Você não possuí saldo suficiente, por favor informe outro valor.";
				next;
				continue;
			}
			
			if( .@zenyItem && .@zenyItem == .@nameid[.@i] ) {
				if( (Zeny + .@amount) > MAX_ZENY ) {
					mes "Você não pode sacar esta quantidade.";
					mes "No momento você pode sacar no máximo ^0000FF" + callfunc("F_InsertComma", (MAX_ZENY - Zeny)) + " Zenys^000000, por favor informe outro valor.";
					next;
					continue;
				}
			}
			else if( .@cashItem && .@cashItem == .@nameid[.@i] ) {
				if( (#CASHPOINTS + .@amount) > MAX_CASHPOINT ) {
					mes "Você não pode sacar esta quantidade.";
					mes "No momento você pode sacar no máximo ^0000FF" + callfunc("F_InsertComma", (MAX_CASHPOINT - #CASHPOINTS)) + " Pontos de Cash^000000, por favor informe outro valor.";
					next;
					continue;
				}
			}
			else {
				if( !checkweight(.@nameid[.@i],.@amount) ) {
					mes "Você não pode sacar esta quantidade, pois excede o limite de peso.";
					mes "Por favor, informe outro valor.";
					next;
					continue;
				}
			}
			
			mes "Tem certeza que deseja sacar ^0000FF" + callfunc("F_InsertComma", .@amount) + "x " + getitemname(.@nameid[.@i]) + "^000000?";
			next;
			switch( select("^0000FF- Sim, por favor.^000000", "- Não, escolher outro valor.", "^FFA500- Escolher outro item para saque.^000000", "^FF0000- Cancelar.^000000") ) {
				case 1:
					if( .@zenyItem && .@zenyItem == .@nameid[.@i] )
						Zeny += .@amount;
					else if( .@cashItem && .@cashItem == .@nameid[.@i] )
						#CASHPOINTS += .@amount;
					else
						getitem .@nameid[.@i], .@amount;

					.@rest = .@value[.@i] - .@amount;
					
					mes "[Sistema de Venda]";
					mes "Você sacou ^0000FF" + .@amount + "x " + getitemname(.@nameid[.@i]) + "^000000.";
					if( .@rest <= 0 )
						query_sql "DELETE FROM `vending_bank` WHERE `nameid`='" + .@nameid[.@i] + "' AND `char_id`='" + .@CID + "' AND `type`='" + .@type[.@i] + "'";
					else {
						query_sql "UPDATE `vending_bank` SET `value`='" + .@rest + "' WHERE `nameid`='" + .@nameid[.@i] + "' AND `char_id`='" + .@CID + "' AND `type`='" + .@type[.@i] + "'";
						mes "Você ainda possuí ^FF0000" + callfunc("F_InsertComma", .@rest) + " " + getitemname(.@nameid[.@i]) + "^000000 para sacar.";
					}
					next;
					.@loop_2 = 0;
					break;
				case 2:
					break;
				case 3:
					.@loop_2 = 0;
					break;
				case 4:
					.@loop_1 = 0;
					.@loop_2 = 0;
					break;
			}
		}
	}
	
	mes "[Sistema de Venda]";
	mes "Estarei aqui caso precisar.";
	close;
}