//===== rAthena Script =======================================
//= Sistema de Player Bot
//===== Por: =================================================
//= RomuloSM (sbk_)
//===== Versão Atual: ========================================
//= 1.0
//===== Compátivel com: ======================================
//= rAthena RSM Mod
//===== Descrição: ===========================================
//= Cria Bots com formato de jogadores para ajudar no
//= crescimento do servidor.
//===== Nota: ================================================
//= Converte e vende itens para visual.                                                        
//===== Comentários Adicional: ===============================
//= 1.0 Primeira versão.[RomuloSM]
//= 2.0 Correções e Otmizações. [RomuloSM]
//= 3.0 Correções e Otmizações. [RomuloSM]
//===== Contato: =============================================
//= Email: romulodevel@gmail.com
//= Site: https://www.rsmdeveloper.com.br
//= Facebook: https://www.facebook.com/RomuloDevel
//============================================================
-	script	Players Bot#main	-1,{
OnLoad:
OnInterIfInitOnce:
	deletearray $BotPlayersUID;
	deletearray $BotPlayersMap$;
	setarray .@Bots$[0],
		// <Name>, <Class>, <BaseLevel>, <JobLevel>, <Sex>, <HairStyle>, <HairColor>, <Clother>, <HeadBottom>, <HeadMiddle>, <HeadTop>, <Mapname>, <CoordX>, <CoordY>, <Dir>, <Mode>, <WaitingRoom Name>, "<GuildId>", "<GuildEmblemId>", "<GuildName>", "<GuildPosition>"
		// <Mode>:
		//		0 - Walk
		//		1 - Sit
		//		2 - Waiting Room (stand)
		//		3 - Waiting Room (sit)
		//		4 - Stand
		//
		// <GuildEmblemId>: Emblem ID, you need to create a guild and use the @botguildinfo command to get the information.
		//
		"Lucian", Job_Novice, 99, 50, 1, 0, 0, 0, 0, 0, 0, "gld2_prt", 156, 191, 4, 0, "", 1, 1, "Os Furiosos", "Guild Master",
		"20matar70correr", Job_Novice, 99, 50, 0, 0, 0, 0, 0, 0, 0, "gld2_prt", 156, 191, 4, 0, "", 1, 1, "Os Furiosos", "Position 2",
		"Iruza", Job_Swordman, 99, 50, 1, 0, 0, 0, 0, 0, 0, "gld2_prt", 156, 191, 4, 0, "", 1, 1, "Os Furiosos", "Position 3",
		"Dilmas Is My", Job_Swordman, 80, 50, 0, 1, 1, 1, 8, 0, 0, "gld2_prt", 146, 189, 4, 3, "", 1, 1, "Faiz o Pix", "Guild Lider",
		"SeuZee", Job_Swordman, 80, 50, 0, 1, 1, 1, 8, 0, 0, "gld2_prt", 146, 189, 4, 3, "", 1, 1, "Faiz o Pix", "Guild Lider",
		"Donas", Job_Swordman, 80, 50, 0, 1, 1, 1, 8, 0, 0, "gld2_prt", 146, 189, 4, 3, "", 1, 1, "Faiz o Pix", "Guild Lider",
		"01aS", Job_Swordman, 80, 50, 0, 1, 1, 1, 8, 0, 0, "gld2_prt", 146, 189, 4, 3, "", 1, 1, "Faiz o Pix", "Guild Lider",
		"000Ome", Job_Swordman, 80, 50, 0, 1, 1, 1, 8, 0, 0, "gld2_prt", 146, 189, 4, 3, "", 1, 1, "Faiz o Pix", "Guild Lider",
		"OffMets", Job_Swordman, 80, 50, 0, 1, 1, 1, 8, 0, 0, "gld2_prt", 146, 189, 4, 3, "", 1, 1, "Faiz o Pix", "Guild Lider",
		"Dizliz", Job_Swordman, 80, 50, 0, 1, 1, 1, 8, 0, 0, "gld2_prt", 146, 189, 4, 3, "", 1, 1, "Faiz o Pix", "Guild Lider",
		"Xandeco", Job_Archer, 88, 42, 1, 3, 4, 2, 7, 0, 0, "gld2_prt", 150, 185, 2, 4, "", 1, 1, "Os Furiosos", "Artilheiro",
		"Belatrix", Job_Mage, 92, 47, 0, 1, 2, 1, 4, 0, 0, "gld2_prt", 151, 184, 4, 0, "", 1, 1, "Mestres do Caos", "Feiticeira",
		"Zordak", Job_Thief, 77, 39, 1, 5, 5, 2, 0, 0, 0, "gld2_prt", 153, 183, 1, 0, "", 1, 1, "Os Furiosos", "Espião",
		"Levy", Job_Acolyte, 81, 41, 0, 2, 1, 0, 2, 0, 0, "gld2_prt", 154, 182, 3, 0, "", 1, 1, "Divinos", "Curandeira",
		"ToxicOne", Job_Merchant, 90, 45, 1, 4, 2, 1, 1, 0, 0, "gld2_prt", 149, 186, 0, 2, "", 1, 1, "Mercadores", "Negociante",
		"ShinTenshi", Job_Swordman, 95, 50, 0, 6, 3, 0, 0, 0, 0, "gld2_prt", 152, 187, 4, 3, "", 1, 1, "Faiz o Pix", "Duelista",
		"NovaEra", Job_Archer, 84, 43, 1, 0, 2, 1, 3, 0, 0, "gld2_prt", 147, 184, 1, 2, "", 1, 1, "Os Furiosos", "Atirador",
		"Yasminne", Job_Mage, 86, 44, 0, 3, 2, 2, 6, 0, 0, "gld2_prt", 155, 185, 0, 4, "", 1, 1, "Mestres do Caos", "Conjuradora",
		"DeadlyRook", Job_Thief, 89, 47, 1, 2, 5, 0, 9, 0, 0, "gld2_prt", 148, 183, 2, 1, "", 1, 1, "Sombras do Medo", "Lâmina",
		"Samantha", Job_Acolyte, 91, 46, 0, 4, 1, 1, 1, 0, 0, "gld2_prt", 150, 183, 3, 3, "", 1, 1, "Divinos", "Sacerdotisa";

	.@c = 0;
	.@m = 0;
	deletearray $BotUID[0];
	query_sql "DELETE FROM `player_bot`";

	for( .@i=0; .@i < getarraysize(.@Bots$); .@i+=21 ) {
		.@Name$ = .@Bots$[.@i];
		.@Class = atoi(.@Bots$[.@i+1]);
		.@BaseLevel = atoi(.@Bots$[.@i+2]);
		.@JobLevel = atoi(.@Bots$[.@i+3]);
		.@Sex = atoi(.@Bots$[.@i+4]);
		.@HairStyle = atoi(.@Bots$[.@i+5]);
		.@HairColor = atoi(.@Bots$[.@i+6]);
		.@Clother = atoi(.@Bots$[.@i+7]);
		.@HeadBottom = atoi(.@Bots$[.@i+8]);
		.@HeadMiddle = atoi(.@Bots$[.@i+9]);
		.@HeadTop = atoi(.@Bots$[.@i+10]);
		.@MapName$ = .@Bots$[.@i+11];
		.@CoordX = atoi(.@Bots$[.@i+12]);
		.@CoordY = atoi(.@Bots$[.@i+13]);
		.@Dir = atoi(.@Bots$[.@i+14]);
		.@Mode = atoi(.@Bots$[.@i+15]);
		.@WaitingRoom$ = .@Bots$[.@i+16];
		.@GuildId = atoi(.@Bots$[.@i+17]);
		.@EmblemId = atoi(.@Bots$[.@i+18]);
		.@GuildName$ = .@Bots$[.@i+19];
		.@GuildPosition$ = .@Bots$[.@i+20];
		
		monster .@MapName$,.@CoordX,.@CoordY,.@Name$,1002,1,"Players Bot#main::OnKill";
		.@UID = $@mobid[0];

		setunitdata .@UID, UMOB_CLASS, .@Class;
		setunitdata .@UID, UMOB_LEVEL, .@BaseLevel;
		setunitdata .@UID, UMOB_JOBLEVEL, .@JobLevel;
		setunitdata .@UID, UMOB_SEX, .@Sex;
		setunitdata .@UID, UMOB_DMGIMMUNE, 1;
		setunitdata .@UID, UMOB_SPEED, 170;
		setunitdata .@UID, UMOB_HAIRSTYLE, .@HairStyle;
		setunitdata .@UID, UMOB_HAIRCOLOR, .@HairColor;
		setunitdata .@UID, UMOB_CLOTHCOLOR, .@Clother;
		setunitdata .@UID, UMOB_LOOKDIR, .@Dir;
		setunitdata .@UID, UMOB_HEADBOTTOM, .@HeadBottom;
		setunitdata .@UID, UMOB_HEADMIDDLE, .@HeadMiddle;
		setunitdata .@UID, UMOB_HEADTOP, .@HeadTop;
		setunitdata .@UID, UMOB_PLAYER_BOT, 1;
		
		if( .@Mode == 1 || .@Mode == 3 )
			setunitdata .@UID, UMOB_DEADSIT, 2;
		if( .@Mode == 2 || .@Mode == 3 )
			mobwaitingroom .@UID, .@WaitingRoom$, 20;
		if( .@Mode == 2 || .@Mode == 3 || .@Mode == 4 ) {
			unitstopwalk .@UID, USW_FORCE_STOP;
			setunitdata .@UID, UMOB_MODE, MD_NORANDOMWALK;
		}
		
		if( .@GuildName$ != ""  && .@GuildPosition$ != "" )
			mobfakeguild .@UID, .@GuildId, .@EmblemId, .@GuildName$, .@GuildPosition$;

		$BotPlayersUID[.@c] = .@UID;
		.@c++;
		
		if( inarray($BotPlayersMap$,.@MapName$) == -1 ) {
			$BotPlayersMap$[.@m] = .@MapName$;
			.@m++;
		}
		
		query_sql "INSERT INTO `player_bot` (`name`, `mapname`, `coord_x`, `coord_y`, `class`, `sex`, `base_level`, `job_level`) VALUES ('" + .@Name$ + "', '" + .@MapName$ + "', " + .@CoordX + ", " + .@CoordY + ", " + .@Class + ", " + .@Sex + ", " + .@BaseLevel + ", " + .@JobLevel + ")";
	}
	end;
	
OnInit:
	bindatcmd "botreload", "Players Bot#main::OnReloadCmd", 99, 99;
	bindatcmd "botkillall", "Players Bot#main::OnKillAllCmd", 99, 99;
	bindatcmd "botguildinfo", "Players Bot#main::OnGuildInfoCmd", 99, 99;
	initnpctimer;
	end;
	
OnGuildInfoCmd:
	.@GuildId = getcharid(2);
	.@EmblemId = getguildemblemid(.@GuildId);
	dispbottom "Guild Id: " + .@GuildId;
	dispbottom "Emblem Id: " + .@EmblemId;
	end;
	
OnReloadCmd:
	for( .@i=0; .@i < getarraysize($BotPlayersMap$); .@i++ ) {
		killmonster $BotPlayersMap$[.@i],"Players Bot#main::OnKill";
	}
	donpcevent "Players Bot#main::OnLoad";
	message strcharinfo(0), "The bots will be reloaded.";
	end;
	
OnKillAllCmd:
	for( .@i=0; .@i < getarraysize($BotPlayersMap$); .@i++ ) {
		killmonster $BotPlayersMap$[.@i],"Players Bot#main::OnKill";
	}
	deletearray $BotPlayersUID;
	deletearray $BotPlayersMap$;
	message strcharinfo(0), "Bots have been removed.";
	end;
	
OnKill:
	end;
	
OnTimer5000:
	stopnpctimer;
	initnpctimer;
	query_sql "DELETE FROM `player_bot`";
	
	if( getarraysize($BotPlayersUID) ) {
		for( .@i=0; .@i < getarraysize($BotPlayersUID); .@i++ ) {
			getunitdata $BotPlayersUID[.@i], .@infos;
			.@Name$ = getunitname($BotPlayersUID[.@i]);
			.@MapId = .@infos[UMOB_MAPID];
			.@MapName$ = mapid2name(.@MapId);
			.@CoordX = .@infos[UMOB_X];
			.@CoordY = .@infos[UMOB_Y];
			.@Class = .@infos[UMOB_CLASS];
			.@Sex = .@infos[UMOB_SEX];
			.@BaseLevel = .@infos[UMOB_LEVEL];
			.@JobLevel = .@infos[UMOB_JOBLEVEL];
			query_sql "INSERT INTO `player_bot` (`name`, `mapname`, `coord_x`, `coord_y`, `class`, `sex`, `base_level`, `job_level`) VALUES ('" + .@Name$ + "', '" + .@MapName$ + "', " + .@CoordX + ", " + .@CoordY + ", " + .@Class + ", " + .@Sex + ", " + .@BaseLevel + ", " + .@JobLevel + ")";
		}
	}
	end;
}
	