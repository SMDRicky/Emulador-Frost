-	script	Leablem#dummy::SocketEnchant2	-1,{
	if (checkweight(1201,1) == 0) {
		mes "Espere um segundo !!";
		mes "Você está muito pesado, assim, você não pode obter mais itens.";
		mes "Deixe alguns itens no depósito e tente fale comigo novamente.";
		close;
	}
	mes "[Leablem]";
	mes "Bom dia.";
	mes "Meu nome é Leablem, eu sou um técnico que se especializou em adicionar slots para armas e armaduras.";
	mes "Pode ser muito difícil, mas eu gosto do que eu faço, e tenho orgulho.";
	next;
	mes "[Leablem]";
	mes "Eu aprendi muitas coisas com meu irmão, Seyablem, mas acho que ainda tenho muitas coisas a aprender.";
	mes "Então, por favor, entenda que meus serviços de slot's são limitados, ao contrário do meu irmão Seyableam.";
	next;
	mes "[Leablem]";
	mes "Meu irmão estava tão feliz me ensinando suas habilidades, pois ele estava preocupado que as suas habilidades não seriam as únicas sempre.";
	mes "Ele fez, no entanto, o seu melhor para me ensinar.";
	next;
	mes "[Leablem]";
	mes "Graças a ele, eu aprendi muitas coisas.";
	mes "Eu não estou muito confiante na tentativa de adicionar slot's em coisas diferentes.";
	mes "Mas é isso que eu faço, então vou fazer agora.";
	next;
	mes "[Leablem]";
	mes "De fato, há uma coisa que meu irmão não quer que eu faça.";
	mes "Eu não quero magoá-lo, então...";
	mes "Vamos pular apenas essa parte.";
	mes "Não vou falar mais nada sobre ele!";
	next;
	mes "[Leablem]";
	mes "De qualquer forma, você tem algum equipamento que você gostaria de adicionar slot's?";
	mes "Minha taxa de serviço, os materiais, e a chance de sucesso depende do item específico que estou trabalhando.";
	next;
	switch(select("Arma","Armadura","Sobre a coisa que você pulou","Mais informações","Sair")) {
		case 1:
			mes "[Leablem]";
			mes "Arma, é isso?";
			mes "Certo, sem problemas.";
			mes "As armas são divididas em 4 classes diferentes dependendo da dificuldade do trabalho.";
			mes "A classe C é mais fácil, e a classe S é a mais difícil.";
			mes "Qual classe você gostaria de tentar?";
			next;
			switch(select("C:B:A:S")) {
				case 1:
					mes "[Leablem]";
					mes "Classe C, tudo bem.";
					mes "Então, que tipo de arma você tem?";
					next;
					switch(select("Livro da Mãe Terra","Livro das Ondas","Livro de Golpe de Vento","Livro de Sol Ardente")) {
						case 1: callfunc "Func_Socket2",1554,1569,40,66,200,1010,10,0,0,EQI_HAND_R;
						case 2: callfunc "Func_Socket2",1553,1568,40,66,200,1010,10,0,0,EQI_HAND_R;
						case 3: callfunc "Func_Socket2",1556,1571,40,66,200,1010,10,0,0,EQI_HAND_R;
						case 4: callfunc "Func_Socket2",1555,1570,40,66,200,1010,10,0,0,EQI_HAND_R;
					}
				case 2:
					mes "[Leablem]";
					mes "Você escolheu a classe B - Média.";
					mes "Então, que tipo de arma você tem?";
					next;
					switch(select("Machado dos Orcs:Cimitarra:Clava com Espetos")) {
						case 1: callfunc "Func_Socket2",1304,1309,40,66,200,984,1,0,0,EQI_HAND_R;
						case 2: callfunc "Func_Socket2",1113,1114,40,66,200,984,1,0,0,EQI_HAND_R;
						case 3: callfunc "Func_Socket2",1523,1538,40,61,300,984,1,0,0,EQI_HAND_R;
					}
				case 3:
					mes "[Leablem]";
					mes "Classe A? Você está querendo demais. Mas, sem problemas.";
					mes "Então, que tipo de arma você tem?";
					next;
					switch(select("Matadora de Dragão:Katar da Espinheira Empoeirada:Katar da Chama Impetuosa:Katar Gélida:Katar do Vento Perfurante:Maça de Ouro:Alaúde Oriental:Chicote da Rainha:Lança Espectral:Gae Bolg:Schweizersabel")) {
						case 1:  callfunc "Func_Socket2",13001,13030,40,61,500,984,2,0,0,EQI_HAND_R;
						case 2:  callfunc "Func_Socket2", 1257, 1276,40,66,500,984,2,0,0,EQI_HAND_R;
						case 3:  callfunc "Func_Socket2", 1258, 1277,40,66,500,984,2,0,0,EQI_HAND_R;
						case 4:  callfunc "Func_Socket2", 1256, 1275,40,66,500,984,2,0,0,EQI_HAND_R;
						case 5:  callfunc "Func_Socket2", 1259, 1278,40,66,500,984,2,0,0,EQI_HAND_R;
						case 6:  callfunc "Func_Socket2", 1524, 1539,40,61,500,984,2,0,0,EQI_HAND_R;
						case 7:  callfunc "Func_Socket2", 1918, 1922,40,61,500,984,2,0,0,EQI_HAND_R;
						case 8:  callfunc "Func_Socket2", 1970, 1976,40,61,500,984,2,0,0,EQI_HAND_R;
						case 9:  callfunc "Func_Socket2", 1477, 1479,40,56,500,984,2,0,0,EQI_HAND_R;
						case 10: callfunc "Func_Socket2", 1474, 1480,40,56,500,984,2,0,0,EQI_HAND_R;
						case 11: callfunc "Func_Socket2", 1167, 1178,40,61,500,984,2,0,0,EQI_HAND_R;
					}
				case 4:
					mes "[Leablem]";
					mes "Hm, Classe S?!";
					mes "Você está falando sério?";
					mes "Então...";
					mes "Qual arma classe S você gostaria de fazer um slot?!";
					next;
					switch(select("Zéfiro:Destruidora de Malhas:Caçadora de Dragões:Destruidor de Espadas:Adaga Assasina:Grande Cruz:Executora")) {
						case 1: callfunc "Func_Socket2",1468, 1481,40,51,1000,984,5,999,10,EQI_HAND_R;
						case 2: callfunc "Func_Socket2",1225,13032,40,51,1000,984,5,999,10,EQI_HAND_R;
						case 3: callfunc "Func_Socket2",1166, 1180,40,51,1000,984,5,999,10,EQI_HAND_R;
						case 4: callfunc "Func_Socket2",1224,13031,40,51,1000,984,5,999,10,EQI_HAND_R;
						case 5: callfunc "Func_Socket2",1232,13033,40,51,1000,984,5,999,10,EQI_HAND_R;
						case 6: callfunc "Func_Socket2",1528, 1540,40,51,1000,984,5,999,10,EQI_HAND_R;
						case 7: callfunc "Func_Socket2",1169, 1179,40,51,1000,984,5,999,10,EQI_HAND_R;
					}
			}
		case 2:
			mes "[Leablem]";
			mes "Armadura, é isso? Certo, sem problemas.";
			mes "As armaduras são divididas em 4 classes diferentes dependendo da dificuldade do trabalho.";
			mes "A classe C é a mais fácil, e a classe S é a mais dificil.";
			mes "Qual classe você gostaria de tentar?";
			next;
			switch(select("C:B:A:S")) {
				case 1:
					mes "[Leablem]";
					mes "Classe C, tudo bem.";
					mes "Então, que tipo de armadura que você tem?";
					next;
					switch(select("Presilha de Girassol:Barrete:Laço Grande:Quepe")) {
						case 1: callfunc "Func_Socket2",2253,5351,40,66,100,999,3,0,0,EQI_HEAD_TOP;
						case 2: callfunc "Func_Socket2",5012,5347,40,66,100,999,3,0,0,EQI_HEAD_TOP;
						case 3: callfunc "Func_Socket2",2244,5348,40,66,100,999,5,0,0,EQI_HEAD_TOP;
						case 4: callfunc "Func_Socket2",5016,5349,40,66,100,999,5,0,0,EQI_HEAD_TOP;
					}
				case 2:
					mes "[Leablem]";
					mes "Você escolheu a classe B - Média.";
					mes "Então, que tipo de armadura você tem?";
					next;
					switch(select("Anel de Caveira:Salto Alto")) {
						case 1: callfunc "Func_Socket2",2609,2715,40,61,300,999,5,0,0,EQI_ACC_R;
						case 2: callfunc "Func_Socket2",2409,2432,40,61,300,999,5,0,0,EQI_SHOES;
					}
				case 3:
					mes "[Leablem]";
					mes "Classe A?";
					mes "Você está querendo demais.";
					mes "Mas, sem problemas.";
					mes "Então, que tipo de armadura você tem?";
					next;
					switch(select("Bandana Pirata:Botas Pretas de Couro"))
					{
						case 1: callfunc "Func_Socket2",2287,5350,50,61,500,985,1,0,0,EQI_HEAD_TOP; // 50,61 - it's not a typo
						case 2: callfunc "Func_Socket2",2425,2434,40,51,500,985,1,0,0,EQI_SHOES;
						//case 3: callfunc "Func_Socket2",15053,15056,x,x,x,x,x;	// Special Ninja Suit (Needs more info)
					}
				case 4:
					mes "[Leablem]";
					mes "Hm, Classe S?!";
					mes "Você está falando sério?";
					mes "Então...";
					mes "Qual armadura de classe S você gostaria de fazer um slot?";
					next;
					switch(select("Vestimenta Arcana:Batina:Spiritus Sancti:Camiseta:Roupa de Baixo"))
					{
						case 1: callfunc "Func_Socket2",2334,2372,40,51,1000,985,1,0,0,EQI_ARMOR; // Mage Coat has 10% success chance
						case 2: callfunc "Func_Socket2",2327,2373,45,51,1000,985,1,0,0,EQI_ARMOR; // the rest has 5%
						case 3: callfunc "Func_Socket2",2111,2128,45,51,1000,985,1,0,0,EQI_HAND_L;
						case 4: callfunc "Func_Socket2",2522,2523,45,51,1000,985,1,0,0,EQI_GARMENT;
						case 5: callfunc "Func_Socket2",2339,2371,45,51,1000,985,1,0,0,EQI_ARMOR;
					}
			}
		case 3:
			mes "[Leablem]";
			mes "Oh, não é nada de especial, mas ele acha que não é algo para se meter com os seres humanos.";
			mes "Eu não quero perturbar meu irmão, então é melhor esquecê-lo.";
			next;
			mes "[Leablem]";
			mes "O própio serviço tem uma chance de sucesso muito alta, mas exige de uma grande quantidade de materiais e uma alta taxa de serviço.";
			mes "Eu dúvido que alguém realmente queira usar o serviço, mesmo se eu abri-lo.";
			next;
			mes "[Leablem]";
			mes "...Ummm...";
			next;
			mes "[Leablem]";
			mes ".............";
			next;
			mes "[Leablem]";
			mes ".............";
			next;
			mes "[Leablem]";
			mes "Você parece interessado em usar o serviço.";
			mes "Se você quiser, eu posso pelo menos falar sobre isso.";
			mes "Mas você deve prometer guardar segredo.";
			mes "Se meu irmão decobre que eu estou dizendo, ele vai me dar uma surra!";
			next;
			mes "[Leablem]";
			mes "Esse serviço especial tem uma chance de sucesso de 90%, mas requer 2 Ouros e 200 milhões de zeny.";
			mes "Eu posso acrescentar slots para o Chapéu do Deus-Sol.";
			next;
			mes "[Leablem]";
			mes "Meu irmão disse que, desde os tempos antigos, é proibido modificar esse chapéu.";
			mes "Mas eu acredito que os seres humanos são muitos curiosos para não experimentar, especialmente quando é proibido.";
			mes "Eu só não quero que meu irmão grite comigo.";
			next;
			switch(select("Usar o serviço:Sair")) {
				case 1:
					mes "[Leablem]";
					mes ".................";
					next;
					mes "[Leablem]";
					mes ".................";
					next;
					mes "[Leablem]";
					mes "...Oq... Oque?";
					mes "Não!";
					mes "Olhando desse jeito não vai me obrigar a fazer isso!";
					mes "Não, Eu disse que não! Não e não! Não!";
					next;
					mes "[Leablem]";
					mes "Não, eu não vou! Não!";
					next;
					mes "[Leablem]";
					mes "Não, eu não vou! Não!";
					next;
					mes "[Leablem]";
					mes "Não, eu não vou! Não!";
					next;
					mes "[Leablem]";
					mes ".................";
					next;
					mes "[Leablem]";
					mes "Umm...";
					next;
					mes "[Leablem]";
					mes "Você é tão persistente.";
					mes "Ok, eu vou te fazer uma exceção.";
					mes "Só não conte a ninguém, especialmente meu irmão. Você promete?";
					next;
					mes "[Leablem]";
					mes "Ok, então, por favor, traga-me ^FF00002 Ouros, 200 Milhões de Zenys -- A taxa de serviço --,";
					mes "^FF0000e um Chapéu do Deus-Sol.";
					mes "^FF0000Você sabe que sua chance de sucesso é de 90%, não sabe?^000000";
					mes "Nós não temos tempo a perder, assim que trazer vamos fazer imediatamente.";
					next;
					if ((Zeny > 199999999) && (countitem(969) > 1) && (countitem(5022) > 0))
					{
						mes "[Leablem]";
						mes "Vamos começar agora?";
						mes "Reze para seus deuses de boa sorte.";
						next;
						set .@a, rand(1,100);
						if ((.@a > 4) && (.@a < 95))
						{
							specialeffect EF_LORD;
							mes "[Leablem]";
							mes "Excelente! Uau, eu acho que a chance de sucesso de 90% é verdade!";
							mes "Olhe, o slot foi inserido no lugar certo.";
							mes "Parabéns.";
							delitem 5022,1; //Coroa do Deus-Sol
							delitem 969,2; //Ouro
							set Zeny, Zeny - 200000000;
							getitem 5353,1; //Coroa do Deus-Sol [1]
							next;
							mes "[Leablem]";
							mes "Esta é a primeira e última vez que eu vou fazer isso por você.";
							mes "Nunca diga a ninguém sobre isso.";
							mes "Está bem?";
							close;
						}
						else
						{
							specialeffect EF_SUI_EXPLOSION;
							mes "[Leablem]";
							mes "Wah! Oh meu Deus, nós falhamos!";
							mes "Meu Deus, acho que tivemos o pior tipo de sorte...";
							mes "O que devemos fazer? ... É por isso que eu não queria fazer em primeiro lugar!";
							delitem 5022,1; //Coroa do Deus-Sol
							delitem 969,2; //Ouro
							set Zeny, Zeny - 200000000;
							next;
							mes "[Leablem]";
							mes "Não, nunca me peça para fazer uma coisa tão arriscada de novo!";
							mes "Bah~";
							close;
						}
					}
					else
					{
						mes "[Leablem]";
						mes "Umm...";
						next;
						mes "[Leablem]";
					mes "Você não tem material suficiente.";
					mes "Rapaz, por que você ainda não me pede para tentar?";
					mes "Eu não vou fazer isso denovo para você, mesmo se você levar material suficiente.";
						close;
					}
				case 2:
					mes "[Leablem]";
					mes "Nunca diga a ninguém sobre o que eu te disse.";
					mes "Esttá bem?";
					close;
			}
		case 4:
			mes "[Leablem]";
			mes "Você quer saber mais informações?";
			mes "Bem, eu não acho que há algo que você especificamente precisa saber.";
			mes "Mais eu posso pelo menos dar uma dica.";
			next;
			mes "[Leablem]";
			mes "Os técnicos de slot em cada cidade proporcionam diferentes possibilidades de sucesso de slot dependendo do nível de seu equipamento.";
			mes "Eu não sei se isso é verdade, mas eu posso ver a diferença em cada cidade.";
			next;
			mes "[Leablem]";
			mes "Por exemplo, um técnico de entalhe em uma cidade proporciona uma maior chance de sucesso itens de equipamentos em níveis mais elevados de armas/armaduras.";
			mes "Enquanto um técnico em outra cidade tem maior sucesso em níveis mais baixos de armas/armaduras.";
			next;
			mes "[Leablem]";
			mes "Quando não for possível adicionar um slot, o equipamento será quebrado e desaparece.";
			mes "Eu tento o meu melhor para ter êxito, mas é dificil trabalhar com slot's.";
			next;
			mes "[Leablem]";
			mes "Oh, há uma coisa importante que você precisa ter em mente.";
			mes "^FF0000Tente não carregar dois equipamentos/itens idênticos ^FF0000quando você adicionar slots.";
			mes "^FF0000Caso contrário, as faixas podem ser adicionados a um dos itens escolhidos aleatoriamente,";
			mes "^FF0000e na pior das hipóteses, você pode perder um item que é mais valioso para você.^000000";
			next;
			mes "[Leablem]";
			mes "Espero que esta informação seja útil para você.";
			mes "Obrigado.";
			close;
		case 5:
			mes "[Leablem]";
			mes "Tudo bem então.";
			close;
	}
}

payon,236,199,3	duplicate(SocketEnchant2)	Leablem#pay	86
lighthalzen,96,137,3	duplicate(SocketEnchant2)	Leablem#lhz	86
prt_in,22,60,6	duplicate(SocketEnchant2)	Leablem#prt	86

function	script	Func_Socket2	{
	.@item_id = getarg(0);
	.@zeny_req = getarg(4);
	setarray .@item_req[0], getarg(5), getarg(7,0);
	setarray .@req_amount[0], getarg(6), getarg(8,0);
	.@type = getarg(9);
	
	disable_items;
	mes "[Leablem]";
	.@location =  .@type;
	.@location = ((getequipid(.@location) != .@item_id && .@type == 8) ? EQP_ACC_L:.@type);
	if(getequipid(.@location) != .@item_id){
		mes "você precisa estar com o item equipado.";
		end;
	}
	.@part = .@location;
	if (.@item_req[1] != 0 && .@req_amount[1] != 0) {
		mes getitemname(.@item_id) +"...";
		mes "Ok, então você precisa me trazer ^FF0000" + (.@zeny_req >= 1000 ? (.@zeny_req /1000 +",000") : .@zeny_req) + ",000 Zeny como taxa de serviço.";
		mes "^FF0000"+ .@req_amount[0] +" para "+ getitemname(.@item_req[0]) +" e "+ .@req_amount[1] +" para "+ getitemname(.@item_req[1]) +".";
		mes "^FF0000Claro, você precisa de um "+ getitemname(.@item_id) +".^000000";
	} else {
		mes getitemname(.@item_id) +"? Ok, então você precisa para me trazer:";
		mes "^FF0000" + (.@zeny_req >= 1000 ? (.@zeny_req/1000 +",000") : .@zeny_req) + ",000 zenys como taxa de serviço e " + .@req_amount[0] + " " + getitemname(.@item_req[0]) + (.@req_amount[0] > 1 && .@item_req[0] != 999 ? "s":"") + " como exigência.";
		mes "^FF0000Claro, você precisa de um " + getitemname(.@item_id) + ".";
		mes "^FF0000Você deve ter todos os itens.^000000";
	}
	next;
	mes "[Leablem]";
	mes "Você trouxe todos os itens?";
	mes "Para sua informação, se você não conseguir criar um slot,";
	mes "você perderá todos os requisitos do item, bem como a "+ ((getiteminfo(.@item_id,5)&EQP_HAND_R) ? "Arma" : "Armadura") +".";
	mes "Also remember, if the "+ ((getiteminfo(.@item_id,5)&EQP_HAND_R) ? "Arma" : "Armadura") +" has been upgraded, and has been inserted with a card,";
	mes "você vai perdê-las, mesmo se você tiver sucesso na criação de um slot.";
	next;
	switch(select("Perguntar pela Criação de Slots.:Tente da próxima vez.")) {
		case 1:
			if ((Zeny >= .@zeny_req*1000) && countitem(.@item_req[0]) >= .@req_amount[0] && countitem(getarg(7,512)) >= .@req_amount[1] && countitem(.@item_id) > 0) {
				mes "[Leablem]";
				mes "Tudo bem então, vamos começar o trabalho!";
				mes "É melhor rezar por um resultado bem sucedido.";
				next;
				if(getequipid(.@part) != .@item_id){
					mes "você precisa estar com o item equipado.";
					end;
				}
				set .@a, rand(1,100);
				if ((.@a > getarg(2)) && (.@a < getarg(3))) {
					specialeffect EF_LORD;
					mes "[Leablem]";
					mes "Ótimo, parece ter tido sucesso.";
					mes "Parece muito bem feito. Parabéns!";
					// save status item
					setarray .@OptID[0],getequiprandomoption(.@part,0,ROA_ID),getequiprandomoption(.@part,1,ROA_ID),getequiprandomoption(.@part,2,ROA_ID),getequiprandomoption(.@part,3,ROA_ID),getequiprandomoption(.@part,4,ROA_ID);
					setarray .@OptVal[0],getequiprandomoption(.@part,0,ROA_VALUE),getequiprandomoption(.@part,1,ROA_VALUE),getequiprandomoption(.@part,2,ROA_VALUE) ,getequiprandomoption(.@part,3,ROA_VALUE),getequiprandomoption(.@part,4,ROA_VALUE);
					setarray .@OptParam[0],getequiprandomoption(.@part,0,ROA_PARAM),getequiprandomoption(.@part,1,ROA_PARAM),getequiprandomoption(.@part,2,ROA_PARAM),getequiprandomoption(.@part,3,ROA_PARAM),getequiprandomoption(.@part,4,ROA_PARAM);
					.@refine = getequiprefinerycnt(.@part);
					//delitem .@item_id,1;
					delequip .@part;
					delitem .@item_req[0],.@req_amount[0];
					if (.@item_req[1] != 0 && .@req_amount[1] != 0)
						delitem .@item_req[1],.@req_amount[1];
					Zeny = Zeny - .@zeny_req *1000;
					//getitem getarg(1),1;
					if(getarraysize(.@OptID)){
						getitem3 getarg(1),1,1,.@refine,0,0,0,0,0,.@OptID,.@OptVal,.@OptParam;
					}else{
						getitem2 getarg(1),1,1,.@refine,0,0,0,0,0;
					}	
					next;
					mes "[Leablem]";
					mes "Te vejo por aí!";
					close;
				} else {
					specialeffect EF_SUI_EXPLOSION;
					mes "[Leablem]";
					mes "Wah! ...Me desculpe, ele falhou.";
					mes "No entanto, você pode vir denovo.";
					mes "Não fique triste, tente na próxima vez.";
					//delitem .@item_id,1;
					delequip .@part;
					delitem .@item_req[0],.@req_amount[0];
					if (.@item_req[1] != 0 && .@req_amount[1] != 0)
						delitem .@item_req[1],.@req_amount[1];
					Zeny = Zeny - .@zeny_req *1000;
					next;
					mes "[Leablem]";
					mes "Vejo você na próxima vez.";
					close;
				}
			} else {
				mes "[Leablem]";
			mes "Você não trouxe todos os itens necessários!";
			mes "Venha logo com isso, rápido!";
				close;
			}
		case 2:
			mes "[Leablem]";
			mes "Vejo você na próxima vez.";
			close;
	}
}