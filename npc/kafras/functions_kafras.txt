//===== rAthena Script ======================================= 
//= Kafra Functions
//===== By: ================================================== 
//= rAthena Dev Team
//= Darlskies, Darkchild, Syrus22, Lupus, kobra_k88 (2.0)
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 6.8
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: =========================================
//= These functions handle save, storage, cart rental, teleport,
//= and Free Teleport/Warp/Rent Cart options for all Kafra NPCs.
//=
//= Kafra's will need a lot of work. The Welcome Message argument
//= may become obsolete, as most kafras have a slightly differing
//= Welcome message.
//===== Additional Comments: ================================= 
//= 1.1 Now using functions v2.1 Added Cart Rent for Classes: Whitesmith, Professor.
//=     Replaced checkoption(x) into checkcart(0) [Lupus] v2.1b Added Fix Kafra Pass Func [Kobra_k88]
//= 2.2 Final fix of the Kafra Pass Exploit! [Lupus] a -Izlude[4] fix
//= 2.2a Minor changes to function calls.  Using agruments. Added Guild options. [kobra_k88]
//= 2.2b This version uses arrays for the teleport option. Rearranged next statements to make menu transitions smoother. [kobra_k88]
//= 2.3 Removed SAVE from Niflheim. [Lupus]
//= 2.3 removed "fix" by HawkMoon RTFM and check supernovice.txt . There's a SPECIAL Kafra which gives CARTS to SN for a special proce. [Lupus]
//= 2.4 Added Baby Class Support (Baby Novice check) Removed annoying storage feature where u had to close dialog window to be able to use your storage [Lupus]
//= 2.5 Added Louyang official Kafra, fixes some Kafras, not letting you to Save your position [Lupus]
//= 2.6 Reverted Dungeons Kafras (they should offer only Storage). Added temp Ayothaya Kafra
//= 2.7 Added correct Ayothaya, Louyang & Amatsu Kafras. [Lupus]
//= 2.8 Fixed Amatsu Storage problems [Lupus] 2.9 Fixed spelling mistakes. [Nexon]
//= 3.0 Added special "not working teleport menu" for Einbroch Kafras [Lupus]
//= 3.1 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 3.2 Fixed an exploit [Lupus]
//= 4.0 Added Kafra storage password protection. [Lupus]
//=     to block Kafra Password, read comments at 350 line
//= 5.0 Fixed the close2;....close; end; lines. Who let them slip away? =/ [erKURITA]
//=     Also, the kafra upon warp was giving back the zeny. Removed. [erKURITA]
//= 5.1 Optimized a little, added no tele/save arg's [Evera]
//= 5.1a Temporary? Added F_ClearGarbage to clear unused/outdated variables [Lupus]
//= 5.2 By default commented out custom Guilds Kafra's "Guild Storage". [Lupus]
//= 5.3 uncommented Guild Storage. Confirmed kRO feature. [Lupus]
//= 5.4 added -Guide option (Kafra shows you nearest Guide) Work in progress. 
//=     Need all coords for all guides.. and somehow pass their coords to kafra.
//=     Removed universal Kafra Pass, added 3 new Kafra Tickets [Lupus]
//= 5.4b temporary moved "-Guide" from the 1st menu punct [Lupus]
//= 5.5 Added proper Niflheim welcome message.	[L0ne_W0lf]
//=     Cleaned up the menus a bit. Got rid of the proceeding "-" prefix.
//= 5.5b Missed a preceeding hyphen, which was screwing up teleporting. [L0ne_W0lf]
//= 5.6 Further dialog updates, added  dynamic costs for cart and storage use. [L0ne_W0lf]
//=     Updated some functions to handle Guild Kafras the way they should. 
//= 5.6a Corrected a few wrong names in the teleport function. [L0ne_W0lf]
//= 5.7 Correced end message no longer displaying, and the duplicate names [L0ne_W0lf]
//=     Thanks to Barron-Monster for pointing out the issues.
//= 5.8 Fixed another double name being shown when "saving" [L0ne_W0lf]
//= 5.9 Fixed the Kafra Welcome message for guilds. Thanks Barron-Monster. [L0ne_W0lf]
//= 6.0 Closed Kafra Password exploit in Cool Event Corp. Storages until we got a client/packet based password support.
//=     Added 2 args into F_SetKafCode to fit it in Cool Evnt Co. NPC [Lupus]
//= 6.1 Added menu for Turbo Track Kafra Staff. [L0ne_W0lf]
//= 6.2 Updated/Fixed warp cords. [Kisuka]
//= 6.3 #kafra_code is now stored as is. [brianluau]
//= 6.4 Removed unofficial Guide function. [Daegaladh]
//= 6.5 Optimized. [Joseph]
//= 6.6 Added Warp Points / View Points Function. [Joseph]
//= 6.7 Added Rune Knight Kafra. [Euphy]
//= 6.8 Updated some functions and added VIP features. [Euphy]
//============================================================ 

//=== Main Function ========================================================
//=   arg(0): Used to determine which welcome message to show.
//=   arg(1): Used to determine which menu to display.
//=   arg(2): Used to determine if the info menu is shown in F_KafInfo.
//=   arg(3): Cost to use Storage
//=   arg(4): Cost to Rent a Pushcart
//==========================================================================
function	script	F_Kafra	{
	callfunc "F_ClearGarbage"; // Clear outdated, unused variables
	.@welcome = getarg(0);
	.@menu_num = getarg(1);

	// Display Kafra Welcome Message
	switch(.@welcome){
		default:
		case 0:
			mes "^339966[ Funcionária Kafra ]^000000";
			mes (gettime(3)>= 6&&gettime(3)<= 12?"Bom-dia":(gettime(3)>=13&&gettime(3)<=18?"Boa-tarde":"Boa-noite"))+", ^008aff"+strcharinfo(0)+"^000000 !";
			mes "Bem vindo à Corporação Kafra. Os serviços da Corporação Kafra sempre estarão ao seu lado. Como posso lhe ajudar ?";
			break;
		// Niflheim mensagem específica
		case 1:
			mes "^339966[ Funcionária Kafra ]^000000";
			mes (gettime(3)>= 6&&gettime(3)<= 12?"Bom-dia":(gettime(3)>=13&&gettime(3)<=18?"Boa-tarde":"Boa-noite"))+", ^008aff"+strcharinfo(0)+"^000000 !";
			mes "^666666B-Be-Beeeemmm V-vindooo";
			mes "a c-cooorpoor-r-raaçãaaoo";
			mes "K-k-Kafraaaa...^000000";
			break;
		case 2:
			// Guild Castle Kafra message
			@GID = getcharid(2);
			mes "^339966[ Funcionária Kafra ]^000000";
			mes (gettime(3)>= 6&&gettime(3)<= 12?"Bom-dia":(gettime(3)>=13&&gettime(3)<=18?"Boa-tarde":"Boa-noite"))+", ^008aff"+strcharinfo(0)+"^000000 !";
			mes "Vejo que você é membro do Clã ^ff0000" + getguildname(@GID) + "^000000 .";
			mes "A Corporação Kafra, está aqui para atender você.";
			break;
		case 3:
			// Amatsu mensagem específica
			mes "^339966[ Funcionária Kafra ]^000000";
			mes (gettime(3)>= 6&&gettime(3)<= 12?"Bom-dia":(gettime(3)>=13&&gettime(3)<=18?"Boa-tarde":"Boa-noite"))+", ^008aff"+strcharinfo(0)+"^000000 !";
			mes "Então, você vem de uma terra distante para estudar nossa cultura, ou você só está observando?";
			mes "Em ambos os casos, porque não permanecer por algum tempo?";
			mes "Com a eterna fragância";
			mes "do aroma de flores silvestres.";
			break;		
		case 4:
			// Louyang e Ayothaya mensagem específica
			mes "^339966[ Funcionária Kafra ]^000000";
			mes (gettime(3)>= 6&&gettime(3)<= 12?"Bom-dia":(gettime(3)>=13&&gettime(3)<=18?"Boa-tarde":"Boa-noite"))+", ^008aff"+strcharinfo(0)+"^000000 !";
			mes "Com tantas Kafras";
			mes "espalhadas, você nunca";
			mes "estará longe de casa.";
			break;	
		case 5:
			// NPC has it's own welcome message. (Display nothing)
			break;
	}
	next;
	while (1) {
		deletearray @K_Menu0$[0],getarraysize(@K_Menu0$);
		if (.@welcome == 2) {
			// Guild Kafra Menu override (free Teleport, free Storage, Free Cart)
			setarray @K_Menu0$[0],"^3CB371[»]^000000 Usar Armazém","^3CB371[»]^000000 Usar Armazém da Guilda","^ff0000[»]^000000 Cancelar";
		} else {
			switch(.@menu_num){
				// Save and Storage only
				case 1:	setarray @K_Menu0$[0],"^3CB371[»]^000000 Salvar","^3CB371[»]^000000 Usar Armazém","^ff0000[»]^000000 Cancelar"; break;
				// Usar Armazém.
				case 2:	setarray @K_Menu0$[0],"^3CB371[»]^000000 Usar Armazém","^ff0000[»]^000000 Cancelar"; break;
				// Opção de Salvar, Usar Armazém, Alugar Carrinho.
				case 3: setarray @K_Menu0$[0],"Salvar","^3CB371[»]^000000 Usar Armazém","^3CB371[»]^000000 Checar outra informação","^ff0000[»]^000000 Cancelar"; break;
				// Usar Armazém, Alugar Carrinho, Checar outra informação.
				case 5:	setarray @K_Menu0$[0],"^3CB371[»]^000000 Usar Armazém","^3CB371[»]^000000 Checar outra informação","^ff0000[»]^000000 Cancelar"; break;
				// Usar Armazém e Checar outra informação.
				case 6:	setarray @K_Menu0$[0],"^3CB371[»]^000000 Usar Armazém","^3CB371[»]^000000 Checar outra informação","^ff0000[»]^000000 Cancelar";	break;
				// Opção de Salvar, Usar Armazém, Alugar Carrinho
				case 7:	setarray @K_Menu0$[0],"^3CB371[»]^000000 Salvar","^3CB371[»]^000000 Usar Armazém","^ff0000[»]^000000 Cancelar"; break;
				// Opção de Salvar, Usar Armazém, Checar outra informação
				case 8: setarray @K_Menu0$[0],"^3CB371[»]^000000 Salvar","^3CB371[»]^000000 Usar Armazém","^3CB371[»]^000000 Checar outra informação","^ff0000[»]^000000 Cancelar"; break;
				// No Save (Rune Knight)
				case 9: setarray @K_Menu0$[0],"^3CB371[»]^000000 Usar Armazém","^3CB371[»]^000000 Checar outra informação","^ff0000[»]^000000 Cancelar"; break;
				// Usar Armazém, Usar Armazém, e Alugar Carrinho.
				case 10: setarray @K_Menu0$[0],"^3CB371[»]^000000 Usar Armazém","^3CB371[»]^000000 Salvar","^ff0000[»]^000000 Cancelar"; break;
				// Mensagem Padrão.
				default: setarray @K_Menu0$[0],"^3CB371[»]^000000 Salvar","^3CB371[»]^000000 Usar Armazém","^3CB371[»]^000000 Checar outra informação","^ff0000[»]^000000 Cancelar"; break;
			}
		}
		set .@menu$,"";
		for (set .@i, 0; .@i < getarraysize(@K_Menu0$); set .@i, .@i + 1)
			set .@menu$, .@menu$ + @K_Menu0$[.@i] + ":";	
		set .@j, select (.@menu$) - 1;
		if (@K_Menu0$[.@j] == "^3CB371[»]^000000 Salvar") {
			return;
		} else if (@K_Menu0$[.@j] == "^3CB371[»]^000000 Usar Armazém") {
			// Do not charge for Guild Storage
			if(.@welcome == 2) 
				callfunc "F_KafStor",2,0,0;
			else 
				callfunc "F_KafStor",0,getarg(3),.@welcome;
			next;
		} else if (@K_Menu0$[.@j] == "^3CB371[»]^000000 Usar Serviço de Teleporte") {
			// Display Einbroch "No Teleport Service" notice.
			if(.@menu_num != 4) callfunc "F_Teleporter",.@welcome;
			else {
				mes "^339966[ Funcionária Kafra ]^000000";
				mes "Por causa do ^FF0000Acordo de limite";
				mes "de transporte^000000, a";
				mes "Corporação Kafra não pode";
				mes "fornecer serviços de teletransporte";
				mes "aqui na república de Schwartzwald.";
				next;
				mes "^339966[ Funcionária Kafra ]^000000";
				mes "Nós solicitamos a você utilizar";
				mes "o serviço de Aeroplano no lugar";
				mes "do teletransporte. Muito obrigado por sua";
				mes "compreensão e cooperação.";
			}
			next;
		} else if (@K_Menu0$[.@j] == "^3CB371[»]^000000 Checar outra informação") {
			callfunc "F_KafInfo",getarg(2);
			next;
		} else if (@K_Menu0$[.@j] == "^ff0000[»]^000000 Cancelar"){
			callfunc "F_KafEnd",.@welcome,0;
			end;
		} else if (@K_Menu0$[.@j] == "^3CB371[»]^000000 Usar Armazém da Guilda") {
			callfunc "F_KafStor",1,0;
			next;
		}
	}
}

//===================================================
// Storage Function
//===================================================
function	script	F_KafStor	{
	.@type = getarg(0);
	.@fee = getarg(1);

	// Unable to access Guild Storage (Busy)
	if(.@type == 1){
		if(guildopenstorage()){
			mes "^339966[ Funcionária Kafra ]^000000";
			mes "Sinto muito, mas outro membro do Clã está usando o armazém neste instante.";
			mes "Por favor, aguarde até que esta pessoa termine.";
			close2;
			cutin "", 255;
			end;
		}
		cutin "", 255;
		close;
	}
	// Unable to access Normal Storage (Insufficient Basic Skills)
	if(!callfunc("F_CanOpenStorage")){
		mes "^339966[ Funcionária Kafra ]^000000";
		// Niflheim Specific Message
		if (getarg(2) == 1) {
			mes "^666666S-D-ddeessssculllpppe,";
			mes "v-vo-voooccêe éee umm";
			mes "Appreenddizz... P-Prreeecisa teerrr";
			mes "nívveelll d-de hh-habibilidaddeee level 6...^000000";
			return;
		}
		mes "Lamento, mas você";
		mes "precisa ser um Aprendiz com ";
		mes "nível mínimo de habilidade";
		mes "level 6 para usar o armazém.";
		return;
	}
	
	
	// Accessing Normal Storage (Skipped if accessing Storage from Guild castle)
	if(.@type != 2){
		// Consume "Free Ticket for Kafra Storage" if available.
		if(countitem(7059)) delitem 7059,1;
		else {
			if(Zeny<.@fee){
				mes "^339966[ Funcionária Kafra ]^000000";
				// Niflheim Specific Message
				if (getarg(2) == 1) {
					percentheal -50,-50;
					mes "^666666Zeeeeeny...";
					mes "P-Preeciso de Z-Zeny...!";
					mes "P-Preciiiso de 150... Zenys...";
					mes "Ergh!!! T-tomando e-energia!";
					return;
				}
				// Standard Message
				mes "Lamento, mas você não possui";
				mes "Zenys suficientes para usar";
				mes "o serviço de armazém.";
				mes "Para acessar o armazém o custo é: "+.@fee+" Zenys.";
				return;
			}
			set Zeny, Zeny-.@fee;
			set RESRVPTS, RESRVPTS + (.@fee/5);
		}
	}

	// Surpress the "End" message if in guild castle.
	if (.@type != 2) {
		// Niflheim specific Message
		if (getarg(2) == 1) {
			percentheal 0,-10;
			mes "^339966[ Funcionária Kafra ]^000000";
			for (set .@i, 0; .@i < 5; set .@i, .@i + 1)
				mes "^666666Obrigado.. por... usar... os nossos serviços...^000000";
		}
		// Normal message
		else {
			mes "^339966[ Funcionária Kafra ]^000000";
			mes "Aqui, deixe-me abrir";
			mes "seu Armazém para você.";
			mes "Obrigado por usar";
			mes "o Serviço Kafra.";
		}
	}
	callfunc("F_CheckKafCode");	//check your storage password, if set
	close2;
	openstorage;
	cutin "", 255;
	end;
}

//===================================================
// Teleport Function
//===================================================
function	script	F_KafTele	{
	mes "^339966[ Funcionária Kafra ]^000000";
	mes "Por favor, escolha seu destino.";
	next;
	set .@menu$, "";
	for (set .@i, 0; .@i <= 6; set .@i, .@i + 1)
		set .@menu$, .@menu$ + @wrpC$[.@i] + ":";
	set .@j, select (.@menu$) - 1;
	if (@wrpC$[.@j] == "Cancelar") return;
	// Consume "Free Ticket for Kafra Transportation" if available.
	// Do not consume if in Guild castle
	if(countitem(7060) > 0 && getarg(0) != 2) delitem 7060,1;
	else {
		if (Zeny<@wrpP[.@j]){
			mes "^339966[ Funcionária Kafra ]^000000";
			mes "Sinto muito, mas você não possui";
			mes "Zenys para teleportar.";
			mes "O serviço de teleporte ";
			mes "para "+@wrpD$[.@j]+" é de "+@wrpP[.@j]+" zenys.";
			close2;
			cutin "", 255;
			end;
		}
		set Zeny, Zeny - @wrpP[.@j];
		set RESRVPTS, RESRVPTS + (@wrpP[.@j]/16);
	}
	cutin "", 255;
	if (@wrpD$[.@j] == "Al De Baran") warp "aldebaran",168,112;
	else if (@wrpD$[.@j] == "Alberta") warp "alberta", 117, 56;
	else if (@wrpD$[.@j] == "Comodo") warp "comodo", 209, 143;
	else if (@wrpD$[.@j] == "Comodo Ilha do Farol") warp "cmd_fild07", 127, 134;
	else if (@wrpD$[.@j] == "Geffen") warp "geffen", 120, 39;
	else if (@wrpD$[.@j] == "Izlude") { if (checkre(0)) warp "izlude", 128, 98; else warp "izlude", 91, 105; }
	else if (@wrpD$[.@j] == "Juno") warp "yuno", 158, 125;
	else if (@wrpD$[.@j] == "Mina Abandonada de Mjolnir") warp "mjolnir_02", 99, 351;
	else if (@wrpD$[.@j] == "Morroc") warp "morocc", 156, 46;
	else if (@wrpD$[.@j] == "Caverna dos Orcs") warp "gef_fild10", 52, 326;
	else if (@wrpD$[.@j] == "Payon") warp "payon", 161, 58;
	else if (@wrpD$[.@j] == "Prontera") warp "prontera", 116, 72;
	else if (@wrpD$[.@j] == "Umbala") warp "umbala", 100, 154;
	end;
}


//===================================================
// Cart Function
//===================================================
function	script	F_KafCart	{
	// Ensure that the class wanting to rent a pushcart is a merchant
	if(BaseClass != Job_Merchant){
		mes "^339966[ Funcionária Kafra ]^000000";
		mes "Eu lamento, mas o serviço de aluguel de carrinho...";
		if (checkre(0))
			mes "Está disponível somente para as classes de Mercadores e Comerciantes.";
		else {
			mes "Está apenas disponível para clases e suas evoluções de Mercadores e Comerciantes,";
		}
		return 1;
	}
	// Make sure the invoking character does not have a cart already
	else if(checkcart() == 1){
		mes "^339966[ Funcionária Kafra ]^000000";
		mes "Você já possui";
		mes "o Carrinho equipado.";
		return 1;
	}
	// Check if the player has the Pushcart skill
	else if(getskilllv("MC_PUSHCART") == 0) {
		mes "^339966[ Funcionária Kafra ]^000000";
		mes "Você precisa da habilidade \"Usar Carrinho\".";
		return 1;
	}
	// Consume "Free Ticket for the Cart Service" if available.
	// Do not consume if in Guild castle.
	if(countitem(7061) > 0 && getarg(0) != 2) delitem 7061,1;
	else {
		mes "^339966[ Funcionária Kafra ]^000000";
		mes "O aluguel do carrinho de mão";
		mes "custará "+getarg(1)+" Zenys.";
		mes "Você deseja alugar um carrinho de mão?";;
		next;
		if (select("Alugar Carrinho.:Cancel") == 2)
			if(Zeny<getarg(1)){
				mes "^339966[ Funcionária Kafra ]^000000";
				mes "Lamento, mas você não possui Zenys suficientes para alugar um carrinho.";
				mes "o aluguel custa "+getarg(1)+" Zenys.";
				return 1;
			}
			set Zeny,Zeny-getarg(1);
			set RESRVPTS, RESRVPTS + 48;
	}
	setcart;
	return 1;
}

//===================================================
// Special Reserve Points Function
//===================================================
function	script	F_KafInfo	{
	// Uncomment next line to block Kafra Storage Protection
	//	set .@block, 1;
		setarray .@m$, "^3CB371[»]^000000 Checar reserva de pontos especiais.","^3CB371[»]^000000 Senha do Armazém","^3CB371[»]^000000 Localização das Funcionárias","^ff0000[»]^000000 Cancelar";
		if (getarg(0) == 2) deletearray .@m$[1],2; // Port Malaya, Rune Knight
		else if (.@block) deletearray .@m$[1],1;
	while (1) {
		set .@j, select(implode(.@m$,":")) - 1;
		if (.@m$[.@j] == "^3CB371[»]^000000 Checar reserva de pontos especiais.") {
			mes "^339966[ Funcionária Kafra ]^000000";
			mes "Vamos ver...";
			mes strcharinfo(0) + "...";
			mes "Ah, você tem um total de ";
			mes RESRVPTS+ " reservas de pontos especiais.";
			next;
			mes "^339966[ Funcionária Kafra ]^000000";
			mes "Você pode trocar suas";
			mes "Reservas de pontos especiais por";
			mes "recompensas no escritório da Corporação Kafra em Aldebaran. Por favor,";
			mes "utilize nossos serviços convenientes receber os benefícios do nosso programa de recompensas.";
			next;
			if(getarg(0) == 1) return;
		} else if (.@m$[.@j] == "^3CB371[»]^000000 Localização das Funcionárias") {
			for (set .@i, 0; .@i <= 3; set .@i, .@i + 1)
				viewpoint 1,@viewpX[.@i],@viewpY[.@i],(.@i+1),0xFF00FF;
			next;
			for (set .@i, 0; .@i <= 3; set .@i, .@i + 1)
				viewpoint 2,@viewpX[.@i],@viewpY[.@i],(.@i+1),0xFF00FF;
		} else if (.@m$[.@j] == "^3CB371[»]^000000 Senha do Armazém") {
			callfunc("F_SetKafCode","[Funcionária Kafra]","Serviços Kafra");
		} else {
			return;
		}
	}
}

// === End Function ==================================================
//  arg(0): used to determine what message to display.
//  arg(1): used to determine if save message is displayed.
//  arg(2): used to display the name of the area you're saving in.
//===================================================================
function	script	F_KafEnd	{
	mes "^339966[ Funcionária Kafra ]^000000";
	if(getarg(1)==1) {	
		// Save
		mes "Seu ponto de renascimento";
		mes "foi salvo aqui.";
		mes getarg(2)+".";
		mes "Muito obrigado por usar";
		mes "os serviços da Corporação Kafra.";
	} else if(getarg(0)==0 || getarg(0)==5) {	
		// Generic End
		mes "Nós da Corporação Kafra,";
		mes "sempre estamos nos esforçando para proporcionoar os melhores serviços. Esperamos satisfazer as suas necessidades de aventureiro nos seus padrões de excelência.";
	} else if(getarg(0)==1) {
		// Niflheim End
		mes "^666666Kaffffra n-nunca";
		mes "mooooorrrrre. Semprre...";
		mes "Es-ta  ao seeeuuu laaadooo~^000000";
	}
	close2;
	cutin "", 255;
	end;
}

//===================================================
// Check Storage Password Function
//===================================================
function	script	F_CheckKafCode	{
	if(!#kafra_code) return;
	mes "Digite a senha do armazém:";
	set @code_,0;
	input @code_;
	if(@code_ != #kafra_code) {
		dispbottom "Senha errada.";
		close2;
		cutin "",255;
		end;
	}
	set @kafcode_try,0;
	set @code_,0;
	return;
}

// === Set / Change / Clear Storage Password Function ===
// getarg(0): NPC Name
// getarg(1): Company Name
//=======================================================
function	script	F_SetKafCode	{
	.@npc_name$ = getarg(0);
	.@comp_name$ = getarg(1);

	mes .@npc_name$;
	if(#kafra_code) {
		mes "Seu armazém está protegido com uma senha. O que você fará agora?";
		next;
		switch (select("^3CB371[»]^000000 Mudar senha antiga -> 5000z:^3CB371[»]^000000 Remover senha do Armazém -> 1000z:^ff0000[»]^000000 Cancelar")) {
			case 1:
				mes .@npc_name$;
				mes "Primeiro, digite sua ^0000FFsenha antiga^000000.";
				set @code,callfunc("F_EntKafCode");
				if(!@code || @code != #kafra_code) {
					mes "Senha errada. Você não pode inserir uma nova senha.";
					break;
				}
				next;
				callsub S_SET,.@npc_name$,.@comp_name$;
				break;
			case 2:
				mes .@npc_name$;
				mes "Por favor, entre com a senha antes de sua retirada.";
				set @code,callfunc("F_EntKafCode");
				if(!@code) {
					mes "A senha não foi removida.";
					break;
				}
				next;
				mes .@npc_name$;
				if(Zeny < 1000) {
					mes "Você não possui Zenys o suficiente.";
					break;
				}
				set Zeny,Zeny-1000;
				if(@code == #kafra_code) {
					set #kafra_code,0;
					mes "Você teve êxito ao colocar senha em seu armazém.";
					mes "Muito obrigado por usar os  "+.@comp_name$+".";
				} else {
					mes "Senha errada! Não devolverei seus 1000 Zenys.";
					mes "Por favor, na próxima entre com a senha correta.";
				}
				break;
			case 3:
				break;
		}
	} else {
		mes .@comp_name$+" orgulhosamente apresenta um novo serviço:";
		mes "Proteção adicional do seu armazém com senha.";
		next;
		switch (select("^3CB371[»]^000000 Definir nova senha -> 5000z:^ff0000[»]^000000 Cancelar")) {
			case 1: callsub S_SET,.@npc_name$,.@comp_name$; break;
			case 2: break;		
		}
	}
	close2;
	cutin "",255;
	end;

S_SET:
	mes .@npc_name$;
	mes "Agora entre com sua ^FF0000nova senha^000000 para proteger seu armazém de roubos.";
	set @code,callfunc("F_EntKafCode");
	if(!@code) {
		mes "A senha não foi trocada.";
		return;
	}
	next;
	mes .@npc_name$;
	if(Zeny < 5000) {
		mes "Você não possui Zenys o suficiente.";
		return;
	}
	set Zeny,Zeny-5000;
	set #kafra_code,@code;
	mes "Seu armazém está protegido com uma senha secreta.";
	mes "Obrigado por usar "+.@comp_name$+".";
	return;
}

//===================================================
// Basic Password Validation Function
//===================================================
function	script	F_EntKafCode	{
	mes "Digite um número entre 1000~10000000:";
	set @code_,0;
	set @kafcode_try,@kafcode_try+1;
	if(@kafcode_try>10) {
		set @kafcode_try,0;
		logmes "Hack: Tentou colocar senha de armazenamento.";
	}
	if(input(@code_) == 1) {
		mes "Você não pode usar uma senha tão grande.";
		return 0;
	}
	if(@code_<1000) {
		mes "Você não pode usar uma senha tão curta.";
		return 0;
	}
	return @code_;
}

//===================================================
// Warp Points / View Points Function
//===================================================
function	script	F_KafSet	{
	cleararray @wrpC$[0],"",getarraysize(@wrpC$);
	cleararray @wrpD$[0],"",getarraysize(@wrpD$);
	cleararray @wrpP$[0],"",getarraysize(@wrpP$);
	cleararray @viewpX[0],"",getarraysize(@viewpX);
	cleararray @viewpY[0],"",getarraysize(@viewpY);
	if (strnpcinfo(4) == "prontera") {
		setarray @wrpD$[0], "Izlude", "Geffen", "Payon", "Morroc", "Caverna dos Orcs", "Alberta";
		setarray @wrpP[0], 600, 1200, 1200, 1200, 1700, 1800;
		setarray @viewpX[0], 151, 29, 282, 152;
		setarray @viewpY[0], 29, 207, 200, 326;
	} else if (strnpcinfo(4) == "alberta") {
		setarray @wrpP[0], 1200, 1800, 1800;
		setarray @wrpD$[0], "Payon", "Morroc", "Prontera";
		setarray @viewpX[0], 28, 113, 0, 0;
		setarray @viewpY[0], 229, 60, 0, 0;
	} else if (strnpcinfo(4) == "aldebaran") {
		setarray @wrpP[0], 1200, 1200, 1800, 1700;
		setarray @wrpD$[0], "Geffen", "Juno" , "Izlude", "Mina Abandonada de Mjolnir";
	} else if (strnpcinfo(4) == "comodo" ) {
		setarray @wrpD$[0], "Morroc", "Comodo Ilha do Farol", "Umbala";
		setarray @wrpP[0], 1800, 1200, 1800;
	} else if (strnpcinfo(4) == "cmd_fild07") {
		setarray @wrpD$[0], "Comodo", "Morroc";
		setarray @wrpP[0], 1200, 1200;
	} else if (strnpcinfo(4) == "geffen") {
		setarray @wrpD$[0], "Prontera", "Al De Baran", "Caverna dos Orcs", "Mina Abandonada de Mjolnir";
		setarray @wrpP[0], 1200, 1200, 1700, 1700;
		setarray @viewpX[0], 120, 203;
		setarray @viewpY[0], 62, 123;
	} else if (strnpcinfo(4) == "izlude") {
		setarray @wrpD$[0], "Geffen", "Payon", "Morroc", "Al De Baran";
		setarray @wrpP[0], 1200, 1200, 1200, 1800;
	} else if (strnpcinfo(4) == "morocc") {
		setarray @wrpD$[0], "Prontera", "Payon", "Alberta", "Comodo", "Comodo Ilha do Farol";
		setarray @wrpP[0], 1200, 1200, 1800, 1800, 1200;
		setarray @viewpX[0], 156, 163, 28, 292;
		setarray @viewpY[0], 97, 260, 167, 211;
	} else if (strnpcinfo(4) == "umbala") {
		setarray @wrpD$[0], "Comodo";
		setarray @wrpP[0], 1800;
	} else if (strnpcinfo(4) == "payon") {
		setarray @wrpD$[0], "Prontera", "Alberta", "Morroc";
		setarray @wrpP[0], 1200, 1200, 1200;
	} else if (strnpcinfo(4) == "yuno") {
		setarray @wrpD$[0], "Al De Baran";
		setarray @wrpP[0], 1200;
		setarray @viewpX[0], 328, 278, 153, 0;
		setarray @viewpY[0], 108, 221, 187, 0;
	} else if (strnpcinfo(4) == "job3_rune01") {
		setarray @wrpD$[0], "Izlude", "Geffen", "Payon", "Morroc", "Alberta";
		setarray @wrpP[0], 600, 1200, 1200, 1200, 1800;
	}

	// VIP pricing varies across the regional servers; this is most practical.
	if (VIP_SCRIPT && !vip_status(1)) {
		for (set .@i, 0; .@i < getarraysize(@wrpD$); set .@i, .@i + 1)
			setd "@wrpP["+.@i+"]",getd("@wrpP["+.@i+"]") * 2;
	}

	for (set .@i, 0; .@i < getarraysize(@wrpD$); set .@i, .@i + 1)
		setd "@wrpC$["+.@i+"]",getd("@wrpD$["+.@i+"]")+" -> "+getd("@wrpP["+.@i+"]")+" z";
	setd "@wrpC$["+getarraysize(@wrpC$)+"]","Cancelar";
	return;
	
	

}
